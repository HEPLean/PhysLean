/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.BeyondTheStandardModel.TwoHDM.GramMatrix
/-!

# The potential of the two Higgs doublet model

## i. Overview

In this file we give the potential of the two Higgs doublet model (2HDM) in Lean, and derive
properties thereof.

-/
namespace TwoHiggsDoublet
open InnerProductSpace
open StandardModel

/-- The parameters of the Two Higgs doublet model potential.
  Following the convention of https://arxiv.org/pdf/1605.03237. -/
structure PotentialParameters where
  /-- The parameter corresponding to `mâ‚â‚Â²` in the 2HDM potential. -/
  mâ‚â‚2 : â„
  /-- The parameter corresponding to `mâ‚‚â‚‚Â²` in the 2HDM potential. -/
  mâ‚‚â‚‚2 : â„
  /-- The parameter corresponding to `mâ‚â‚‚Â²` in the 2HDM potential. -/
  mâ‚â‚‚2 : â„‚
  /-- The parameter corresponding to `Î»â‚` in the 2HDM potential. -/
  ğ“µâ‚ : â„
  /-- The parameter corresponding to `Î»â‚‚` in the 2HDM potential. -/
  ğ“µâ‚‚ : â„
  /-- The parameter corresponding to `Î»â‚ƒ` in the 2HDM potential. -/
  ğ“µâ‚ƒ : â„
  /-- The parameter corresponding to `Î»â‚„` in the 2HDM potential. -/
  ğ“µâ‚„ : â„
  /-- The parameter corresponding to `Î»â‚…` in the 2HDM potential. -/
  ğ“µâ‚… : â„‚
  /-- The parameter corresponding to `Î»â‚†` in the 2HDM potential. -/
  ğ“µâ‚† : â„‚
  /-- The parameter corresponding to `Î»â‚‡` in the 2HDM potential. -/
  ğ“µâ‚‡ : â„‚

namespace PotentialParameters

/-!

## The zero potential parameters

-/

instance : Zero PotentialParameters where
  zero :=
    { mâ‚â‚2 := 0
      mâ‚‚â‚‚2 := 0
      mâ‚â‚‚2 := 0
      ğ“µâ‚ := 0
      ğ“µâ‚‚ := 0
      ğ“µâ‚ƒ := 0
      ğ“µâ‚„ := 0
      ğ“µâ‚… := 0
      ğ“µâ‚† := 0
      ğ“µâ‚‡ := 0 }

@[simp] lemma zero_mâ‚â‚2 : (0 : PotentialParameters).mâ‚â‚2 = 0 := rfl

@[simp] lemma zero_mâ‚‚â‚‚2 : (0 : PotentialParameters).mâ‚‚â‚‚2 = 0 := rfl

@[simp] lemma zero_mâ‚â‚‚2 : (0 : PotentialParameters).mâ‚â‚‚2 = 0 := rfl

@[simp] lemma zero_ğ“µâ‚ : (0 : PotentialParameters).ğ“µâ‚ = 0 := rfl

@[simp] lemma zero_ğ“µâ‚‚ : (0 : PotentialParameters).ğ“µâ‚‚ = 0 := rfl

@[simp] lemma zero_ğ“µâ‚ƒ : (0 : PotentialParameters).ğ“µâ‚ƒ = 0 := rfl

@[simp] lemma zero_ğ“µâ‚„ : (0 : PotentialParameters).ğ“µâ‚„ = 0 := rfl

@[simp] lemma zero_ğ“µâ‚… : (0 : PotentialParameters).ğ“µâ‚… = 0 := rfl

@[simp] lemma zero_ğ“µâ‚† : (0 : PotentialParameters).ğ“µâ‚† = 0 := rfl

@[simp] lemma zero_ğ“µâ‚‡ : (0 : PotentialParameters).ğ“µâ‚‡ = 0 := rfl

/-- A reparameterization of the parameters of the quadratic terms of the
  potential for use with the gramVector. -/
noncomputable def Î¾ (P : PotentialParameters) : Fin 1 âŠ• Fin 3 â†’ â„ := fun Î¼ =>
  match Î¼ with
  | Sum.inl 0 => (P.mâ‚â‚2 + P.mâ‚‚â‚‚2) / 2
  | Sum.inr 0 => -Complex.re P.mâ‚â‚‚2
  | Sum.inr 1 => Complex.im P.mâ‚â‚‚2
  | Sum.inr 2 => (P.mâ‚â‚2 - P.mâ‚‚â‚‚2) / 2

/-- A reparameterization of the parameters of the quartic terms of the
  potential for use with the gramVector. -/
noncomputable def Î· (P : PotentialParameters) : Fin 1 âŠ• Fin 3 â†’ Fin 1 âŠ• Fin 3 â†’ â„
  | Sum.inl 0, Sum.inl 0 => (P.ğ“µâ‚ + P.ğ“µâ‚‚ + 2 * P.ğ“µâ‚ƒ) / 8
  | Sum.inl 0, Sum.inr 0 => (P.ğ“µâ‚†.re + P.ğ“µâ‚‡.re) * (1 / 4)
  | Sum.inl 0, Sum.inr 1 => (P.ğ“µâ‚†.im + P.ğ“µâ‚‡.im) * (-1 / 4)
  | Sum.inl 0, Sum.inr 2 => (P.ğ“µâ‚ - P.ğ“µâ‚‚) * (1 / 8)
  | Sum.inr 0, Sum.inl 0 => (P.ğ“µâ‚†.re + P.ğ“µâ‚‡.re) * (1 / 4)
  | Sum.inr 1, Sum.inl 0 => (P.ğ“µâ‚†.im + P.ğ“µâ‚‡.im) * (-1 / 4)
  | Sum.inr 2, Sum.inl 0 => (P.ğ“µâ‚ - P.ğ“µâ‚‚) * (1 / 8)
  /-Î·_a_a-/
  | Sum.inr 0, Sum.inr 0 => (P.ğ“µâ‚….re + P.ğ“µâ‚„) * (1 / 4)
  | Sum.inr 1, Sum.inr 1 => (-P.ğ“µâ‚….re + P.ğ“µâ‚„) * (1 / 4)
  | Sum.inr 2, Sum.inr 2 => (P.ğ“µâ‚ + P.ğ“µâ‚‚ - 2 * P.ğ“µâ‚ƒ) * (1 / 8)
  | Sum.inr 0, Sum.inr 1 => P.ğ“µâ‚….im * (-1 / 4)
  | Sum.inr 2, Sum.inr 0 => (P.ğ“µâ‚†.re - P.ğ“µâ‚‡.re) * (1 / 4)
  | Sum.inr 2, Sum.inr 1 => (P.ğ“µâ‚‡.im - P.ğ“µâ‚†.im) * (1 / 4)
  | Sum.inr 1, Sum.inr 0 => P.ğ“µâ‚….im * (-1 / 4)
  | Sum.inr 0, Sum.inr 2 => (P.ğ“µâ‚†.re - P.ğ“µâ‚‡.re) * (1 / 4)
  | Sum.inr 1, Sum.inr 2 => (P.ğ“µâ‚‡.im - P.ğ“µâ‚†.im) * (1 / 4)

lemma Î·_symm (P : PotentialParameters) (Î¼ Î½ : Fin 1 âŠ• Fin 3) :
    P.Î· Î¼ Î½ = P.Î· Î½ Î¼ := by
  fin_cases Î¼ <;> fin_cases Î½ <;> simp [Î·]

end PotentialParameters

open ComplexConjugate

/-- The mass term of the two Higgs doublet model potential. -/
noncomputable def massTerm (P : PotentialParameters) (H : TwoHiggsDoublet) : â„ :=
  P.mâ‚â‚2 * â€–H.Î¦1â€– ^ 2 + P.mâ‚‚â‚‚2 * â€–H.Î¦2â€– ^ 2 -
  (P.mâ‚â‚‚2 * âŸªH.Î¦1, H.Î¦2âŸ«_â„‚ + conj P.mâ‚â‚‚2 * âŸªH.Î¦2, H.Î¦1âŸ«_â„‚).re

lemma massTerm_eq_gramVector (P : PotentialParameters) (H : TwoHiggsDoublet) :
    massTerm P H = âˆ‘ Î¼, P.Î¾ Î¼ * H.gramVector Î¼ := by
  simp [massTerm, Fin.sum_univ_three, PotentialParameters.Î¾, normSq_Î¦1_eq_gramVector,
    normSq_Î¦2_eq_gramVector, Î¦1_inner_Î¦2_eq_gramVector, Î¦2_inner_Î¦1_eq_gramVector]
  ring

@[simp]
lemma gaugeGroupI_smul_massTerm (g : StandardModel.GaugeGroupI) (P : PotentialParameters)
    (H : TwoHiggsDoublet) :
    massTerm P (g â€¢ H) = massTerm P H := by
  rw [massTerm_eq_gramVector, massTerm_eq_gramVector]
  simp

/-- The quartic term of the two Higgs doublet model potential. -/
noncomputable def quarticTerm (P : PotentialParameters) (H : TwoHiggsDoublet) : â„ :=
  1/2 * P.ğ“µâ‚ * â€–H.Î¦1â€– ^ 2 * â€–H.Î¦1â€– ^ 2 + 1/2 * P.ğ“µâ‚‚ * â€–H.Î¦2â€– ^ 2 * â€–H.Î¦2â€– ^ 2
  + P.ğ“µâ‚ƒ * â€–H.Î¦1â€– ^ 2 * â€–H.Î¦2â€– ^ 2
  + P.ğ“µâ‚„ * â€–âŸªH.Î¦1, H.Î¦2âŸ«_â„‚â€– ^ 2
  + (1/2 * P.ğ“µâ‚… * âŸªH.Î¦1, H.Î¦2âŸ«_â„‚ ^ 2 + 1/2 * conj P.ğ“µâ‚… * âŸªH.Î¦2, H.Î¦1âŸ«_â„‚ ^ 2).re
  + (P.ğ“µâ‚† * â€–H.Î¦1â€– ^ 2 * âŸªH.Î¦1, H.Î¦2âŸ«_â„‚ + conj P.ğ“µâ‚† * â€–H.Î¦1â€– ^ 2 * âŸªH.Î¦2, H.Î¦1âŸ«_â„‚).re
  + (P.ğ“µâ‚‡ * â€–H.Î¦2â€– ^ 2 * âŸªH.Î¦1, H.Î¦2âŸ«_â„‚ + conj P.ğ“µâ‚‡ * â€–H.Î¦2â€– ^ 2 * âŸªH.Î¦2, H.Î¦1âŸ«_â„‚).re

lemma quarticTerm_ğ“µâ‚„_expand (P : PotentialParameters) (H : TwoHiggsDoublet) :
    H.quarticTerm P =
    1/2 * P.ğ“µâ‚ * â€–H.Î¦1â€– ^ 2 * â€–H.Î¦1â€– ^ 2 + 1/2 * P.ğ“µâ‚‚ * â€–H.Î¦2â€– ^ 2 * â€–H.Î¦2â€– ^ 2
    + P.ğ“µâ‚ƒ * â€–H.Î¦1â€– ^ 2 * â€–H.Î¦2â€– ^ 2
    + P.ğ“µâ‚„ * (âŸªH.Î¦1, H.Î¦2âŸ«_â„‚ * âŸªH.Î¦2, H.Î¦1âŸ«_â„‚).re
    + (1/2 * P.ğ“µâ‚… * âŸªH.Î¦1, H.Î¦2âŸ«_â„‚ ^ 2 + 1/2 * conj P.ğ“µâ‚… * âŸªH.Î¦2, H.Î¦1âŸ«_â„‚ ^ 2).re
    + (P.ğ“µâ‚† * â€–H.Î¦1â€– ^ 2 * âŸªH.Î¦1, H.Î¦2âŸ«_â„‚ + conj P.ğ“µâ‚† * â€–H.Î¦1â€– ^ 2 * âŸªH.Î¦2, H.Î¦1âŸ«_â„‚).re
    + (P.ğ“µâ‚‡ * â€–H.Î¦2â€– ^ 2 * âŸªH.Î¦1, H.Î¦2âŸ«_â„‚ + conj P.ğ“µâ‚‡ * â€–H.Î¦2â€– ^ 2 * âŸªH.Î¦2, H.Î¦1âŸ«_â„‚).re := by
  simp [quarticTerm]
  left
  rw [Complex.sq_norm]
  rw [â† Complex.mul_re]
  rw [â† inner_conj_symm, â† Complex.normSq_eq_conj_mul_self]
  simp only [inner_conj_symm, Complex.ofReal_re]
  rw [â† inner_conj_symm]
  exact Complex.normSq_conj âŸªH.Î¦2, H.Î¦1âŸ«_â„‚

lemma quarticTerm_eq_gramVector (P : PotentialParameters) (H : TwoHiggsDoublet) :
    quarticTerm P H = âˆ‘ a, âˆ‘ b, H.gramVector a * H.gramVector b * P.Î· a b := by
  simp [quarticTerm_ğ“µâ‚„_expand, Fin.sum_univ_three, PotentialParameters.Î·, normSq_Î¦1_eq_gramVector,
    normSq_Î¦2_eq_gramVector, Î¦1_inner_Î¦2_eq_gramVector, Î¦2_inner_Î¦1_eq_gramVector]
  ring_nf
  simp [â† Complex.ofReal_pow, Complex.ofReal_re, normSq_Î¦1_eq_gramVector,
    normSq_Î¦2_eq_gramVector]
  ring

@[simp]
lemma gaugeGroupI_smul_quarticTerm (g : StandardModel.GaugeGroupI) (P : PotentialParameters)
    (H : TwoHiggsDoublet) :
    quarticTerm P (g â€¢ H) = quarticTerm P H := by
  rw [quarticTerm_eq_gramVector, quarticTerm_eq_gramVector]
  simp

/-- The potential of the two Higgs doublet model. -/
noncomputable def potential (P : PotentialParameters) (H : TwoHiggsDoublet) : â„ :=
  massTerm P H + quarticTerm P H

@[simp]
lemma gaugeGroupI_smul_potential (g : StandardModel.GaugeGroupI)
    (P : PotentialParameters) (H : TwoHiggsDoublet) :
    potential P (g â€¢ H) = potential P H := by
  rw [potential, potential]
  simp
/-!

## Boundedness of the potential

-/

/-- The condition that the potential is bounded from below. -/
def PotentialIsBounded (P : PotentialParameters) : Prop :=
  âˆƒ c : â„, âˆ€ H : TwoHiggsDoublet, c â‰¤ potential P H

lemma potentialIsBounded_iff_forall_gramVector (P : PotentialParameters) :
    PotentialIsBounded P â†” âˆƒ c : â„, âˆ€ K : Fin 1 âŠ• Fin 3 â†’ â„, 0 â‰¤ K (Sum.inl 0) â†’
      âˆ‘ Î¼ : Fin 3, K (Sum.inr Î¼) ^ 2 â‰¤ K (Sum.inl 0) ^ 2 â†’
      c â‰¤ âˆ‘ Î¼, P.Î¾ Î¼ * K Î¼ + âˆ‘ a, âˆ‘ b, K a * K b * P.Î· a b := by
  apply Iff.intro
  Â· intro h
    obtain âŸ¨c, hcâŸ© := h
    use c
    intro v hvâ‚€ hv_sum
    obtain âŸ¨H, hHâŸ© := gramVector_surjective v hvâ‚€ hv_sum
    apply (hc H).trans
    apply le_of_eq
    rw [potential, massTerm_eq_gramVector, quarticTerm_eq_gramVector]
    simp [hH]
  Â· intro h
    obtain âŸ¨c, hcâŸ© := h
    use c
    intro H
    apply (hc H.gramVector (gramVector_inl_nonneg H) (gramVector_inr_sum_sq_le_inl H)).trans
    apply le_of_eq
    rw [potential, massTerm_eq_gramVector, quarticTerm_eq_gramVector]

lemma potentialIsBounded_iff_forall_euclid (P : PotentialParameters) :
    PotentialIsBounded P â†” âˆƒ c, âˆ€ K0 : â„, âˆ€ K : EuclideanSpace â„ (Fin 3), 0 â‰¤ K0 â†’
      â€–Kâ€– ^ 2 â‰¤ K0 ^ 2 â†’ c â‰¤ P.Î¾ (Sum.inl 0) * K0 + âˆ‘ Î¼, P.Î¾ (Sum.inr Î¼) * K Î¼
      + K0 ^ 2 * P.Î· (Sum.inl 0) (Sum.inl 0)
      + 2 * K0 * âˆ‘ b, K b * P.Î· (Sum.inl 0) (Sum.inr b) +
      âˆ‘ a, âˆ‘ b, K a * K b * P.Î· (Sum.inr a) (Sum.inr b) := by
  rw [potentialIsBounded_iff_forall_gramVector]
  refine exists_congr (fun c => ?_)
  rw [Equiv.forall_congr_left (Equiv.sumArrowEquivProdArrow (Fin 1) (Fin 3) â„)]
  simp only [Fin.isValue, Fintype.sum_sum_type, Finset.univ_unique, Fin.default_eq_zero,
    Finset.sum_singleton, Prod.forall, Equiv.sumArrowEquivProdArrow_symm_apply_inl,
    Equiv.sumArrowEquivProdArrow_symm_apply_inr]
  rw [Equiv.forall_congr_left <| Equiv.funUnique (Fin 1) â„]
  apply forall_congr'
  intro K0
  rw [Equiv.forall_congr_left <| (WithLp.equiv 2 ((i : Fin 3) â†’ (fun x => â„) i)).symm]
  apply forall_congr'
  intro K
  simp only [Fin.isValue, Equiv.funUnique_symm_apply, uniqueElim_const, Equiv.symm_symm,
    WithLp.equiv_apply]
  refine imp_congr_right ?_
  intro hle
  simp only [PiLp.norm_sq_eq_of_L2]
  simp only [Fin.isValue, Real.norm_eq_abs, sq_abs]
  refine imp_congr_right ?_
  intro hle'
  apply le_iff_le_of_cmp_eq_cmp
  congr 1
  simp [add_assoc, sq, Finset.sum_add_distrib]
  ring_nf
  simp [mul_assoc, â† Finset.mul_sum]
  conv_lhs =>
    enter [2, 2, 2, i]
    rw [PotentialParameters.Î·_symm]
  ring

lemma potentialIsBounded_iff_forall_euclid_lt (P : PotentialParameters) :
    PotentialIsBounded P â†” âˆƒ c â‰¤ 0, âˆ€ K0 : â„, âˆ€ K : EuclideanSpace â„ (Fin 3), 0 < K0 â†’
      â€–Kâ€– ^ 2 â‰¤ K0 ^ 2 â†’ c â‰¤ P.Î¾ (Sum.inl 0) * K0 + âˆ‘ Î¼, P.Î¾ (Sum.inr Î¼) * K Î¼
      + K0 ^ 2 * P.Î· (Sum.inl 0) (Sum.inl 0)
      + 2 * K0 * âˆ‘ b, K b * P.Î· (Sum.inl 0) (Sum.inr b) +
      âˆ‘ a, âˆ‘ b, K a * K b * P.Î· (Sum.inr a) (Sum.inr b) := by
  rw [potentialIsBounded_iff_forall_euclid]
  apply Iff.intro
  Â· intro h
    obtain âŸ¨c, hcâŸ© := h
    use c
    apply And.intro
    Â· simpa using hc 0 0 (by simp) (by simp)
    Â· intro K0 K hk0 hle
      exact hc K0 K hk0.le hle
  Â· intro h
    obtain âŸ¨c, hcâ‚€, hcâŸ© := h
    use c
    intro K0 K hK0 hle
    by_cases hK0' : K0 = 0
    Â· subst hK0'
      simp_all
    Â· refine hc K0 K ?_ hle
      grind

/-!

## Mass term reduced

-/

/-- A function related to the mass term of the potential, used in the boundedness
  condition and equivalent to the term `J2` in
  https://arxiv.org/abs/hep-ph/0605184. -/
noncomputable def massTermReduced (P : PotentialParameters) (k : EuclideanSpace â„ (Fin 3)) : â„ :=
  P.Î¾ (Sum.inl 0) + âˆ‘ Î¼, P.Î¾ (Sum.inr Î¼) * k Î¼

lemma massTermReduced_lower_bound (P : PotentialParameters) (k : EuclideanSpace â„ (Fin 3))
    (hk : â€–kâ€– ^ 2 â‰¤ 1) : P.Î¾ (Sum.inl 0) - âˆš(âˆ‘ a, |P.Î¾ (Sum.inr a)| ^ 2) â‰¤ massTermReduced P k := by
  simp only [Fin.isValue, massTermReduced]
  have h1 (a b c : â„) (h : - b â‰¤ c) : a - b â‰¤ a + c:= by grind
  apply h1
  let Î¾Euclid : EuclideanSpace â„ (Fin 3) := WithLp.toLp 2 (fun a => P.Î¾ (Sum.inr a))
  trans - â€–Î¾Euclidâ€–
  Â· simp [@PiLp.norm_eq_of_L2, Î¾Euclid]
  trans - (â€–kâ€– * â€–Î¾Euclidâ€–)
  Â· simp
    simp at hk
    have ha (a b : â„) (h : a â‰¤ 1) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) : a * b â‰¤ b := by nlinarith
    apply ha
    Â· exact hk
    Â· exact norm_nonneg k
    Â· exact norm_nonneg Î¾Euclid
  trans - â€–âŸªk, Î¾EuclidâŸ«_â„â€–
  Â· simp
    exact abs_real_inner_le_norm k Î¾Euclid
  trans âŸªk, Î¾EuclidâŸ«_â„
  Â· simp
    grind
  simp [PiLp.inner_apply, Î¾Euclid]

/-!

## Quartic term reduced

-/

/-- A function related to the quartic term of the potential, used in the boundedness
  condition and equivalent to the term `J4` in
  https://arxiv.org/abs/hep-ph/0605184. -/
noncomputable def quarticTermReduced (P : PotentialParameters) (k : EuclideanSpace â„ (Fin 3)) : â„ :=
  P.Î· (Sum.inl 0) (Sum.inl 0) + 2 * âˆ‘ b, k b * P.Î· (Sum.inl 0) (Sum.inr b) +
  âˆ‘ a, âˆ‘ b, k a * k b * P.Î· (Sum.inr a) (Sum.inr b)

lemma potentialIsBounded_iff_exists_forall_forall_reduced (P : PotentialParameters) :
    PotentialIsBounded P â†” âˆƒ c â‰¤ 0, âˆ€ K0 : â„, âˆ€ k : EuclideanSpace â„ (Fin 3), 0 < K0 â†’
      â€–kâ€– ^ 2 â‰¤ 1 â†’ c â‰¤ K0 * massTermReduced P k + K0 ^ 2 * quarticTermReduced P k := by
  rw [potentialIsBounded_iff_forall_euclid_lt]
  refine exists_congr <| fun c => and_congr_right <| fun hc => forall_congr' <| fun K0 => ?_
  apply Iff.intro
  Â· refine fun h k hK0 k_le_one => (h (K0 â€¢ k) hK0 ?_).trans (le_of_eq ?_)
    Â· simp [norm_smul]
      rw [abs_of_nonneg (by positivity), mul_pow]
      nlinarith
    Â· simp [add_assoc, massTermReduced, quarticTermReduced]
      ring_nf
      simp [add_assoc, mul_assoc, â† Finset.mul_sum, sq]
      ring
  Â· intro h K hK0 hle
    refine (h ((1 / K0) â€¢ K) hK0 ?_).trans (le_of_eq ?_)
    Â· simp [norm_smul]
      field_simp
      rw [sq_le_sq] at hle
      simpa using hle
    Â· simp [add_assoc, massTermReduced, quarticTermReduced]
      ring_nf
      simp [add_assoc, mul_assoc, â† Finset.mul_sum, sq]
      field_simp
      ring_nf
      simp only [â† Finset.sum_mul, Fin.isValue]
      field_simp
      ring

lemma quarticTermReduced_nonneg_of_potentialIsBounded (P : PotentialParameters)
    (hP : PotentialIsBounded P) (k : EuclideanSpace â„ (Fin 3))
    (hk : â€–kâ€– ^ 2 â‰¤ 1) : 0 â‰¤ quarticTermReduced P k := by
  rw [potentialIsBounded_iff_exists_forall_forall_reduced] at hP
  suffices hp : âˆ€ (a b : â„), (âˆƒ c â‰¤ 0, âˆ€ x, 0 < x â†’ c â‰¤ a * x + b * x ^ 2) â†’
      0 â‰¤ b âˆ§ (b = 0 â†’ 0 â‰¤ a) by
    obtain âŸ¨c, hc, hâŸ© := hP
    refine (hp (massTermReduced P k) (quarticTermReduced P k) âŸ¨c, hc, ?_âŸ©).1
    grind
  intro a b
  by_cases hb : b = 0
  /- The case of b = 0. -/
  Â· subst hb
    by_cases ha : a = 0
    Â· subst ha
      simp
    Â· simp only [zero_mul, add_zero, le_refl, forall_const, true_and]
      rintro âŸ¨c, hc, hxâŸ©
      by_contra h2
      simp_all
      refine not_lt_of_ge (hx (c/a + 1) ?_) ?_
      Â· exact add_pos_of_nonneg_of_pos (div_nonneg_of_nonpos hc (Std.le_of_lt h2))
          Real.zero_lt_one
      Â· field_simp
        grind
  /- The case of b â‰  0. -/
  have h1 (x : â„) : a * x + b * x ^ 2 = b * (x + a / (2 * b)) ^ 2 - a ^ 2 / (4 * b) := by grind
  generalize a ^ 2 / (4 * b) = c1 at h1
  generalize a / (2 * b) = d at h1
  simp only [hb, IsEmpty.forall_iff, and_true]
  have hlt (c : â„) (x : â„) : (c â‰¤ a * x + b * x ^ 2) â†” c + c1 â‰¤ b * (x + d) ^ 2 := by grind
  conv_lhs => enter [1, c, 2, x]; rw [hlt c]
  trans âˆƒ c, âˆ€ x, 0 < x â†’ c â‰¤ b * (x + d) ^ 2
  Â· rintro âŸ¨c, hc, hxâŸ©
    use c + c1
  rintro âŸ¨c, hcâŸ©
  by_contra hn
  suffices hs : âˆ€ x, x ^ 2 â‰¤ c/b from not_lt_of_ge (hs âˆš(|c/b| + 1)) (by grind)
  suffices hs : âˆ€ x, 0 < x â†’ (x + d) ^ 2 â‰¤ c/b from
    fun x => le_trans ((Real.sqrt_le_left (by grind)).mp
      (by grind [Real.sqrt_sq_eq_abs])) (hs (|x| + |d| + 1) (by positivity))
  exact fun x hx => (le_div_iff_of_neg (by grind)).mpr (by grind)

lemma potentialIsBounded_iff_exists_forall_reduced (P : PotentialParameters) :
    PotentialIsBounded P â†” âˆƒ c, 0 â‰¤ c âˆ§ âˆ€ k : EuclideanSpace â„ (Fin 3), â€–kâ€– ^ 2 â‰¤ 1 â†’
      0 â‰¤ quarticTermReduced P k âˆ§
      (massTermReduced P k < 0 â†’
      massTermReduced P k ^ 2 â‰¤ 4 * quarticTermReduced P k * c) := by
  rw [potentialIsBounded_iff_exists_forall_forall_reduced]
  refine Iff.intro (fun âŸ¨c, hc, hâŸ© => âŸ¨-c, by grind, fun k hk => ?_âŸ©)
    (fun âŸ¨c, hc, hâŸ© => âŸ¨-c, by grind, fun K0 k hk0 hk => ?_âŸ©)
  Â· have hJ4_nonneg : 0 â‰¤ quarticTermReduced P k := by
      refine quarticTermReduced_nonneg_of_potentialIsBounded P ?_ k hk
      rw [potentialIsBounded_iff_exists_forall_forall_reduced]
      exact âŸ¨c, hc, hâŸ©
    have h0 : âˆ€ K0, 0 < K0 â†’ c â‰¤ K0 * massTermReduced P k + K0 ^ 2 * quarticTermReduced P k :=
      fun K0 a => h K0 k a hk
    clear h
    generalize massTermReduced P k = j2 at *
    generalize quarticTermReduced P k = j4 at *
    by_cases j4_zero : j4 = 0
    Â· subst j4_zero
      simp_all
      intro hj2
      by_contra hn
      specialize h0 ((c - 1) / j2) <| by
        refine div_pos_iff.mpr (Or.inr ?_)
        grind
      field_simp at h0
      linarith
    Â· have hsq (K0 : â„) : K0 * j2 + K0 ^ 2 * j4 =
            j4 * (K0 + j2 / (2 * j4)) ^ 2 - j2 ^ 2 / (4 * j4) := by
        grind
      have hj_pos : 0 < j4 := by grind
      apply And.intro
      Â· grind
      Â· intro j2_neg
        conv at h0 => enter [2]; rw [hsq]
        specialize h0 (- j2 / (2 * j4)) <| by
          field_simp
          grind
        ring_nf at h0
        field_simp at h0
        grind
  Â· specialize h k hk
    generalize massTermReduced P k = j2 at *
    generalize quarticTermReduced P k = j4 at *
    by_cases hJ4 : j4 = 0
    Â· subst j4
      simp_all
      trans 0
      Â· grind
      Â· by_cases hJ2 : j2 = 0
        Â· simp_all
        Â· simp_all
    Â· have hJ4_pos : 0 < j4 := by grind
      have h0 : K0 * j2 + K0 ^ 2 * j4 = j4 * (K0 + j2 / (2 * j4)) ^ 2 - j2 ^ 2 / (4 * j4) := by
          grind
      rw [h0]
      by_cases hJ2_neg : j2 < 0
      Â· trans j4 * (K0 + j2 / (2 * j4)) ^ 2 - c
        Â· nlinarith
        Â· field_simp
          grind
      Â· refine neg_le_sub_iff_le_add.mpr ?_
        trans j4 * (K0 + j2 / (2 * j4)) ^ 2
        Â· nlinarith
        Â· grind

lemma massTermReduced_pos_of_quarticTermReduced_zero_potentialIsBounded (P : PotentialParameters)
    (hP : PotentialIsBounded P) (k : EuclideanSpace â„ (Fin 3))
    (hk : â€–kâ€– ^ 2 â‰¤ 1) (hq : quarticTermReduced P k = 0) : 0 â‰¤ massTermReduced P k := by
  rw [potentialIsBounded_iff_exists_forall_reduced] at hP
  obtain âŸ¨c, hcâ‚€, hcâŸ© := hP
  specialize hc k hk
  rw [hq] at hc
  simp only [le_refl, mul_zero, zero_mul, sq_nonpos_iff, true_and] at hc
  generalize massTermReduced P k = j2 at *
  grind


/-- A lemma invalidating the step in https://arxiv.org/pdf/hep-ph/0605184 leading to
  equation (4.4). -/
lemma forall_reduced_exists_not_potentialIsBounded :
    âˆƒ P, Â¬ PotentialIsBounded P âˆ§ (âˆ€ k : EuclideanSpace â„ (Fin 3), â€–kâ€– ^ 2 â‰¤ 1 â†’
    0 â‰¤ quarticTermReduced P k âˆ§ (quarticTermReduced P k = 0 â†’ 0 â‰¤ massTermReduced P k)) := by
  /- Construction of the explicit counter example. -/
  let P : PotentialParameters := {(0 : PotentialParameters) with
    mâ‚â‚‚2 := Complex.I
    ğ“µâ‚ := 2
    ğ“µâ‚‚ := 2
    ğ“µâ‚ƒ := 2
    ğ“µâ‚„ := 2
    ğ“µâ‚… := 2
    ğ“µâ‚† := -2
    ğ“µâ‚‡ := -2}
  have P_massTermReduced (k : EuclideanSpace â„ (Fin 3)) : massTermReduced P k = k 1 := by
    simp [massTermReduced, PotentialParameters.Î¾, Fin.isValue, P, Fin.sum_univ_three]
  have P_quarticTermReduced (k : EuclideanSpace â„ (Fin 3)) :
      quarticTermReduced P k = (1 - k 0) ^ 2 := by
    simp [quarticTermReduced, PotentialParameters.Î·, Fin.isValue, P, Fin.sum_univ_three]
    ring
  have P_quarticTermReduced_nonneg (k : EuclideanSpace â„ (Fin 3)) :
      0 â‰¤ quarticTermReduced P k := by
    rw [P_quarticTermReduced]
    exact sq_nonneg (1 - k 0)
  use P
  apply And.intro
  /- The condition that P is not bounded. -/
  Â· /- Changing the goal to an existence. -/
    rw [potentialIsBounded_iff_exists_forall_reduced]
    by_contra hc
    obtain âŸ¨c, c_pos, hcâŸ© := hc
    suffices h_exists : âˆƒ k, â€–kâ€– ^ 2 â‰¤ 1 âˆ§
        0 â‰¤ quarticTermReduced P k âˆ§ massTermReduced P k < 0 âˆ§
        4 * c * quarticTermReduced P k < (massTermReduced P k) ^ 2 by
      obtain âŸ¨k, hk_norm, hk_quartic, hk_mass_neg, hk_massâŸ© := h_exists
      specialize hc k hk_norm
      have := hc.2 hk_mass_neg
      linarith
    /- A general sequence of points. -/
    let kt (t : â„) : EuclideanSpace â„ (Fin 3) :=
      !â‚‚[(1 : â„), 0, 0] - t â€¢ !â‚‚[Real.sin t, Real.cos t, 0]
    have kt_normSq (t : â„) : â€–kt tâ€– ^ 2 = 1 + t ^ 2 - 2 * t * Real.sin t := by
      simp [kt, PiLp.norm_sq_eq_of_L2, Fin.sum_univ_three]
      trans 1 - t * Real.sin t * 2 + t ^ 2 * (Real.sin t ^ 2 +Real.cos t ^ 2)
      Â· ring
      rw [Real.sin_sq_add_cos_sq]
      ring
    have kt_normSq_le_one (t : â„) (ht : 0 â‰¤ t) (htu : t â‰¤ Real.pi / 2) : â€–kt tâ€– ^ 2 â‰¤ 1 := by
      rw [kt_normSq, tsub_le_iff_right, add_le_add_iff_left]
      trans 2 * t * (2 / Real.pi * t)
      Â· ring_nf
        rw [mul_assoc]
        apply le_mul_of_one_le_right
        Â· positivity
        Â· field_simp
          exact Real.pi_le_four
      Â· nlinarith [Real.mul_le_sin ht htu]
    have kt_quarticTermReduced (t : â„) :
        quarticTermReduced P (kt t) = t ^ 2 * Real.sin t ^ 2 := by
      simp only [P_quarticTermReduced, Fin.isValue, PiLp.sub_apply, Matrix.cons_val_zero,
        PiLp.smul_apply, smul_eq_mul, sub_sub_cancel, kt]
      ring
    have kt_massTermReduced (t : â„) : massTermReduced P (kt t) = - t * Real.cos t := by
      simp [P_massTermReduced, kt]
    have kt_massTermReduced_neg (t : â„) (ht : 0 < t) (htu : t < Real.pi / 2) :
        massTermReduced P (kt t) < 0 := by
      rw [kt_massTermReduced, neg_mul, Left.neg_neg_iff]
      refine (mul_pos_iff_of_pos_right (Real.cos_pos_of_mem_Ioo <| Set.mem_Ioo.mp âŸ¨?_, htuâŸ©)).mpr ht
      linarith
    /- A specific point invalidating the boundedness. -/
    use kt (Real.arctan (2 * Real.sqrt (c + 1))â»Â¹)
    refine âŸ¨?_, ?_, ?_, ?_âŸ©
    Â· /- Norm le 1. -/
      apply kt_normSq_le_one
      Â· simp
      Â· exact le_of_lt <| Real.arctan_lt_pi_div_two _
    Â· /- Quadratic term non negative. -/
      exact P_quarticTermReduced_nonneg (kt (Real.arctan _))
    Â· /- Mass term negative. -/
      apply kt_massTermReduced_neg
      Â· simp only [mul_inv_rev, Real.arctan_pos, inv_pos, Nat.ofNat_pos, mul_pos_iff_of_pos_right,
        Real.sqrt_pos]
        linarith
      Â· apply Real.arctan_lt_pi_div_two
    Â· /- The inequality -/
      rw [kt_quarticTermReduced, kt_massTermReduced]
      simp [mul_pow]
      refine (mul_inv_lt_iffâ‚€ ?_).mp ?_
      Â· refine pow_two_pos_of_ne_zero (ne_of_gt ?_)
        exact Real.cos_pos_of_mem_Ioo (Real.arctan_mem_Ioo ((âˆš(c + 1))â»Â¹ * 2â»Â¹))
      apply lt_of_eq_of_lt (b :=  4 * c * (Real.arctan ((âˆš(c + 1))â»Â¹ * 2â»Â¹) ^ 2 *
          Real.tan (Real.arctan ((âˆš(c + 1))â»Â¹ * 2â»Â¹)) ^ 2))
      Â· rw [Real.tan_eq_sin_div_cos]
        field_simp
      Â· rw [Real.tan_arctan]
        simp [mul_pow]
        rw [Real.sq_sqrt (by positivity)]
        field_simp
        grind
  /- The condition on the reduced terms. -/
  Â· intro k hk
    apply And.intro
    Â· exact P_quarticTermReduced_nonneg k
    intro hq
    rw [P_quarticTermReduced] at hq
    simp at hq
    have hk0 : k 0 = 1 := by linarith
    have hk1 : k 1 = 0 := by
      simp only [PiLp.norm_sq_eq_of_L2, Real.norm_eq_abs, sq_abs, Fin.sum_univ_three,
        Fin.isValue] at hk
      nlinarith
    rw [P_massTermReduced, hk1]

end TwoHiggsDoublet
