/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.SpaceTime.Basic
import PhysLean.Particles.StandardModel.Basic
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.Analysis.InnerProductSpace.Adjoint
/-!

# The Higgs field

## i. Overview

The Higgs field describes is the underlying field of the Higgs boson.
It is a map from SpaceTime to a 2-dimensional complex vector space.
In this module we define the Higgs field and prove some basic properties.

## ii. Important results

## iii. Table of Contents

## iv. References

- We use conventions given in: [Review of Particle Physics, PDG][ParticleDataGroup:2018ovx]

-/

namespace StandardModel
noncomputable section

open Manifold
open Matrix
open Complex
open ComplexConjugate
open SpaceTime

/-!

##Â A. The Higgs vector space

The target space of the Higgs field is a 2-dimensional complex vector space.
In this section we will define this vector space, and the action of the
global gauge group on it.

-/


/-!

### A.1 Definition of the Higgs vector space

-/
/-- The vector space `HiggsVec` is defined to be the complex Euclidean space of dimension 2.
  For a given spacetime point a Higgs field gives a value in `HiggsVec`. -/
abbrev HiggsVec := EuclideanSpace â„‚ (Fin 2)

namespace HiggsVec

/-!

### A.2 Relation to `(Fin 2 â†’ â„‚)`

We define the continuous linear map from `HiggsVec` to `(Fin 2 â†’ â„‚)` achieved by
casting vectors, we also show that this map is smooth.

-/

/-- The continuous linear map from the vector space `HiggsVec` to `(Fin 2 â†’ â„‚)` achieved by
casting vectors. -/
def toFin2â„‚ : HiggsVec â†’L[â„] (Fin 2 â†’ â„‚) where
  toFun x := x
  map_add' x y := rfl
  map_smul' a x := rfl

/-- The map `toFin2â„‚` is smooth. -/
lemma smooth_toFin2â„‚ : ContMDiff ğ“˜(â„, HiggsVec) ğ“˜(â„, Fin 2 â†’ â„‚) âŠ¤ toFin2â„‚ :=
  ContinuousLinearMap.contMDiff toFin2â„‚


/-!

### A.3 Orthonormal basis

We define an orthonormal basis of `HiggsVec`.

-/

/-- An orthonormal basis of `HiggsVec`. -/
def orthonormBasis : OrthonormalBasis (Fin 2) â„‚ HiggsVec :=
  EuclideanSpace.basisFun (Fin 2) â„‚

/-!

## A.4 Generating Higgs vectors from real numbers

Given a real number `a` we define the Higgs vector corresponding to that real number
as `(âˆša, 0)`. This has the property that it's norm is equal to `a`.

-/

/-- Generating a Higgs vector from a real number, such that the norm-squared of that Higgs vector
  is the given real number. -/
def ofReal (a : â„) : HiggsVec :=
  ![Real.sqrt a, 0]

@[simp]
lemma ofReal_normSq {a : â„} (ha : 0 â‰¤ a) : â€–ofReal aâ€– ^ 2 = a := by
  simp only [ofReal]
  rw [PiLp.norm_sq_eq_of_L2]
  rw [@Fin.sum_univ_two]
  simp only [Fin.isValue, cons_val_zero, norm_real, Real.norm_eq_abs, _root_.sq_abs, cons_val_one,
    norm_zero, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow, add_zero]
  exact Real.sq_sqrt ha

/-!

### A.5 Action of the gauge group on `HiggsVec`

The gauge group of the Standard Model acts on `HiggsVec` by matrix multiplication.

-/

/-!

#### A.5.1 Definition of the action

-/

instance : SMul StandardModel.GaugeGroupI HiggsVec where
  smul g Ï† :=  g.toU1 ^ 3 â€¢ (g.toSU2.1 *áµ¥ Ï†)

lemma gaugeGroupI_smul_eq (g : StandardModel.GaugeGroupI) (Ï† : HiggsVec) :
    g â€¢ Ï† = g.toU1 ^ 3 â€¢ (g.toSU2.1 *áµ¥ Ï†) := rfl

lemma gaugeGroupI_smul_eq_U1_mul_SU2 (g : StandardModel.GaugeGroupI) (Ï† : HiggsVec) :
    g â€¢ Ï† = g.toSU2.1 *áµ¥ (g.toU1 ^ 3 â€¢ Ï†)  := by
  rw [gaugeGroupI_smul_eq, â† mulVec_smul]

instance : MulAction StandardModel.GaugeGroupI HiggsVec where
  one_smul Ï† := by simp [gaugeGroupI_smul_eq]
  mul_smul gâ‚ gâ‚‚ Ï† := by
    rw [gaugeGroupI_smul_eq, gaugeGroupI_smul_eq, gaugeGroupI_smul_eq_U1_mul_SU2]
    rw [mulVec_smul, mulVec_smul, smul_smul, mulVec_mulVec]
    congr
    simp [mul_pow]

/-!

#### A.5.2 Unitary nature of the action

The action of `StandardModel.GaugeGroupI` on `HiggsVec` is unitary.

-/
open InnerProductSpace

@[simp]
lemma gaugeGroupI_smul_inner (g : StandardModel.GaugeGroupI) (Ï† Ïˆ : HiggsVec) :
    âŸªg â€¢ Ï†, g â€¢ ÏˆâŸ«_â„‚ = âŸªÏ†, ÏˆâŸ«_â„‚  := by
  calc âŸªg â€¢ Ï†, g â€¢ ÏˆâŸ«_â„‚
    _ =   WithLp.ofLp (g â€¢ Ïˆ) â¬áµ¥ star (WithLp.ofLp (g â€¢ Ï†)) := by
      rw [EuclideanSpace.inner_eq_star_dotProduct]
    _ = (g â€¢ Ïˆ) â¬áµ¥ star (g â€¢ Ï†) := by rfl
    _ = (g.toSU2.1 *áµ¥ (g.toU1 ^ 3 â€¢ Ïˆ) ) â¬áµ¥ star (g.toSU2.1 *áµ¥ (g.toU1 ^ 3 â€¢ Ï†)) := by
      rw [gaugeGroupI_smul_eq_U1_mul_SU2, gaugeGroupI_smul_eq_U1_mul_SU2]
    _ = (g.toSU2.1 *áµ¥ (g.toU1 ^ 3 â€¢ Ïˆ) ) â¬áµ¥ (star ((g.toU1 ^ 3 â€¢ Ï†)) áµ¥*  star (g.toSU2.1)) := by
      rw [star_mulVec]
      rfl
    _ = ((star (g.toSU2.1) * g.toSU2.1) *áµ¥ (g.toU1 ^ 3 â€¢ Ïˆ)) â¬áµ¥ star ((g.toU1 ^ 3 â€¢ Ï†)) := by
      rw [dotProduct_comm, â† Matrix.dotProduct_mulVec, dotProduct_comm, mulVec_mulVec]
    _ = ((g.toU1 ^ 3 â€¢ Ïˆ)) â¬áµ¥ star ((g.toU1 ^ 3 â€¢ Ï†)) := by
      rw [mem_unitaryGroup_iff'.mp (GaugeGroupI.toSU2 g).2.1]
      simp
    _ = ((g.toU1 ^ 3 â€¢ Ïˆ)) â¬áµ¥ star (g.toU1 ^ 3) â€¢ star (Ï†.toFin2â„‚) := by
      congr
      ext i
      simp
      change (starRingEnd â„‚) (GaugeGroupI.toU1 g ^ 3 * Ï† i)  = _
      simp
      rfl
    _ = (Ïˆ â¬áµ¥ star (Ï†.toFin2â„‚)) := by
      rw [dotProduct_smul, smul_dotProduct, smul_smul, unitary.star_mul_self]
      simp

@[simp]
lemma gaugeGroupI_smul_norm (g : StandardModel.GaugeGroupI) (Ï† : HiggsVec) :
    â€–g â€¢ Ï†â€–  = â€–Ï†â€–  := by
  rw [norm_eq_sqrt_re_inner (ğ•œ := â„‚), norm_eq_sqrt_re_inner (ğ•œ := â„‚)]
  simp

/-!

### A.6. The Gauge orbit of a Higgs vector

We show that two Higgs vectors are in the same gauge orbit if and only if they have the same norm.

-/

/-!

#### A.6.1 The rotation matrix to ofReal

We define an element of `GaugeGroupI` which takes a given Higgs vector to the
corresponding `ofReal` Higgs vector.

-/

/-- Given a Higgs vector, a rotation matrix which puts the second component of the
  vector to zero, and the first component to a real -/
def toRealGroupElem (Ï† : HiggsVec) : GaugeGroupI :=
  if hÏ† : Ï† = 0 then 1 else by
  have h0 :  (â€–Ï†â€–^2 : â„) = Ï† 0 * (starRingEnd â„‚) (Ï† 0) + Ï† 1 * (starRingEnd â„‚) (Ï† 1) := by
    rw [â† @real_inner_self_eq_norm_sq]
    simp only [Fin.isValue, mul_conj,  PiLp.inner_apply, Complex.inner, ofReal_re,
      Fin.sum_univ_two, ofReal_add]
  have h0' :  (â€–Ï†â€–^2 : â„‚) = Ï† 0 * (starRingEnd â„‚) (Ï† 0) + Ï† 1 * (starRingEnd â„‚) (Ï† 1) := by
    rw [â† h0]
    simp
  refine âŸ¨1, âŸ¨!![conj (Ï† 0) / â€–Ï†â€–, conj (Ï† 1) / â€–Ï†â€–; -Ï† 1 /â€–Ï†â€–, Ï† 0 /â€–Ï†â€– ; ], ?_, ?_âŸ©, 1âŸ©
  /- Member of the unitary group. -/
  Â· simp only [Fin.isValue, SetLike.mem_coe]
    rw [mem_unitaryGroup_iff']
    funext i j
    rw [Matrix.mul_apply]
    simp
    have hÏ† : Complex.ofReal â€–Ï†â€– â‰  0 := ofReal_inj.mp.mt (norm_ne_zero_iff.mpr hÏ†)
    fin_cases i <;> fin_cases j <;>
    all_goals
    Â· simp
      field_simp
      rw [h0']
      ring
  /- Determinant equals zeor. -/
  Â· have h1 : (â€–Ï†â€– : â„‚) â‰  0 := ofReal_inj.mp.mt (norm_ne_zero_iff.mpr hÏ†)
    simp [det_fin_two]
    field_simp
    rw [â† ofReal_pow, â† @real_inner_self_eq_norm_sq,]
    simp only [Fin.isValue, mul_conj,  PiLp.inner_apply, Complex.inner, ofReal_re,
      Fin.sum_univ_two, ofReal_add]
    rw [â† mul_conj, â† mul_conj]
    ring

lemma toRealGroupElem_smul_self (Ï† : HiggsVec) :
    (toRealGroupElem Ï†) â€¢ Ï† = ofReal (â€–Ï†â€– ^ 2) := by
  by_cases hÏ† : Ï† = 0
  Â· simp [hÏ†, toRealGroupElem]
    ext i
    fin_cases i <;> simp [ofReal]
  rw [gaugeGroupI_smul_eq]
  have h0 :  (â€–Ï†â€–^2 : â„) = Ï† 0 * (starRingEnd â„‚) (Ï† 0) + Ï† 1 * (starRingEnd â„‚) (Ï† 1) := by
    rw [â† @real_inner_self_eq_norm_sq]
    simp only [Fin.isValue, mul_conj,  PiLp.inner_apply, Complex.inner, ofReal_re,
      Fin.sum_univ_two, ofReal_add]
  have h0' :  (â€–Ï†â€–^2 : â„‚) = Ï† 0 * (starRingEnd â„‚) (Ï† 0) + Ï† 1 * (starRingEnd â„‚) (Ï† 1) := by
    rw [â† h0]
    simp
  simp [toRealGroupElem, hÏ†]
  Â· simp [GaugeGroupI.toU1, GaugeGroupI.toSU2]
    ext i
    have hÏ† : Complex.ofReal â€–Ï†â€– â‰  0 := ofReal_inj.mp.mt (norm_ne_zero_iff.mpr hÏ†)
    fin_cases i
    Â· simp [ofReal]
      field_simp
      rw [h0']
      ring_nf
      rfl
    Â· simp [ofReal]
      field_simp
      change  -(Ï† 1 * Ï† 0) + Ï† 0 * Ï† 1= _
      ring

/-!

#### A.6.2 Members of orbits

Members of the orbit of a Higgs vector under the action of `GaugeGroupI` are exactly those
Higgs vectors with the same norm.

-/

lemma mem_orbit_gaugeGroupI_iff (Ï† : HiggsVec) (Ïˆ : HiggsVec) :
    Ïˆ âˆˆ MulAction.orbit GaugeGroupI Ï† â†” â€–Ïˆâ€– = â€–Ï†â€– := by
  constructor
  Â· intro h
    obtain âŸ¨g, rflâŸ© := h
    simp
  Â· intro h
    use (toRealGroupElem Ïˆ)â»Â¹ * toRealGroupElem (Ï†)
    simp only
    rw [â† smul_smul, toRealGroupElem_smul_self Ï†, â† h, â† toRealGroupElem_smul_self Ïˆ, smul_smul]
    simp

end HiggsVec

/-!

## B. The Higgs bundle

We define the Higgs bundle as the trivial vector bundle with base `SpaceTime` and fiber `HiggsVec`.
The Higgs field will then be defined as smooth sections of this bundle.
-/

/-!

### B.1 Definition of the Higgs bundle

We define the Higgs bundle.

-/

TODO "6V2IS" "Make `HiggsBundle` an associated bundle."

/-- The `HiggsBundle` is defined as the trivial vector bundle with base `SpaceTime` and
  fiber `HiggsVec`. Thus as a manifold it corresponds to `â„â´ Ã— â„‚Â²`. -/
abbrev HiggsBundle := Bundle.Trivial SpaceTime HiggsVec

/-!

### B.2 Instance of a vector bundle

We given the Higgs bundle an instance of a smooth vector bundle.

-/

/-- The instance of a smooth vector bundle with total space `HiggsBundle` and fiber `HiggsVec`. -/
instance : ContMDiffVectorBundle âŠ¤ HiggsVec HiggsBundle (Lorentz.Vector.asSmoothManifold 3) :=
  Bundle.Trivial.contMDiffVectorBundle HiggsVec


/-!

## C. The Higgs fields

Higgs fields are smooth sections of the Higgs bundle.
This corresponds to smooth maps from `SpaceTime` to `HiggsVec`.
We here define the type of Higgs fields and create an API around them.

-/

/-- The type `HiggsField` is defined such that elements are smooth sections of the trivial
  vector bundle `HiggsBundle`. Such elements are Higgs fields. Since `HiggsField` is
  trivial as a vector bundle, a Higgs field is equivalent to a smooth map
  from `SpaceTime` to `HiggsVec`. -/
abbrev HiggsField : Type := ContMDiffSection
  (Lorentz.Vector.asSmoothManifold 3) HiggsVec âŠ¤ HiggsBundle



namespace HiggsField
open HiggsVec

/-!

### C.1 Relations between `HiggsField` and `HiggsVec`

-/

/-!

#### C.1.1 The constant Higgs field

We define the constant Higgs field associated to a given Higgs vector.

-/


/-- Given a vector in `HiggsVec` the constant Higgs field with value equal to that
section. -/
def const : HiggsVec â†’â‚—[â„] HiggsField where
  toFun  Ï† := {
    toFun := fun _ â†¦ Ï†,
    contMDiff_toFun := by
      intro x
      rw [Bundle.contMDiffAt_section]
      exact contMDiffAt_const}
  map_add' Ï† Ïˆ := by
    ext1 x
    simp
  map_smul' a Ï† := by
    ext1 x
    simp

/-- For all spacetime points, the constant Higgs field defined by a Higgs vector,
  returns that Higgs Vector. -/
@[simp]
lemma const_apply (Ï† : HiggsVec) (x : SpaceTime) : const Ï† x = Ï† := rfl

/-!

### C.1.2 The map from `HiggsField` to `SpaceTime â†’ HiggsVec`

-/

/-- Given a `HiggsField`, the corresponding map from `SpaceTime` to `HiggsVec`. -/
def toHiggsVec (Ï† : HiggsField) : SpaceTime â†’ HiggsVec := Ï†

lemma toHiggsVec_smooth (Ï† : HiggsField) :
    ContMDiff ğ“˜(â„, SpaceTime) ğ“˜(â„, HiggsVec) âŠ¤ Ï†.toHiggsVec := by
  intro x0
  have h1 := Ï†.contMDiff x0
  rw [Bundle.contMDiffAt_section] at h1
  exact h1

lemma const_toHiggsVec_apply (Ï† : HiggsField) (x : SpaceTime) :
    const (Ï†.toHiggsVec x) x = Ï† x := rfl

lemma toFin2â„‚_comp_toHiggsVec (Ï† : HiggsField) :
    toFin2â„‚ âˆ˜ Ï†.toHiggsVec = Ï† := rfl

/-!

### C.2 Smoothness properties of components

We prove some smoothness properties of the components of a Higgs field.

-/

lemma toVec_smooth (Ï† : HiggsField) : ContMDiff ğ“˜(â„, SpaceTime) ğ“˜(â„, Fin 2 â†’ â„‚) âŠ¤ Ï† :=
  smooth_toFin2â„‚.comp Ï†.toHiggsVec_smooth

lemma apply_smooth (Ï† : HiggsField) :
    âˆ€ i, ContMDiff ğ“˜(â„, SpaceTime) ğ“˜(â„, â„‚) âŠ¤ (fun (x : SpaceTime) => (Ï† x i)) :=
  (contMDiff_pi_space).mp (Ï†.toVec_smooth)

lemma apply_re_smooth (Ï† : HiggsField) (i : Fin 2) :
    ContMDiff ğ“˜(â„, SpaceTime) ğ“˜(â„, â„) âŠ¤ (reCLM âˆ˜ (fun (x : SpaceTime) => (Ï† x i))) :=
  (ContinuousLinearMap.contMDiff reCLM).comp (Ï†.apply_smooth i)

lemma apply_im_smooth (Ï† : HiggsField) (i : Fin 2) :
    ContMDiff ğ“˜(â„, SpaceTime) ğ“˜(â„, â„) âŠ¤ (imCLM âˆ˜ (fun (x : SpaceTime) => (Ï† x i))) :=
  (ContinuousLinearMap.contMDiff imCLM).comp (Ï†.apply_smooth i)


/-!

### C.3 The pointwise inner product

The pointwise inner product on the Higgs field.

-/

open InnerProductSpace

instance : Inner (SpaceTime â†’ â„‚) (HiggsField) where
  inner Ï†1 Ï†2 := fun x => âŸªÏ†1 x, Ï†2 xâŸ«_â„‚

/-!

#### C.3.1 Basic equaltities

-/

lemma inner_apply (Ï†1 Ï†2 : HiggsField) (x : SpaceTime) :
    âŸªÏ†1, Ï†2âŸ«_(SpaceTime â†’ â„‚) x = âŸªÏ†1 x, Ï†2 xâŸ«_â„‚ := rfl

lemma inner_eq_expand (Ï†1 Ï†2 : HiggsField) :
    âŸªÏ†1, Ï†2âŸ«_(SpaceTime â†’ â„‚) = fun x => equivRealProdCLM.symm (((Ï†1 x 0).re * (Ï†2 x 0).re
    + (Ï†1 x 1).re * (Ï†2 x 1).re+ (Ï†1 x 0).im * (Ï†2 x 0).im + (Ï†1 x 1).im * (Ï†2 x 1).im),
    ((Ï†1 x 0).re * (Ï†2 x 0).im + (Ï†1 x 1).re * (Ï†2 x 1).im
    - (Ï†1 x 0).im * (Ï†2 x 0).re - (Ï†1 x 1).im * (Ï†2 x 1).re)) := by
  funext x
  simp only [inner_apply, PiLp.inner_apply, RCLike.inner_apply, Fin.sum_univ_two,
    equivRealProdCLM_symm_apply, ofReal_add, ofReal_mul, ofReal_sub]
  rw [RCLike.conj_eq_re_sub_im, RCLike.conj_eq_re_sub_im]
  nth_rewrite 1 [â† RCLike.re_add_im (Ï†2 x 0)]
  nth_rewrite 1 [â† RCLike.re_add_im (Ï†2 x 1)]
  ring_nf
  simp only [Fin.isValue, RCLike.re_to_complex, coe_algebraMap, RCLike.I_to_complex,
    RCLike.im_to_complex, I_sq, mul_neg, mul_one, neg_mul, sub_neg_eq_add, one_mul]
  ring


/-- Expands the inner product on Higgs fields in terms of complex components of the
  Higgs fields. -/
lemma inner_expand_conj (Ï†1 Ï†2 : HiggsField) (x : SpaceTime) :
    âŸªÏ†1, Ï†2âŸ«_(SpaceTime â†’ â„‚) x = conj (Ï†1 x 0) * Ï†2 x 0 + conj (Ï†1 x 1) * Ï†2 x 1 := by
  simp [inner_apply, PiLp.inner_apply]
  ring

/-!

#### C.3.2 Symmetry properties

-/

lemma inner_symm (Ï†1 Ï†2 : HiggsField) :
    conj âŸªÏ†2, Ï†1âŸ«_(SpaceTime â†’ â„‚) = âŸªÏ†1, Ï†2âŸ«_(SpaceTime â†’ â„‚)  := by
  funext x
  simp only [inner_apply, Pi.conj_apply, inner_conj_symm]

/-!

#### C.3.3 Linearity conditions

-/

lemma inner_add_left (Ï†1 Ï†2 Ï†3 : HiggsField) :
    âŸªÏ†1 + Ï†2, Ï†3âŸ«_(SpaceTime â†’ â„‚) = âŸªÏ†1, Ï†3âŸ«_(SpaceTime â†’ â„‚) + âŸªÏ†2, Ï†3âŸ«_(SpaceTime â†’ â„‚) := by
  funext x
  simp [inner_apply]
  rw [_root_.inner_add_left]

lemma inner_add_right (Ï†1 Ï†2 Ï†3 : HiggsField) :
    âŸªÏ†1, Ï†2 + Ï†3âŸ«_(SpaceTime â†’ â„‚) = âŸªÏ†1, Ï†2âŸ«_(SpaceTime â†’ â„‚) + âŸªÏ†1, Ï†3âŸ«_(SpaceTime â†’ â„‚) := by
  funext x
  simp [inner_apply]
  rw [_root_.inner_add_right]

end HiggsField

end
end StandardModel
