/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Dynamics.IsExtrema
import PhysLean.ClassicalMechanics.VectorFields
/-!

# The constant electric and magnetic fields

## i. Overview

In this module we define the electromagnetic potential which gives rise to a
given constant electric and magnetic field matrix.

We show that the kinetic term for this potential has a variational gradient equal to
zero, i.e. it satisfies the source-free Maxwell equations.

## ii. Key results

- `ElectromagneticPotential.constantEB Eâ‚€ Bâ‚€` : An electromagnetic potential which gives rise to a
  given constant electric field `Eâ‚€` and magnetic field `Bâ‚€` in 3d.
- `ElectromagneticPotential.constantEB_gradKineticTerm` : The variational gradient of the kinetic
  term for the potential `constantEB Eâ‚€ Bâ‚€` is equal to zero.

## iii. Table of contents

- A. The definition of the potential
- B. Smoothness of the potential
- C. The scalar potential
- D. The vector potential
- E. The electric field
- F. The magnetic field
- G. The kinetic term
- H. The variational gradient of the kinetic term

## iv. References

-/

namespace Electromagnetism
open Module realLorentzTensor
open IndexNotation
open TensorSpecies
open Tensor ContDiff

namespace ElectromagneticPotential

open TensorSpecies
open Tensor
open SpaceTime
open TensorProduct
open minkowskiMatrix
open InnerProductSpace
attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one

/-!

## A. The definition of the potential

The potential which gives rise to a constant electric field `Eâ‚€` and magnetic field `Bâ‚€` in 3d is
given by
`(- âŸªEâ‚€, xâŸ«, (1/2) Bâ‚€ Ã— x)`
where `x` is the spatial position vector.

-/
open Matrix
set_option linter.unusedVariables false
/-- An electric potential in 3d which gives a given constant E-field and B-field. -/
@[nolint unusedArguments]
noncomputable def constantEB {d : â„•} (c : SpeedOfLight)
    (Eâ‚€ : EuclideanSpace â„ (Fin d)) (Bâ‚€ : Fin d Ã— Fin d â†’ â„)
    (Bâ‚€_antisymm : âˆ€ i j, Bâ‚€ (i, j) = - Bâ‚€ (j, i)) : ElectromagneticPotential d :=
  fun x Î¼ =>
  match Î¼ with
  | Sum.inl _ => - (1/c) * âŸªEâ‚€, x.spaceâŸ«_â„
  | Sum.inr i => (1/2) * âˆ‘ j, Bâ‚€ (i, j) * x.space j

/-!

## B. Smoothness of the potential

The potential `constantEB Eâ‚€ Bâ‚€` is smooth.

-/

lemma constantEB_smooth {c : SpeedOfLight}
    {Eâ‚€ : EuclideanSpace â„ (Fin d)} {Bâ‚€ : Fin d Ã— Fin d â†’ â„}
    {Bâ‚€_antisymm : âˆ€ i j, Bâ‚€ (i, j) = - Bâ‚€ (j, i)} :
    ContDiff â„ âˆ (constantEB c Eâ‚€ Bâ‚€ Bâ‚€_antisymm) := by
  rw [contDiff_euclidean]
  intro Î¼
  match Î¼ with
  | Sum.inl _ =>
      simp [constantEB]
      apply ContDiff.neg
      apply ContDiff.mul
      Â· fun_prop
      apply ContDiff.inner
      Â· fun_prop
      Â· change ContDiff â„ âˆ SpaceTime.space
        fun_prop
  | Sum.inr i =>
      simp [constantEB]
      apply ContDiff.mul
      Â· fun_prop
      Â· apply ContDiff.sum
        intro j _
        apply ContDiff.mul
        Â· fun_prop
        fun_prop

/-!

## C. The scalar potential

The scalar potential for `constantEB Eâ‚€ Bâ‚€` is given by `-âŸªEâ‚€, xâŸ«`.

-/

lemma constantEB_scalarPotential {c : SpeedOfLight}
    {Eâ‚€ : EuclideanSpace â„ (Fin d)} {Bâ‚€ : Fin d Ã— Fin d â†’ â„}
    {Bâ‚€_antisymm : âˆ€ i j, Bâ‚€ (i, j) = - Bâ‚€ (j, i)} :
    (constantEB c Eâ‚€ Bâ‚€ Bâ‚€_antisymm).scalarPotential c = fun _ x => -âŸªEâ‚€, xâŸ«_â„ := by
  ext t x
  simp [scalarPotential, timeSlice, constantEB, space_toCoord_symm, Equiv.coe_fn_mk,
    Function.curry_apply, Function.comp_apply]


/-!

## D. The vector potential

The vector potential for `constantEB Eâ‚€ Bâ‚€` is given by `(1/2) Bâ‚€ Ã— x`.

-/

lemma constantEB_vectorPotential {c : SpeedOfLight}
    {Eâ‚€ : EuclideanSpace â„ (Fin d)} {Bâ‚€ : Fin d Ã— Fin d â†’ â„}
    {Bâ‚€_antisymm : âˆ€ i j, Bâ‚€ (i, j) = - Bâ‚€ (j, i)} :
    (constantEB c Eâ‚€ Bâ‚€ Bâ‚€_antisymm).vectorPotential c = fun _ x i =>
      (1 / 2) * âˆ‘ j, Bâ‚€ (i, j) * x j := by
  ext t x i
  simp [vectorPotential, timeSlice, constantEB, space_toCoord_symm, Equiv.coe_fn_mk,
    Function.curry_apply, Function.comp_apply]

/-!

### D.1. Time derivative of the vector potential

-/
open Time

@[simp]
lemma constantEB_vectorPotential_time_deriv {c : SpeedOfLight}
    {Eâ‚€ : EuclideanSpace â„ (Fin d)} {Bâ‚€ : Fin d Ã— Fin d â†’ â„}
    {Bâ‚€_antisymm : âˆ€ i j, Bâ‚€ (i, j) = - Bâ‚€ (j, i)} (t : Time) (x : Space d) :
    âˆ‚â‚œ ((constantEB c Eâ‚€ Bâ‚€ Bâ‚€_antisymm).vectorPotential c Â·  x) t = 0 := by
  rw [constantEB_vectorPotential]
  simp

/-!

### D.2. Space derivative of the vector potential

-/

lemma constantEB_vectorPotential_space_deriv {c : SpeedOfLight}
    {Eâ‚€ : EuclideanSpace â„ (Fin d)} {Bâ‚€ : Fin d Ã— Fin d â†’ â„}
    {Bâ‚€_antisymm : âˆ€ i j, Bâ‚€ (i, j) = - Bâ‚€ (j, i)} (t : Time) (x : Space d) (i j : Fin d):
    Space.deriv i ((constantEB c Eâ‚€ Bâ‚€ Bâ‚€_antisymm).vectorPotential c t Â· j) x =
       (1 / 2) *  Bâ‚€ (j, i) := by
  rw [constantEB_vectorPotential]
  rw [Space.deriv_eq]
  rw [fderiv_const_mul (by fun_prop)]
  rw [fderiv_fun_sum (by fun_prop)]
  simp
  rw [Finset.sum_eq_single i]
  Â· rw [fderiv_const_mul (by fun_prop)]
    simp [â† Space.deriv_eq]
  Â· intro k _ hk
    rw [fderiv_const_mul (by fun_prop)]
    simp [â† Space.deriv_eq]
    rw [Space.deriv_component_diff]
    simp
    exact id (Ne.symm hk)
  Â· simp


/-!

## E. The electric field

The electric field for `constantEB Eâ‚€ Bâ‚€` is given by `Eâ‚€`.

-/

@[simp]
lemma constantEB_electricField  {c : SpeedOfLight}
    {Eâ‚€ : EuclideanSpace â„ (Fin d)} {Bâ‚€ : Fin d Ã— Fin d â†’ â„}
    {Bâ‚€_antisymm : âˆ€ i j, Bâ‚€ (i, j) = - Bâ‚€ (j, i)} :
    (constantEB c Eâ‚€ Bâ‚€ Bâ‚€_antisymm).electricField c = fun _ _ => Eâ‚€ := by
  funext t x
  rw [electricField_eq]
  simp [constantEB_scalarPotential]
  erw [Space.grad_neg]
  rw [Space.grad_inner_right]
  simp

/-!

## F. The magnetic field

The magnetic field for `constantEB Eâ‚€ Bâ‚€` is given by `Bâ‚€`.

-/

@[simp]
lemma constantEB_magneticFieldMatrix {c : SpeedOfLight}
    {Eâ‚€ : EuclideanSpace â„ (Fin d)} {Bâ‚€ : Fin d Ã— Fin d â†’ â„}
    {Bâ‚€_antisymm : âˆ€ i j, Bâ‚€ (i, j) = - Bâ‚€ (j, i)} :
    (constantEB c Eâ‚€ Bâ‚€ Bâ‚€_antisymm).magneticFieldMatrix c = fun _ _ => Bâ‚€ := by
  funext t x
  funext i
  match i with
  | (i, j) =>
  rw [magneticFieldMatrix_eq_vectorPotential]
  rw [constantEB_vectorPotential_space_deriv, constantEB_vectorPotential_space_deriv]
  conv_lhs =>
    enter [2]
    rw [Bâ‚€_antisymm]
  ring
  apply constantEB_smooth.differentiable (by simp)

/-!

## G. Is extrema

-/

lemma constantEB_isExtrema {ğ“• : FreeSpace}
    {Eâ‚€ : EuclideanSpace â„ (Fin d)} {Bâ‚€ : Fin d Ã— Fin d â†’ â„}
    {Bâ‚€_antisymm : âˆ€ i j, Bâ‚€ (i, j) = - Bâ‚€ (j, i)} :
    IsExtrema ğ“• (constantEB ğ“•.c Eâ‚€ Bâ‚€ Bâ‚€_antisymm) 0 := by
  rw [isExtrema_iff_gauss_ampere_magneticFieldMatrix]
  Â· intro t x
    simp
  Â· exact constantEB_smooth
  Â· exact contDiff_zero_fun

end ElectromagneticPotential

end Electromagnetism
