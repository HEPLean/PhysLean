/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Mathematics.Distribution.Basic
import PhysLean.Meta.Linters.Sorry
import Mathlib.MeasureTheory.Constructions.HaarToSphere
/-!

# Distributions from bounded functions

In this module we define distributions from functions `f : EuclideanSpace â„ (Fin d.succ) â†’ F`
whose norm is bounded by `c1 * â€–xâ€– ^ (-d : â„) + c2 * â€–xâ€– ^ n`
for some constants `c1`, `c2` and `n`.

This gives a convenient way to construct distributions from functions, without needing
to reference the underlying Schwartz maps.

## Key definition

- `ofBounded`: Creates a distribution from a bounded function `f`.

## Method of definition

The `ofBounded` function is defined through two measures `invPowMeasure` and `powMeasure n`,
the first is the measure with density `1/â€–xâ€–áµˆ` and the second is the measure with density `â€–xâ€–^n`.
Both these measures are proven to have temperate growth, and can be used to define a distribution
by intergration.

We also define a `boundMeasure` which is a linear combination of these two measures.

-/
open SchwartzMap NNReal
noncomputable section

variable (ğ•œ : Type) {E F F' : Type} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup F']

namespace Distribution

variable [NormedSpace â„ E]

open MeasureTheory

/-!

## IsBounded

-/

/-- The boundedness condition on a function ` EuclideanSpace â„ (Fin dm1.succ) â†’ F`
  for it to form a distribution. -/
@[fun_prop]
def IsDistBounded {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F) : Prop :=
    âˆƒ c1 c2 c3 n, 0 â‰¤ c1 âˆ§ 0 â‰¤ c2 âˆ§ 0 â‰¤ c3 âˆ§
    âˆ€ x, â€–f xâ€– â‰¤ c1 * â€–xâ€– ^ (-dm1 : â„) + c2 * â€–xâ€– ^ n + c3

@[sorryful]
lemma IsDistBounded.comp_add_left {dm1 : â„•} {f : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    (hf : IsDistBounded f) (c : EuclideanSpace â„ (Fin dm1.succ)) :
    IsDistBounded (fun x => f (x + c)) := by
  sorry

@[fun_prop]
lemma IsDistBounded.const {dm1 : â„•} (f : F) :
    IsDistBounded (dm1 := dm1) (fun _ : EuclideanSpace â„ (Fin dm1.succ) => f) := by
  use 0, 0, â€–fâ€–, 0
  simp

@[fun_prop]
lemma IsDistBounded.const_smul {dm1 : â„•} [NormedSpace â„ F] {f : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    (hf : IsDistBounded f) (c : â„) : IsDistBounded (c â€¢ f) := by
  rcases hf with âŸ¨c1, c2, c3, n, c1_nonneg, c2_nonneg, c3_nonneg, hboundâŸ©
  use |c| * c1, |c| * c2, |c| * c3, n
  constructor
  Â· exact mul_nonneg (abs_nonneg c) c1_nonneg
  constructor
  Â· exact mul_nonneg (abs_nonneg c) c2_nonneg
  constructor
  Â· exact mul_nonneg (abs_nonneg c) c3_nonneg
  intro x
  specialize hbound x
  calc
    â€–(c â€¢ f) xâ€– = â€–câ€– * â€–f xâ€– := norm_smul _ _
    _ â‰¤ |c| * (c1 * â€–xâ€– ^ (-dm1 : â„) + c2 * â€–xâ€– ^ n + c3) := by
      apply mul_le_mul_of_nonneg_left hbound (abs_nonneg c)
    _ = |c| * c1 * â€–xâ€– ^ (-dm1 : â„) + |c| * c2 * â€–xâ€– ^ n + |c| * c3 := by
      ring

@[fun_prop]
lemma IsDistBounded.const_mul_fun {dm1 : â„•}
    {f : EuclideanSpace â„ (Fin dm1.succ) â†’ â„}
    (hf : IsDistBounded f) (c : â„) : IsDistBounded (fun x => c * f x) := by
  convert hf.const_smul c using 1

lemma IsDistBounded.congr {dm1 : â„•} {f : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    {g : EuclideanSpace â„ (Fin dm1.succ) â†’ F'}
    (hf : IsDistBounded f) (hfg : âˆ€ x, â€–g xâ€– = â€–f xâ€–) : IsDistBounded g := by
  rcases hf with âŸ¨c1, c2, c3, n, c1_nonneg, c2_nonneg, c3_nonneg, hboundâŸ©
  use c1, c2, c3, n
  refine âŸ¨c1_nonneg, c2_nonneg, c3_nonneg, ?_âŸ©
  intro x
  rw [hfg x]
  exact hbound x

lemma IsDistBounded.mono {dm1 : â„•} {f : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    {g : EuclideanSpace â„ (Fin dm1.succ) â†’ F'}
    (hf : IsDistBounded f) (hfg : âˆ€ x, â€–g xâ€– â‰¤ â€–f xâ€–) : IsDistBounded g := by
  rcases hf with âŸ¨c1, c2, c3, n, c1_nonneg, c2_nonneg, c3_nonneg, hboundâŸ©
  use c1, c2, c3, n
  constructor
  Â· exact c1_nonneg
  constructor
  Â· exact c2_nonneg
  constructor
  Â· exact c3_nonneg
  intro x
  specialize hbound x
  calc
    â€–g xâ€– â‰¤ â€–f xâ€– := hfg x
    _ â‰¤ c1 * â€–xâ€– ^ (-dm1 : â„) + c2 * â€–xâ€– ^ n + c3 := hbound

@[fun_prop]
lemma IsDistBounded.add {dm1 : â„•} {f g : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    (hf : IsDistBounded f) (hg : IsDistBounded g) : IsDistBounded (f + g) := by
  rcases hf with âŸ¨c1, c2, c3, n, c1_nonneg, c2_nonneg, c3_nonneg, hboundfâŸ©
  rcases hg with âŸ¨d1, d2, d3, m, d1_nonneg, d2_nonneg, d3_nonneg, hboundgâŸ©
  let n' := max n m
  use c1 + d1, c2 + d2, c3 + d3 + c2 + d2, n'
  have h1 (r : â„) (hr : 0 â‰¤ r) (n : â„•) (hn : n â‰¤ n') :
      r ^ n â‰¤ 1 + r ^ n' := by
    by_cases hr' : r â‰¤ 1
    Â· apply (pow_le_oneâ‚€ hr hr').trans
      have hr' : 0 â‰¤ r ^ n' := by positivity
      linarith
    Â· trans r ^ n'
      Â· refine Bound.pow_le_pow_right_of_le_one_or_one_le ?_
        left
        simp_all
        exact le_of_lt (by simpa using hr')
      Â· simp
  refine âŸ¨by positivity, by positivity, by positivity, ?_âŸ©
  intro x
  trans â€–f xâ€– + â€–g xâ€–
  Â· simpa using norm_add_le (f x) (g x)
  have hf' : â€–f xâ€– â‰¤ (c1 * â€–xâ€– ^ (- (dm1 : â„)) + c2 * (1 + â€–xâ€– ^ n') + c3) := by
    apply (hboundf x).trans
    refine add_le_add_three (by rfl) ?_ (by rfl)
    exact mul_le_mul_of_nonneg_left (h1 â€–xâ€– (norm_nonneg x) n (by simp [n'])) c2_nonneg
  have hg' : â€–g xâ€– â‰¤ (d1 * â€–xâ€– ^ (- (dm1 : â„)) + d2 * (1 + â€–xâ€– ^ n') + d3) :=
    (hboundg x).trans <| add_le_add_three (by rfl)
      (mul_le_mul_of_nonneg_left (h1 â€–xâ€– (norm_nonneg x) m (by simp [n'])) d2_nonneg) (by rfl)
  linarith

TODO "LSLHW" "The proof `IsDistBounded.pow` needs golfing."

@[fun_prop]
lemma IsDistBounded.pow {dm1 : â„•} (n : â„¤) (hn : - dm1 â‰¤ n) :
    IsDistBounded (dm1 := dm1) (fun x => â€–xâ€– ^ n) := by
  use 1, 1, 0, n.natAbs
  simp only [zero_le_one, le_refl, Nat.succ_eq_add_one, norm_zpow, norm_norm, Real.rpow_neg_natCast,
    zpow_neg, zpow_natCast, one_mul, add_zero, true_and]
  intro x
  have hx := norm_nonneg x
  generalize â€–xâ€– = r at *
  by_cases hr : r = 0
  Â· subst hr
    simp_all [zero_zpow_eq, zero_pow_eq]
    split_ifs <;> simp
  field_simp
  refine (le_div_iffâ‚€ (by positivity)).mpr ?_
  rw [â† pow_add]
  trans r ^ (n + dm1)
  Â· apply le_of_eq
    rw [zpow_add' (Or.inl hr), zpow_natCast]
  obtain âŸ¨m, hmâŸ© : âˆƒ (m : â„•), n + dm1 = m :=
    âŸ¨(n + dm1).natAbs, by rw [Int.natAbs_of_nonneg (by linarith)]âŸ©
  have m_lt : m â‰¤ n.natAbs + dm1 := Int.ofNat_le.mp <| by simpa [â† hm] using le_abs_self n
  generalize (n.natAbs + dm1) = n at *
  simp only [zpow_natCast, ge_iff_le, hm]
  by_cases hr' : r â‰¤ 1
  Â· exact (pow_le_oneâ‚€ hx hr').trans <|
      (le_add_iff_nonneg_right _).mpr <| pow_nonneg hx n
  Â· apply (Bound.pow_le_pow_right_of_le_one_or_one_le <| Or.inl <| âŸ¨le_of_not_ge hr', m_ltâŸ©).trans
    simp

@[fun_prop]
lemma IsDistBounded.inv {n : â„•} :
    IsDistBounded (dm1 := n.succ) (fun x => â€–xâ€–â»Â¹) := by
  convert IsDistBounded.pow (dm1 := n.succ) (-1) (by simp) using 1
  ext1 x
  simp
open InnerProductSpace

@[fun_prop]
lemma IsDistBounded.inner_left {dm1 n : â„•}
    {f : EuclideanSpace â„ (Fin dm1.succ) â†’ EuclideanSpace â„ (Fin n) }
    (hf : IsDistBounded f) (y : EuclideanSpace â„ (Fin n)) :
    IsDistBounded (fun x => âŸªf x, yâŸ«_â„) := by
  rcases hf with âŸ¨c1, c2, c3, n, c1_nonneg, c2_nonneg, c3_nonneg, hboundfâŸ©
  refine âŸ¨c1 * â€–yâ€–, c2 * â€–yâ€–, c3 * â€–yâ€–, n, by positivity, by positivity, by positivity, ?_âŸ©
  intro x
  apply (norm_inner_le_norm (f x) y).trans
  trans  (c1* â€–xâ€– ^ (-â†‘dm1 : â„) + c2 *  â€–xâ€– ^ n + c3) * â€–yâ€–
  Â· apply mul_le_mul_of_nonneg_right (hboundf x) (norm_nonneg y)
  Â· apply le_of_eq
    ring

/-!

## The measures.

-/

/-- The measure on `EuclideanSpace â„ (Fin 3)` weighted by `1 / â€–xâ€– ^ 2`. -/
def invPowMeasure {dm1 : â„•} : Measure (EuclideanSpace â„ (Fin dm1.succ)) :=
  volume.withDensity (fun x : EuclideanSpace â„ (Fin dm1.succ) => ENNReal.ofReal (1 / â€–xâ€– ^ dm1))

/-- The measure on `EuclideanSpace â„ (Fin 3)` weighted by `â€–xâ€– ^ n`. -/
def powMeasure {dm1 : â„•} (n : â„•) : Measure (EuclideanSpace â„ (Fin dm1.succ)) :=
  volume.withDensity (fun x : EuclideanSpace â„ (Fin dm1.succ) =>
    ENNReal.ofReal (â€–xâ€– ^ n))

/-- The measure on `EuclideanSpace â„ (Fin 3)` given by `C1 â€¢ invPowMeasure + C2 â€¢ powMeasure n`,
  for constants `C1` and `C2`. -/
def boundMeasure {dm1 : â„•} (n : â„•) (C1 C2 C3 : â„) :
    Measure (EuclideanSpace â„ (Fin dm1.succ)) :=
  (ENNReal.ofReal C1) â€¢ invPowMeasure +
  (ENNReal.ofReal C2) â€¢ powMeasure n +
  (ENNReal.ofReal C3) â€¢ volume

/-!

## Integrable conditions for the measures.

-/

variable [NormedSpace â„ F]

lemma integrable_invPow_of_boundMeasure {dm1 : â„•} (n : â„•) (C1 C2 C3 : â„) (C1_nonneg : 0 â‰¤ C1)
    (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F) (h : Integrable f (boundMeasure n C1 C2 C3)) :
    Integrable (fun x => C1 â€¢ (1/â€–xâ€–^dm1) â€¢ f x) := by
  simp [boundMeasure] at h
  by_cases hC1 : C1 = 0
  Â· subst hC1
    simp
  refine Integrable.essSup_smul ?_ ?_ ?_
  Â· have h1 := h.1.1
    rw [integrable_smul_measure] at h1
    erw [integrable_withDensity_iff_integrable_smulâ‚€] at h1
    refine (integrable_congr ?_).mp h1
    filter_upwards with x
    refine Eq.symm (Mathlib.Tactic.LinearCombination.smul_eq_const ?_ (f x))
    simp only [one_div, RingHom.toMonoidHom_eq_coe, MonoidHom.coe_coe, coe_toRealHom,
      Real.coe_toNNReal', inv_nonneg, norm_nonneg, pow_nonneg, sup_of_le_left]
    fun_prop
    simp only [ne_eq, ENNReal.ofReal_eq_zero, not_le]
    positivity
    simp
  Â· fun_prop
  Â· simp

lemma integrable_pow_of_boundMeasure {dm1 : â„•} (n : â„•) (C1 C2 C3 : â„) (C2_nonneg : 0 â‰¤ C2)
    (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F) (h : Integrable f (boundMeasure n C1 C2 C3)) :
    Integrable (fun x => C2 â€¢ â€–xâ€– ^ n â€¢ f x) volume := by
  simp [boundMeasure] at h
  by_cases hC2 : C2 = 0
  Â· subst hC2
    simp
  refine Integrable.essSup_smul ?_ ?_ ?_
  Â· have h1 := h.1.2
    rw [integrable_smul_measure] at h1
    erw [integrable_withDensity_iff_integrable_smulâ‚€] at h1
    refine (integrable_congr ?_).mp h1
    filter_upwards with x
    refine Eq.symm (Mathlib.Tactic.LinearCombination.smul_eq_const ?_ (f x))
    simp only [RingHom.toMonoidHom_eq_coe, MonoidHom.coe_coe, coe_toRealHom, Real.coe_toNNReal',
      norm_nonneg, pow_nonneg, sup_of_le_left]
    fun_prop
    simp only [ne_eq, ENNReal.ofReal_eq_zero, not_le]
    positivity
    simp
  Â· fun_prop
  Â· simp

lemma integrable_const_of_boundMeasure {dm1 : â„•} (n : â„•) (C1 C2 C3 : â„) (C3_nonneg : 0 â‰¤ C3)
    (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F) (h : Integrable f (boundMeasure n C1 C2 C3)) :
    Integrable (fun x => C3 â€¢ f x) volume:= by
  simp [boundMeasure] at h
  by_cases hC3 : C3 = 0
  Â· subst hC3
    simp
  have h3 := h.2
  rw [integrable_smul_measure] at h3
  refine Integrable.essSup_smul h3 ?_ ?_
  Â· fun_prop
  Â· simp
  Â· simp
    positivity
  Â· simp

lemma integrable_boundMeasure {dm1 : â„•} (n : â„•) (C1 C2 C3 : â„) (C1_nonneg : 0 â‰¤ C1)
    (C2_nonneg : 0 â‰¤ C2) (C3_nonneg : 0 â‰¤ C3)
    (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F) (h : Integrable f (boundMeasure n C1 C2 C3)) :
    Integrable (fun x => (C1 * (1/â€–xâ€–^dm1) + C2 * â€–xâ€–^n + C3) â€¢ f x) := by
  conv =>
    enter [1, x]
    rw [add_smul, add_smul, â† smul_smul, â† smul_smul]
  apply Integrable.add
  apply Integrable.add
  Â· exact integrable_invPow_of_boundMeasure n C1 C2 C3 C1_nonneg f h
  Â· exact integrable_pow_of_boundMeasure n C1 C2 C3 C2_nonneg f h
  Â· exact integrable_const_of_boundMeasure n C1 C2 C3 C3_nonneg f h

/-!

## Integrals with respect to the measures.

-/

lemma integral_invPowMeasure {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F) :
    âˆ« x, f x âˆ‚invPowMeasure = âˆ« x, (1 / â€–xâ€–^dm1) â€¢ f x := by
  dsimp [invPowMeasure]
  erw [integral_withDensity_eq_integral_smul (by fun_prop)]
  congr
  funext x
  simp only [one_div]
  refine Eq.symm (Mathlib.Tactic.LinearCombination.smul_eq_const ?_ (f x))
  simp

lemma integral_powMeasure {dm1 : â„•} (n : â„•) (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F) :
    âˆ« x, f x âˆ‚(powMeasure n) = âˆ« x, (â€–xâ€– ^ n) â€¢ f x := by
  dsimp [powMeasure]
  erw [integral_withDensity_eq_integral_smul (by fun_prop)]
  congr
  funext x
  refine Eq.symm (Mathlib.Tactic.LinearCombination.smul_eq_const ?_ (f x))
  simp

lemma integral_boundMeasure {dm1 : â„•} (n : â„•) (C1 C2 C3 : â„) (C1_nonneg : 0 â‰¤ C1)
    (C2_nonneg : 0 â‰¤ C2) (C3_nonneg : 0 â‰¤ C3)
    (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F)
    (hf : Integrable f (boundMeasure n C1 C2 C3)) :
    âˆ« x, f x âˆ‚(boundMeasure n C1 C2 C3) = âˆ« x, (C1 * 1/â€–xâ€–^dm1 + C2 * â€–xâ€–^n + C3) â€¢ f x := by
  let hf' := hf
  dsimp [boundMeasure] at âŠ¢ hf
  rw [integrable_add_measure] at hf
  rw [MeasureTheory.integral_add_measure hf.1 hf.2]
  rw [integrable_add_measure] at hf
  rw [MeasureTheory.integral_add_measure hf.1.1 hf.1.2]
  rw [integral_smul_measure, â† integral_smul, integral_smul_measure, â† integral_smul,
    integral_smul_measure, â† integral_smul]
  rw [integral_invPowMeasure, integral_powMeasure]
  rw [â† integral_add, â† integral_add]
  Â· congr
    funext x
    rw [ENNReal.toReal_ofReal C1_nonneg, ENNReal.toReal_ofReal C2_nonneg,
      ENNReal.toReal_ofReal C3_nonneg]
    rw [add_smul, add_smul, smul_smul, smul_smul]
    ring_nf
  apply Integrable.add
  Â· convert integrable_invPow_of_boundMeasure n C1 C2 C3 C1_nonneg f hf' using 1
    funext x
    simp [ENNReal.toReal_ofReal C1_nonneg]
    rw [smul_comm]
  Â· convert integrable_pow_of_boundMeasure n C1 C2 C3 C2_nonneg f hf' using 1
    funext x
    simp [ENNReal.toReal_ofReal C2_nonneg]
    rw [smul_comm]
  Â· convert integrable_const_of_boundMeasure n C1 C2 C3 C3_nonneg f hf' using 1
    funext x
    simp [ENNReal.toReal_ofReal C3_nonneg]
  Â· convert integrable_invPow_of_boundMeasure n C1 C2 C3 C1_nonneg f hf' using 1
    funext x
    simp [ENNReal.toReal_ofReal C1_nonneg]
    rw [smul_comm]
  Â· convert integrable_pow_of_boundMeasure n C1 C2 C3 C2_nonneg f hf' using 1
    funext x
    simp [ENNReal.toReal_ofReal C2_nonneg]
    rw [smul_comm]

/-!

## HasTemperateGrowth of measures

-/

private lemma integrable_neg_pow_on_ioi (n : â„•) :
    IntegrableOn (fun x : â„ => (|((1 : â„) + â†‘x) ^ (- (n + 2) : â„)|)) (Set.Ioi 0) := by
  rw [MeasureTheory.integrableOn_iff_comap_subtypeVal]
  rw [â† MeasureTheory.integrable_smul_measure (c := n + 1)]
  apply MeasureTheory.integrable_of_integral_eq_one
  trans (n + 1) * âˆ« (x : â„) in Set.Ioi 0, ((1 + x) ^ (- (n + 2) : â„)) âˆ‚volume
  Â· rw [â† MeasureTheory.integral_subtype_comap]
    simp only [neg_add_rev, Function.comp_apply, integral_smul_measure, smul_eq_mul]
    congr
    funext x
    simp only [abs_eq_self]
    apply Real.rpow_nonneg
    apply add_nonneg
    Â· exact zero_le_one' â„
    Â· exact le_of_lt x.2
    exact measurableSet_Ioi
  have h0 (x : â„) (hx : x âˆˆ Set.Ioi 0) : ((1 : â„) + â†‘x) ^ (- (n + 2) : â„) =
      ((1 + x) ^ ((n + 2)))â»Â¹ := by
    rw [â† Real.rpow_natCast]
    rw [â† Real.inv_rpow]
    rw [â† Real.rpow_neg_one, â† Real.rpow_mul]
    simp only [neg_mul, one_mul]
    simp only [neg_add_rev, Nat.cast_add, Nat.cast_ofNat]
    have hx : 0 < x := hx
    positivity
    apply add_nonneg
    Â· exact zero_le_one' â„
    Â· exact le_of_lt hx
  trans (n + 1) * âˆ« (x : â„) in Set.Ioi 0, ((1 + x) ^ (n + 2))â»Â¹ âˆ‚volume
  Â· congr 1
    refine setIntegral_congr_aeâ‚€ ?_ ?_
    Â· simp
    filter_upwards with x hx
    rw [h0 x hx]
  trans (n + 1) * âˆ« (x : â„) in Set.Ioi 1, (x ^ (n + 2))â»Â¹ âˆ‚volume
  Â· congr 1
    let f := fun x : â„ => (x ^ (n + 2))â»Â¹
    change âˆ« (x : â„) in Set.Ioi 0, f (1 + x) âˆ‚volume = âˆ« (x : â„) in Set.Ioi 1, f x âˆ‚volume
    let fa := fun x : â„ => 1 + x
    change âˆ« (x : â„) in Set.Ioi 0, f (fa x) âˆ‚volume = _
    rw [â† MeasureTheory.MeasurePreserving.setIntegral_image_emb (Î½ := volume)]
    simp [fa]
    simp [fa]
    exact measurePreserving_add_left volume 1
    simp [fa]
    exact measurableEmbedding_addLeft 1
  Â· trans (n + 1) * âˆ« (x : â„) in Set.Ioi 1, (x ^ (- (n + 2) : â„)) âˆ‚volume
    Â· congr 1
      refine setIntegral_congr_aeâ‚€ ?_ ?_
      Â· simp
      filter_upwards with x hx
      have hx : 1 < x := hx
      rw [â† Real.rpow_natCast]
      rw [â† Real.inv_rpow]
      rw [â† Real.rpow_neg_one, â† Real.rpow_mul]
      simp only [neg_mul, one_mul]
      simp only [Nat.cast_add, Nat.cast_ofNat, neg_add_rev]
      positivity
      positivity

    rw [integral_Ioi_rpow_of_lt]
    field_simp
    have h0 : (-2 + -(n : â„) + 1) â‰  0 := by
      by_contra h
      have h1 : (1 : â„) - 0 = 2 + n := by
        rw [â† h]
        ring
      simp at h1
      linarith
    field_simp
    ring
    linarith
    linarith
  Â· simp
  Â· simp
  Â· simp

lemma invPowMeasure_integrable_pow_neg_two {dm1 : â„•} :
    Integrable (fun x : EuclideanSpace â„ (Fin dm1.succ) => (1 + â€–xâ€–) ^ (- (dm1 + 2) : â„))
      invPowMeasure := by
  simp [invPowMeasure]
  rw [MeasureTheory.integrable_withDensity_iff]
  swap
  Â· fun_prop
  swap
  Â· rw [@ae_iff]
    simp
  conv =>
    enter [1, i]
    rw [ENNReal.toReal_ofReal (by positivity)]
  have h1 := (MeasureTheory.Measure.measurePreserving_homeomorphUnitSphereProd
    (volume (Î± := EuclideanSpace â„ (Fin dm1.succ))))
  have h2 : IntegrableOn (fun x : EuclideanSpace â„ (Fin dm1.succ) =>
      ((1 + â€–xâ€–) ^ (- (dm1 + 2) : â„)) * (â€–xâ€– ^ dm1)â»Â¹) {0}á¶œ := by
    rw [MeasureTheory.integrableOn_iff_comap_subtypeVal]
    swap
    Â· refine MeasurableSet.compl_iff.mpr ?_
      simp
    let f := (fun x : EuclideanSpace â„ (Fin dm1.succ) =>
        ((1 + â€–xâ€–) ^ (- (dm1 + 2) : â„)) * (â€–xâ€– ^ dm1)â»Â¹)
      âˆ˜ @Subtype.val (EuclideanSpace â„ (Fin dm1.succ)) fun x =>
        (@Membership.mem (EuclideanSpace â„ (Fin dm1.succ))
          (Set (EuclideanSpace â„ (Fin dm1.succ))) Set.instMembership {0}á¶œ x)
    have hf : (f âˆ˜ (homeomorphUnitSphereProd (EuclideanSpace â„ (Fin dm1.succ))).symm)âˆ˜
      (homeomorphUnitSphereProd (EuclideanSpace â„ (Fin dm1.succ))) = f := by
      funext x
      simp
    change Integrable f _
    rw [â† hf]
    refine (h1.integrable_comp_emb ?_).mpr ?_
    Â· exact Homeomorph.measurableEmbedding
        (homeomorphUnitSphereProd (EuclideanSpace â„ (Fin dm1.succ)))
    haveI sfinite : SFinite (@Measure.comap â†‘(Set.Ioi 0) â„ Subtype.instMeasurableSpace
        Real.measureSpace.toMeasurableSpace Subtype.val volume) := by
      refine { out' := ?_ }
      have h1 := SFinite.out' (Î¼ := volume (Î± := â„))
      obtain âŸ¨m, hâŸ© := h1
      use fun n => Measure.comap Subtype.val (m n)
      apply And.intro
      Â· intro n
        refine (isFiniteMeasure_iff (Measure.comap Subtype.val (m n))).mpr ?_
        rw [MeasurableEmbedding.comap_apply]
        simp only [Set.image_univ, Subtype.range_coe_subtype, Set.mem_Ioi]
        have hm := h.1 n
        exact measure_lt_top (m n) {x | 0 < x}
        exact MeasurableEmbedding.subtype_coe measurableSet_Ioi
      Â· ext s hs
        rw [MeasurableEmbedding.comap_apply, Measure.sum_apply]
        conv_rhs =>
          enter [1, i]
          rw [MeasurableEmbedding.comap_apply (MeasurableEmbedding.subtype_coe measurableSet_Ioi)]
        have h2 := h.2
        rw [Measure.ext_iff'] at h2
        rw [â† Measure.sum_apply]
        exact h2 (Subtype.val '' s)
        refine MeasurableSet.subtype_image measurableSet_Ioi hs
        exact hs
        exact MeasurableEmbedding.subtype_coe measurableSet_Ioi
    have hf' : (f âˆ˜ (homeomorphUnitSphereProd (EuclideanSpace â„ (Fin dm1.succ))).symm) =
      fun x =>((1 + x.2.val) ^ (- (dm1 + 2) : â„)) * (x.2.val ^ dm1)â»Â¹ := by
      funext x
      simp only [Function.comp_apply, homeomorphUnitSphereProd_symm_apply_coe, f]
      rw [norm_smul]
      simp only [Real.norm_eq_abs, norm_eq_of_mem_sphere, mul_one]
      rw [abs_of_nonneg (le_of_lt x.2.2)]
    rw [hf']
    simp [Measure.volumeIoiPow]
    rw [MeasureTheory.prod_withDensity_right]
    swap
    Â· fun_prop
    rw [MeasureTheory.integrable_withDensity_iff]
    swap
    Â· refine Measurable.ennreal_ofReal ?_
      refine Measurable.pow_const ?_ dm1
      apply Measurable.comp
      Â· exact measurable_subtype_coe
      Â· exact measurable_snd
    swap
    Â· apply Filter.Eventually.of_forall
      intro x
      exact ENNReal.ofReal_lt_top
    have hf'' : (fun (x : â†‘(Metric.sphere (Î± := (EuclideanSpace â„ (Fin dm1.succ))) 0 1) Ã—
        â†‘(Set.Ioi (Î± := â„) 0)) =>
        (((1 + x.2.val) ^ (- (dm1 + 2) : â„)) * (x.2.val ^ dm1)â»Â¹ *
          (ENNReal.ofReal (â†‘x.2.val ^ dm1)).toReal))
        = fun x => ((1 + x.2.val) ^ (- (dm1 + 2) : â„)) := by
      funext x
      rw [ENNReal.toReal_ofReal]
      generalize (1 + â†‘x.2.val)= l
      ring_nf
      have h2 : x.2.val â‰  0 := by
        have h' := x.2.2
        simp [- Subtype.coe_prop] at h'
        linarith
      field_simp
      ring_nf
      field_simp
      exact pow_nonneg (le_of_lt x.2.2) dm1
    simp at hf''
    rw [hf'']
    apply (MeasureTheory.integrable_prod_iff' ?_).mpr ?_
    Â· refine aestronglyMeasurable_iff_aemeasurable.mpr ?_
      fun_prop
    Â· simp
      apply Integrable.const_mul
      have h1 := integrable_neg_pow_on_ioi dm1
      rw [MeasureTheory.integrableOn_iff_comap_subtypeVal] at h1
      simpa using h1
      exact measurableSet_Ioi
  rw [â† MeasureTheory.integrableOn_univ]
  simp at h2
  apply MeasureTheory.IntegrableOn.congr_set_ae h2
  rw [â† MeasureTheory.ae_eq_set_compl]
  trans (âˆ… : Set (EuclideanSpace â„ (Fin dm1.succ)))
  Â· apply Filter.EventuallyEq.of_eq
    rw [â† Set.compl_empty]
    exact compl_compl _
  Â· symm
    simp

instance (dm1 : â„•) : Measure.HasTemperateGrowth (invPowMeasure (dm1 := dm1)) where
  exists_integrable := by
    use dm1 + 2
    simpa using invPowMeasure_integrable_pow_neg_two (dm1 := dm1)

instance (dm1 : â„•) (n : â„•) : Measure.HasTemperateGrowth (powMeasure (dm1 := dm1) n) where
  exists_integrable := by
    let m := (volume (Î± := EuclideanSpace â„ (Fin dm1.succ))).integrablePower
    use (m + n)
    simp only [powMeasure]
    rw [MeasureTheory.integrable_withDensity_iff]
    have h1 : (fun x : EuclideanSpace â„ (Fin dm1.succ) =>
        (1 + â€–xâ€–) ^ (- (m + n : â„)) * (ENNReal.ofReal (â€–xâ€– ^ n)).toReal)
      = fun x => â€–xâ€– ^ n * â€–(1 + â€–xâ€–) ^ (-(m + n : â„))â€– := by
      funext x
      simp only [neg_add_rev, norm_nonneg, pow_nonneg, ENNReal.toReal_ofReal, Real.norm_eq_abs]
      rw [abs_of_nonneg (by positivity)]
      ring
    simp only [Nat.cast_add, neg_add_rev, norm_nonneg, pow_nonneg, ENNReal.toReal_ofReal]
    conv_lhs at h1 =>
      simp only [neg_add_rev, norm_nonneg, pow_nonneg, ENNReal.toReal_ofReal]
    rw [h1]
    apply integrable_of_le_of_pow_mul_le (Câ‚ := 1) (Câ‚‚ := 1)
    Â· intro x
      simp only [neg_add_rev, Real.norm_eq_abs]
      rw [abs_of_nonneg (by positivity)]
      refine Real.rpow_le_one_of_one_le_of_nonpos ?_ ?_
      Â· rw [@le_add_iff_nonneg_right]
        exact norm_nonneg x
      Â· refine neg_add_nonpos_iff.mpr ?_
        refine neg_le_iff_add_nonneg.mpr ?_
        positivity
    Â· intro x
      simp [- neg_add_rev, Real.norm_eq_abs]
      have h1 : (1 + â€–xâ€–) ^ (-((m : â„) + â†‘n)) = ((1 + â€–xâ€–) ^ (m + n))â»Â¹ := by
        have h0 : (1 + â€–xâ€–) ^ (m + n) = (1 + â€–xâ€–) ^ (m + n : â„) := by
          rw [â† Real.rpow_natCast]
          simp
        rw [h0]
        rw [â† Real.inv_rpow]
        rw [â† Real.rpow_neg_one, â† Real.rpow_mul]
        simp only [neg_add_rev, neg_mul, one_mul]
        positivity
        positivity
      rw [h1]
      rw [abs_of_nonneg (by positivity)]
      refine mul_inv_le_one_of_leâ‚€ ?_ ?_
      simp [m]
      conv_lhs => enter [2]; rw [add_comm]
      by_cases hm : m + n = 0
      Â· erw [hm]
        simp
      refine (pow_le_pow_iff_leftâ‚€ ?_ ?_ hm).mpr ?_
      Â· exact norm_nonneg x
      Â· positivity
      Â· refine le_add_of_nonneg_left ?_
        exact zero_le_one' â„
      Â· positivity
    Â· refine Continuous.aestronglyMeasurable ?_
      apply Continuous.rpow_const
      Â· fun_prop
      Â· intro x
        left
        simp only [ne_eq]
        have h1 : 0 < 1 + â€–xâ€– := by
          positivity
        by_contra hn
        rw [hn] at h1
        simp at h1
    Â· fun_prop
    Â· filter_upwards with x
      simp

instance (dm1 : â„•) (n : â„•) (C1 C2 C3 : â„) :
    Measure.HasTemperateGrowth (boundMeasure (dm1 := dm1) n C1 C2 C3) where
  exists_integrable := by
    /- We show that `boundMeasure (dm1 := dm1) n C1 C2 C3` has temperate growth. -/
    let m1 := (invPowMeasure (dm1 := dm1)).integrablePower
    let m2 := (powMeasure (dm1 := dm1) n).integrablePower
    let m3 := (volume (Î± := EuclideanSpace â„ (Fin dm1.succ))).integrablePower
    let mM := max (max m1 m2) m3
    /- We want to show that `(1 + â€–xâ€–) ^ (-â†‘mM)` is integrable over
      `boundMeasure (dm1 := dm1) n C1 C2 C3`. -/
    use mM
    /- We split it into integrability over the indivdual parts -/
    simp [boundMeasure]
    refine âŸ¨âŸ¨Integrable.smul_measure ?_ (by simp), Integrable.smul_measure ?_ (by simp)âŸ©,
      Integrable.smul_measure ?_ (by simp)âŸ©
    all_goals
      /- Integrability over the indivdual parts. -/
      apply MeasureTheory.Integrable.mono (integrable_pow_neg_integrablePower _)
      Â· refine Continuous.aestronglyMeasurable (Continuous.invâ‚€ (by fun_prop) ?_)
        intro x
        refine pow_ne_zero mM (fun hn => ?_)
        have hx : Â¬ (1 : â„) â‰¤ (0 : â„) := by simp
        apply hx
        rw [â† hn]
        simp
      Â· filter_upwards with x
        simp only [norm_inv, norm_pow, Real.norm_eq_abs, Real.rpow_neg_natCast, zpow_neg,
          zpow_natCast]
        refine inv_pow_le_inv_pow_of_le ?_ ?_
        Â· trans |1|
          Â· simp
          refine abs_le_abs_of_nonneg (zero_le_one' â„) ?_
          simp
        Â· simp [mM, m1, m2, m3]
/-!


## Integrability

-/

@[fun_prop]
lemma IsDistBounded.integrable {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume)
    (Î· : ğ“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) :
    Integrable (fun x : EuclideanSpace â„ (Fin dm1.succ) => Î· x â€¢ f x) := by
  obtain âŸ¨c1, c2, c3, n, c1_nonneg, c2_nonneg, c3_nonneg, hboundâŸ© := hf
  apply Integrable.mono (g := fun x => Î· x * (c1 * â€–xâ€– ^ (-dm1 : â„) + c2 * â€–xâ€–^n + c3))
  Â· convert integrable_boundMeasure n c1 c2 c3 c1_nonneg c2_nonneg c3_nonneg Î· Î·.integrable using 1
    funext x
    simp only [Nat.succ_eq_add_one, Real.rpow_neg_natCast, zpow_neg, zpow_natCast, one_div,
      smul_eq_mul]
    ring
  Â· fun_prop
  Â· filter_upwards with x
    simp [norm_smul]
    refine mul_le_mul_of_nonneg (by rfl) ((hbound x).trans ?_) (abs_nonneg _) (abs_nonneg _)
    simpa using le_abs_self (c1 * (â€–xâ€– ^ dm1)â»Â¹ + c2 * â€–xâ€– ^ n + c3)

@[fun_prop]
lemma IsDistBounded.integrable_mul {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ â„)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume)
    (Î· : ğ“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) :
    Integrable (fun x : EuclideanSpace â„ (Fin dm1.succ) => Î· x * f x) := by
  convert hf.integrable f hae Î· using 1

@[fun_prop]
lemma IsDistBounded.integrable_fderviv_schwartzMap_mul {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ â„)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume)
    (Î· : ğ“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) (y : EuclideanSpace â„ (Fin dm1.succ)):
    Integrable (fun x : EuclideanSpace â„ (Fin dm1.succ) => fderiv â„ Î· x y * f x) := by
  exact hf.integrable f hae (((SchwartzMap.evalCLM (ğ•œ := â„) y) ((fderivCLM â„) Î·)))


/-!

## Bounded functions as distributions

-/

/-- A distribution `(EuclideanSpace â„ (Fin 3)) â†’d[â„] F` from a function
  `f : EuclideanSpace â„ (Fin 3) â†’ F` bounded by `c1 * â€–xâ€– ^ (-2 : â„) + c2 * â€–xâ€– ^ n`.
-/
def ofBounded {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume) :
    (EuclideanSpace â„ (Fin dm1.succ)) â†’d[â„] F := by
  refine mkCLMtoNormedSpace (fun Î· => âˆ« x, Î· x â€¢ f x) ?_ ?_ ?_
  Â· intro Î· Îº
    simp only [add_apply]
    conv_lhs =>
      enter [2, a]
      rw [add_smul]
    rw [integral_add]
    Â· exact hf.integrable f hae Î·
    Â· exact hf.integrable f hae Îº
  Â· intro a Î·
    simp only [smul_apply, smul_eq_mul, RingHom.id_apply]
    conv_lhs =>
      enter [2, a]
      rw [â† smul_smul]
    rw [integral_smul]
  obtain âŸ¨c1, c2, c3, r, c1_nonneg, c2_nonneg, c3_nonneg, hboundâŸ© := hf
  haveI hÎ¼ : (boundMeasure (dm1 := dm1) r c1 c2 c3).HasTemperateGrowth := by infer_instance
  rcases hÎ¼.exists_integrable with âŸ¨n, hâŸ©
  let m := (n, 0)
  use Finset.Iic m, 2 ^ n * âˆ« x, (1 + â€–xâ€–) ^ (- (n : â„)) âˆ‚(boundMeasure (dm1 := dm1) r c1 c2 c3)
  refine âŸ¨by positivity, fun Î· â†¦ (norm_integral_le_integral_norm _).trans ?_âŸ©
  trans âˆ« x, â€–Î· xâ€– âˆ‚(boundMeasure r c1 c2 c3)
  Â· have h1 : Integrable (fun x => Î· x) (boundMeasure r c1 c2 c3) := by
        exact integrable Î·
    have h2 : Integrable (fun x => â€–Î· xâ€–) (boundMeasure r c1 c2 c3) := by
        exact Integrable.norm h1
    rw [integral_boundMeasure]
    refine integral_mono_of_nonneg ?_ ?_ ?_
    Â· filter_upwards with x
      positivity
    Â· dsimp
      apply (integrable_congr ?_).mp
        (integrable_boundMeasure r c1 c2 c3 c1_nonneg c2_nonneg c3_nonneg (fun x => â€–Î· xâ€–) h2)
      filter_upwards with x
      simp only [one_div, Real.norm_eq_abs, smul_eq_mul, mul_one, mul_eq_mul_right_iff,
        add_left_inj, abs_eq_zero]
      left
      exact rfl
    Â· filter_upwards with x
      rw [norm_smul, mul_comm]
      simp only [Real.norm_eq_abs, mul_one, smul_eq_mul]
      refine mul_le_mul_of_nonneg ?_ ?_ ?_ ?_
      Â· apply (hbound x).trans
        apply le_of_eq
        simp
        rfl
      Â· rfl
      Â· positivity
      Â· positivity
    Â· exact c1_nonneg
    Â· exact c2_nonneg
    Â· exact c3_nonneg
    Â· exact h2
  have h' : âˆ€ x, â€–Î· xâ€– â‰¤ (1 + â€–xâ€–) ^ (-(n : â„)) *
      (2 ^ n * ((Finset.Iic m).sup (fun m' => SchwartzMap.seminorm â„ m'.1 m'.2) Î·)) := by
    intro x
    rw [Real.rpow_neg (by positivity), â† div_eq_inv_mul,
      le_div_iffâ‚€' (by positivity), Real.rpow_natCast]
    simpa using one_add_le_sup_seminorm_apply (m := m) (k := n) (n := 0) le_rfl le_rfl Î· x
  apply (integral_mono
    (by simpa using Î·.integrable_pow_mul ((boundMeasure r c1 c2 c3)) 0) _ h').trans
  Â· unfold schwartzSeminormFamily
    rw [integral_mul_const, â† mul_assoc, mul_comm (2 ^ n)]
  apply h.mul_const

lemma ofBounded_apply {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume) (Î· : ğ“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) :
    ofBounded f hf hae Î· = âˆ« x, Î· x â€¢ f x := rfl

@[simp]
lemma ofBounded_zero_eq_zero {dm1 : â„•} :
    ofBounded (fun _ : EuclideanSpace â„ (Fin (dm1 + 1)) => (0 : F))
      âŸ¨0, 0, 0, 0, by simpâŸ© (by fun_prop) = 0 := by
  ext Î·
  simp [ofBounded_apply]

TODO "LQX64" "Show that the creation of a distribution
  from a bounded function via `ofBounded` is linear on adding two bounded functions."

lemma ofBounded_smul {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume) (c : â„) :
    ofBounded (c â€¢ f) (by fun_prop) (by fun_prop) = c â€¢ ofBounded f hf hae := by
  ext Î·
  change _ = c â€¢ âˆ« x, Î· x â€¢ f x
  rw [ofBounded_apply]
  simp only [Nat.succ_eq_add_one, Pi.smul_apply]
  rw [â† integral_smul]
  congr
  funext x
  rw [smul_comm]

lemma ofBounded_smul_fun {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume) (c : â„) :
    ofBounded (fun x => c â€¢ f x) (by
      change IsDistBounded (c â€¢ f)
      fun_prop) (by fun_prop) = c â€¢ ofBounded f hf hae := by
  ext Î·
  change _ = c â€¢ âˆ« x, Î· x â€¢ f x
  rw [ofBounded_apply]
  simp only [Nat.succ_eq_add_one]
  rw [â† integral_smul]
  congr
  funext x
  rw [smul_comm]

open InnerProductSpace

lemma ofBounded_inner {dm1 n : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’  EuclideanSpace â„ (Fin n))
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume)
    (Î· : ğ“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) (y :  EuclideanSpace â„ (Fin n)) :
    âŸªofBounded f hf hae Î·, yâŸ«_â„ = âˆ« x, Î· x * âŸªf x, yâŸ«_â„ := by
  rw [ofBounded_apply]
  trans âˆ« x,  âŸªy, Î· x â€¢ f xâŸ«_â„; swap
  Â· congr
    funext x
    rw [real_inner_comm]
    simp [inner_smul_left]
  rw [integral_inner, real_inner_comm]
  fun_prop

end Distribution
