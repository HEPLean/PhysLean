/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.ChargeSpectrum.MinimallyAllowsTerm.OfFinset
import PhysLean.StringTheory.FTheory.SU5.Fluxes.NoExotics.Completeness
/-!

# Quanta of 10d representations

The 10d representations of the `SU(5)Ã—U(1)` carry
the quantum numbers of their U(1) charges and their fluxes.

In this module we define the data structure for these quanta and
properties thereof.

##Â Key definitions

- `TenQuanta` is the type of quanta of 10d representations.
- `TenQuanta.toFluxesTen` is the underlying `FluxesTen` of a `TenQuanta`.
- `TenQuanta.toCharges` is the underlying Multiset charges of a `TenQuanta`.
- `TenQuanta.reduce` is the reduction of a `TenQuanta` which adds together
  all the fluxes corresponding to the same charge (i.e. representation).
- `TenQuanta.anomalyCoefficent` is the anomaly coefficent associated with a `TenQuanta`.
- `TenQuanta.ofChargesExpand` is the `TenQuanta` with fluxes
  `{(1, 0), (1, 0), (1, 0)}` or `{(1, 1), (1, -1), (1, 0)}` and finite set of charges equal to a
  given `c`.

## Key theorems

- `mem_ofChargesExpand_map_reduce_iff` states that a `TenQuanta` is in the
  image of `ofChargesExpand c` under `reduce` if and only if it is a `TenQuanta` with
  charges equal to `c` and fluxes which have no exotics or zero.
-/
namespace FTheory

namespace SU5
open SU5
variable {I : CodimensionOneConfig}

/-- The quanta of w0d representations corresponding to a multiset of
  `(q, M, N)` for each partcile. `(M, N)` are defined in the `FluxesFive` module. -/
abbrev TenQuanta (ğ“© : Type := â„¤) : Type := Multiset (ğ“© Ã— Fluxes)

namespace TenQuanta

variable {ğ“© : Type}

/-- The underlying `FluxesTen` from a `TenQuanta`. -/
def toFluxesTen (x : TenQuanta ğ“©) : FluxesTen := x.map Prod.snd

/-- The underlying Multiset charges from a `TenQuanta`. -/
def toCharges (x : TenQuanta ğ“©) : Multiset ğ“© := x.map Prod.fst


def toChargeMap [DecidableEq ğ“©] (x : TenQuanta ğ“©) : ğ“© â†’ Fluxes :=
  fun z => ((x.filter fun p => p.1 = z).map Prod.snd).sum

lemma toChargeMap_of_not_mem [DecidableEq ğ“©] (x : TenQuanta ğ“©) {z : ğ“©} (h : z âˆ‰ x.toCharges) :
    x.toChargeMap z = 0 := by
  simp [toChargeMap]
  have hl : (Multiset.filter (fun p => p.1 = z) x) = 0 := by
    simp
    intro a b f
    by_contra hn
    subst hn
    simp [toCharges] at h
    exact h b f
  rw [hl]
  simp

/-!

## Reduce

-/

section reduce

variable [DecidableEq ğ“©]

/-- The `reduce` of `TenQuanta` is a new `TenQuanta` with all the fluxes
  corresponding to the same charge (i.e. represenation) added together. -/
def reduce (x : TenQuanta ğ“©) : TenQuanta ğ“© :=
  x.toCharges.dedup.map fun q10 => (q10, ((x.filter (fun f => f.1 = q10)).map (fun y => y.2)).sum)

lemma reduce_nodup (x : TenQuanta ğ“©) : x.reduce.Nodup := by
  simp [reduce, toCharges]
  refine Multiset.Nodup.map ?_ ?_
  Â· intro q1 q2 h
    simp at h
    exact h.1
  Â· exact Multiset.nodup_dedup (Multiset.map Prod.fst x)

@[simp]
lemma reduce_dedup (x : TenQuanta ğ“©) : x.reduce.dedup = x.reduce :=
  Multiset.Nodup.dedup x.reduce_nodup

lemma reduce_toCharges (x : TenQuanta ğ“©) : x.reduce.toCharges = x.toCharges.dedup := by
  simp [reduce, toCharges]

lemma reduce_eq_val (x : TenQuanta ğ“©) :
    x.reduce = (x.toCharges.toFinset.image fun q10 =>
      (q10, ((x.filter (fun f => f.1 = q10)).map (fun y => y.2)).sum)).val := by
  simp only [Finset.image_val, Multiset.toFinset_val]
  rw [â† reduce]
  simp

lemma mem_reduce_iff (x : TenQuanta ğ“©) (p : ğ“© Ã— Fluxes) :
    p âˆˆ x.reduce â†” p.1 âˆˆ x.toCharges âˆ§
      p.2 = ((x.filter (fun f => f.1 = p.1)).map (fun y => y.2)).sum := by
  simp [reduce]
  constructor
  Â· intro h
    obtain âŸ¨q, h1, rflâŸ© := h
    simp_all
  Â· simp only [and_imp]
    intro h1 h2
    use p.1
    simp_all
    rw [â† h2]

lemma reduce_filter (x : TenQuanta ğ“©) (q : ğ“©) (h : q âˆˆ x.toCharges) :
    x.reduce.filter (fun f => f.1 = q) =
    {(q, ((x.filter (fun f => f.1 = q)).map (fun y => y.2)).sum)} := by
  simp [reduce]
  rw [Multiset.filter_map]
  simp only [Function.comp_apply]
  have hx : (Multiset.filter (fun x => x = q) x.toCharges.dedup) = {q} := by
    refine (Multiset.Nodup.ext ?_ ?_).mpr ?_
    Â· refine Multiset.Nodup.filter (fun x => x = q) ?_
      exact Multiset.nodup_dedup x.toCharges
    Â· exact Multiset.nodup_singleton q
    intro a
    simp only [Multiset.mem_filter, Multiset.mem_dedup, Multiset.mem_singleton,
      and_iff_right_iff_imp]
    intro h'
    subst h'
    exact h
  rw [hx]
  simp

@[simp]
lemma reduce_reduce (x : TenQuanta ğ“©) :
    x.reduce.reduce = x.reduce := by
  refine Multiset.Nodup.toFinset_inj ?_ ?_ ?_
  Â· exact reduce_nodup x.reduce
  Â· exact reduce_nodup x
  ext p
  simp only [Multiset.mem_toFinset]
  rw [mem_reduce_iff, reduce_toCharges, mem_reduce_iff]
  simp only [Multiset.mem_dedup, and_congr_right_iff]
  intro hp
  have h1 (a b c : Fluxes) (h : b = c) : a = b â†” a = c := by subst h; rfl
  apply h1
  rw [reduce_filter]
  simp only [Multiset.map_singleton, Multiset.sum_singleton]
  exact hp

lemma reduce_sum_eq_sum_toCharges {M} [AddCommMonoid M] (x : TenQuanta ğ“©) (f : ğ“© â†’ Fluxes â†’+ M) :
    (x.reduce.map fun (q, x) => f q x).sum = (x.map fun (q, x) => f q x).sum := by
  calc _
      _ = âˆ‘ q âˆˆ x.toCharges.toFinset,
          f q ((x.filter (fun f => f.1 = q)).map (fun y => y.2)).sum := by
        rw [reduce]
        simp [Finset.sum]
      _ = âˆ‘ q âˆˆ x.toCharges.toFinset,
          (((x.filter (fun f => f.1 = q)).map (fun y => f q y.2))).sum := by
        congr
        funext q5
        rw [AddMonoidHom.map_multiset_sum, Multiset.map_map]
        rfl
      _ = (x.toCharges.dedup.bind fun q =>
          ((x.filter (fun f => f.1 = q)).map (fun y => f q y.2))).sum := by
        rw [Multiset.sum_bind]
        simp [Finset.sum]
      _ = (((x.toCharges.dedup.bind fun q =>
          ((x.filter (fun f => f.1 = q)))).map (fun y => f y.1 y.2))).sum := by
        congr
        rw [Multiset.map_bind]
        congr
        funext q
        refine Multiset.map_congr rfl ?_
        intro y hy
        simp at hy
        rw [hy.2]
      _ = ((x.map (fun y => f y.1 y.2))).sum := by
        congr
        apply Multiset.ext.mpr
        intro p
        trans ((x.map Prod.fst).dedup.map (fun y => if p.1 = y then x.count p else 0)).sum
        Â· rw [@Multiset.count_bind]
          congr
          funext q5
          rw [Multiset.count_filter]
        by_cases h_mem : p.1 âˆˆ x.map Prod.fst
        Â· have h_mem_dedup : p.1 âˆˆ (x.map Prod.fst).dedup := by rwa [Multiset.mem_dedup]
          rw [Multiset.sum_map_eq_nsmul_single p.1]
          simp only [â†“reduceIte, smul_eq_mul]
          have h_count_one : Multiset.count p.1 (Multiset.map Prod.fst x).dedup = 1 := by
            refine Multiset.count_eq_one_of_mem ?_ h_mem_dedup
            exact Multiset.nodup_dedup (Multiset.map Prod.fst x)
          simp [h_count_one]
          intro q5' h h2
          simp_all [eq_comm]
        Â· rw [Multiset.sum_eq_zero]
          refine Eq.symm (Multiset.count_eq_zero_of_notMem ?_)
          intro h
          have h_mem : p.1 âˆˆ Multiset.map Prod.fst x := by
            simp_all
          (expose_names; exact h_mem_1 h_mem)
          intro p' hp
          simp at hp
          obtain âŸ¨q5', âŸ¨f1, hfâŸ©, hp'âŸ© := hp
          by_cases h_eq : p.1 = q5'
          Â· simp_all
          Â· simp_all

lemma reduce_eq_self_of_ofCharges_nodup (x : TenQuanta ğ“©) (h : x.toCharges.Nodup) :
    x.reduce = x := by
  rw [reduce]
  rw [Multiset.Nodup.dedup h]
  simp [toCharges]
  conv_rhs => rw [â† Multiset.map_id x]
  apply Multiset.map_congr rfl
  intro p hp
  simp only [id_eq]
  have x_noDup : x.Nodup := Multiset.Nodup.of_map Prod.fst h
  suffices (Multiset.filter (fun f => f.1 = p.1) x) = {p} by simp [this]
  refine (Multiset.Nodup.ext ?_ ?_).mpr ?_
  Â· exact Multiset.Nodup.filter (fun f => f.1 = p.1) x_noDup
  Â· exact Multiset.nodup_singleton p
  intro p'
  simp only [Multiset.mem_filter, Multiset.mem_singleton]
  constructor
  Â· rintro âŸ¨h1, h2âŸ©
    simp [toCharges] at h
    rw [propext (Multiset.nodup_map_iff_inj_on x_noDup)] at h
    apply h
    Â· exact h1
    Â· exact hp
    Â· exact h2
  Â· rintro âŸ¨rflâŸ©
    simp_all



lemma reduce_toChargeMap_eq (x : TenQuanta ğ“©) :
    x.reduce.toChargeMap = x.toChargeMap := by
  funext q
  by_cases h : q âˆˆ x.toCharges
  Â· rw [toChargeMap, reduce_filter]
    Â· simp
      rfl
    Â· exact h
  Â· rw [toChargeMap_of_not_mem, toChargeMap_of_not_mem]
    Â· exact h
    Â· rw [reduce_toCharges]
      simp only [Multiset.mem_dedup]
      exact h

lemma mem_powerset_sum_of_mem_reduce_toFluxesTen {F : TenQuanta ğ“©}
    {f : Fluxes} (hf : f âˆˆ F.reduce.toFluxesTen) :
    f âˆˆ (Multiset.powerset F.toFluxesTen).map fun s => s.sum := by
  rw [toFluxesTen, Multiset.mem_map] at hf
  obtain âŸ¨âŸ¨q, fâŸ©, hp, rflâŸ© := hf
  rw [mem_reduce_iff] at hp
  simp at hp
  obtain âŸ¨hq, rflâŸ© := hp
  simp only [Multiset.mem_map, Multiset.mem_powerset]
  use (Multiset.map (fun x => x.2) (Multiset.filter (fun x => x.1 = q) F))
  simp only [and_true]
  rw [toFluxesTen]
  refine Multiset.map_le_map ?_
  exact Multiset.filter_le (fun x => x.1 = q) F

lemma reduce_numChiralU_of_mem_elemsNoExotics {F : TenQuanta ğ“©}
    (hx : F.toFluxesTen âˆˆ FluxesTen.elemsNoExotics) :
    F.reduce.toFluxesTen.numChiralU = 3 := by
  have hE : F.toFluxesTen.NoExotics := by
    rw [â† FluxesTen.noExotics_iff_mem_elemsNoExotics]  at hx
    exact hx.1
  rw [â† hE.2.2.1, FluxesTen.numChiralU, FluxesTen.numChiralU, FluxesTen.chiralIndicesOfU]
  trans (F.reduce.toFluxesTen.map (fun f => f.M - f.N)).sum
  Â· congr
    refine Multiset.filter_eq_self.mpr ?_
    intro a ha
    rw [Multiset.mem_map] at ha
    obtain âŸ¨f, hf, rflâŸ© := ha
    replace hf := mem_powerset_sum_of_mem_reduce_toFluxesTen hf
    generalize F.toFluxesTen = G at *
    revert f
    revert G
    decide
  Â· let f : ğ“© â†’ Fluxes â†’+ â„¤ := fun q5 => âŸ¨âŸ¨fun x => x.M - x.N, by simpâŸ©,
      fun x y => by simp; ringâŸ©
    rw [toFluxesTen, Multiset.map_map]
    change (F.reduce.map (fun (q5, x) => f q5 x)).sum = _
    rw [reduce_sum_eq_sum_toCharges]
    congr
    rw [FluxesTen.chiralIndicesOfU, toFluxesTen, Multiset.map_map]
    refine (Multiset.filter_eq_self.mpr ?_).symm
    have h' : Multiset.map (fun x => (f x.1) x.2) F = F.toFluxesTen.map (fun f => f.M - f.N) := by
      simp [toFluxesTen, Multiset.map_map]
      rfl
    rw [h']
    clear h'
    generalize F.toFluxesTen = G at *
    revert G
    decide

lemma reduce_numAntiChiralU_of_mem_elemsNoExotics {F : TenQuanta ğ“©}
    (hx : F.toFluxesTen âˆˆ FluxesTen.elemsNoExotics) :
    F.reduce.toFluxesTen.numAntiChiralU = 0 := by
  rw [FluxesTen.numAntiChiralU, FluxesTen.chiralIndicesOfU]
  have hx : (Multiset.filter (fun x => x < 0) (F.reduce.toFluxesTen.map (fun f => f.M - f.N)))
      = 0 := by
    refine Multiset.filter_eq_nil.mpr ?_
    intro a ha
    rw [Multiset.mem_map] at ha
    obtain âŸ¨f, hf, rflâŸ© := ha
    replace hf := mem_powerset_sum_of_mem_reduce_toFluxesTen hf
    generalize F.toFluxesTen = G at *
    revert f
    revert G
    decide
  rw [hx]
  rfl

lemma reduce_numChiralQ_of_mem_elemsNoExotics {F : TenQuanta ğ“©}
    (hx : F.toFluxesTen âˆˆ FluxesTen.elemsNoExotics) :
    F.reduce.toFluxesTen.numChiralQ = 3 := by
  have hE : F.toFluxesTen.NoExotics := by
    rw [â† FluxesTen.noExotics_iff_mem_elemsNoExotics]  at hx
    exact hx.1
  rw [â† hE.1, FluxesTen.numChiralQ, FluxesTen.numChiralQ, FluxesTen.chiralIndicesOfQ]
  trans (F.reduce.toFluxesTen.map (fun f => f.M)).sum
  Â· congr
    refine Multiset.filter_eq_self.mpr ?_
    intro a ha
    rw [Multiset.mem_map] at ha
    obtain âŸ¨f, hf, rflâŸ© := ha
    replace hf := mem_powerset_sum_of_mem_reduce_toFluxesTen hf
    generalize F.toFluxesTen = G at *
    revert f
    revert G
    decide
  Â· let f : ğ“© â†’ Fluxes â†’+ â„¤ := fun q5 => âŸ¨âŸ¨fun x => x.M, by simpâŸ©,
      fun x y => by simpâŸ©
    rw [toFluxesTen, Multiset.map_map]
    change (F.reduce.map (fun (q5, x) => f q5 x)).sum = _
    rw [reduce_sum_eq_sum_toCharges]
    congr
    rw [FluxesTen.chiralIndicesOfQ, toFluxesTen, Multiset.map_map]
    refine (Multiset.filter_eq_self.mpr ?_).symm
    have h' : Multiset.map (fun x => (f x.1) x.2) F = F.toFluxesTen.map (fun f => f.M) := by
      simp [toFluxesTen, Multiset.map_map]
      rfl
    rw [h']
    clear h'
    generalize F.toFluxesTen = G at *
    revert G
    decide

lemma reduce_numAntiChiralQ_of_mem_elemsNoExotics {F : TenQuanta ğ“©}
    (hx : F.toFluxesTen âˆˆ FluxesTen.elemsNoExotics) :
    F.reduce.toFluxesTen.numAntiChiralQ = 0 := by
  rw [FluxesTen.numAntiChiralQ, FluxesTen.chiralIndicesOfQ]
  have hx : (Multiset.filter (fun x => x < 0) (F.reduce.toFluxesTen.map (fun f => f.M)))
      = 0 := by
    refine Multiset.filter_eq_nil.mpr ?_
    intro a ha
    rw [Multiset.mem_map] at ha
    obtain âŸ¨f, hf, rflâŸ© := ha
    replace hf := mem_powerset_sum_of_mem_reduce_toFluxesTen hf
    generalize F.toFluxesTen = G at *
    revert f
    revert G
    decide
  rw [hx]
  rfl


lemma reduce_numChiralE_of_mem_elemsNoExotics {F : TenQuanta ğ“©}
    (hx : F.toFluxesTen âˆˆ FluxesTen.elemsNoExotics) :
    F.reduce.toFluxesTen.numChiralE = 3 := by
  have hE : F.toFluxesTen.NoExotics := by
    rw [â† FluxesTen.noExotics_iff_mem_elemsNoExotics]  at hx
    exact hx.1
  rw [â† hE.2.2.2.2.1, FluxesTen.numChiralE, FluxesTen.numChiralE, FluxesTen.chiralIndicesOfE]
  trans (F.reduce.toFluxesTen.map (fun f => f.M + f.N)).sum
  Â· congr
    refine Multiset.filter_eq_self.mpr ?_
    intro a ha
    rw [Multiset.mem_map] at ha
    obtain âŸ¨f, hf, rflâŸ© := ha
    replace hf := mem_powerset_sum_of_mem_reduce_toFluxesTen hf
    generalize F.toFluxesTen = G at *
    revert f
    revert G
    decide
  Â· let f : ğ“© â†’ Fluxes â†’+ â„¤ := fun q5 => âŸ¨âŸ¨fun x => x.M + x.N, by simpâŸ©,
      fun x y => by simp; ringâŸ©
    rw [toFluxesTen, Multiset.map_map]
    change (F.reduce.map (fun (q5, x) => f q5 x)).sum = _
    rw [reduce_sum_eq_sum_toCharges]
    congr
    rw [FluxesTen.chiralIndicesOfE, toFluxesTen, Multiset.map_map]
    refine (Multiset.filter_eq_self.mpr ?_).symm
    have h' : Multiset.map (fun x => (f x.1) x.2) F = F.toFluxesTen.map (fun f => f.M + f.N) := by
      simp [toFluxesTen, Multiset.map_map]
      rfl
    rw [h']
    clear h'
    generalize F.toFluxesTen = G at *
    revert G
    decide

lemma reduce_numAntiChiralE_of_mem_elemsNoExotics {F : TenQuanta ğ“©}
    (hx : F.toFluxesTen âˆˆ FluxesTen.elemsNoExotics) :
    F.reduce.toFluxesTen.numAntiChiralE = 0 := by
  rw [FluxesTen.numAntiChiralE, FluxesTen.chiralIndicesOfE]
  have hx : (Multiset.filter (fun x => x < 0) (F.reduce.toFluxesTen.map (fun f => f.M + f.N)))
      = 0 := by
    refine Multiset.filter_eq_nil.mpr ?_
    intro a ha
    rw [Multiset.mem_map] at ha
    obtain âŸ¨f, hf, rflâŸ© := ha
    replace hf := mem_powerset_sum_of_mem_reduce_toFluxesTen hf
    generalize F.toFluxesTen = G at *
    revert f
    revert G
    decide
  rw [hx]
  rfl
lemma reduce_noExotics_of_mem_elemsNoExotics {F : TenQuanta ğ“©}
    (hx : F.toFluxesTen âˆˆ FluxesTen.elemsNoExotics) :
    F.reduce.toFluxesTen.NoExotics := by
  rw [FluxesTen.NoExotics]
  rw [reduce_numChiralU_of_mem_elemsNoExotics hx, reduce_numAntiChiralU_of_mem_elemsNoExotics hx,
    reduce_numChiralQ_of_mem_elemsNoExotics hx, reduce_numAntiChiralQ_of_mem_elemsNoExotics hx,
    reduce_numChiralE_of_mem_elemsNoExotics hx, reduce_numAntiChiralE_of_mem_elemsNoExotics hx]
  simp

end reduce

/-!

## Anomaly cancellation

-/
section ACCs

variable [CommRing ğ“©]

/--
  The anomaly coefficent of a `TenQuanta` is given by the pair of integers:
  `(âˆ‘áµ¢ qáµ¢ Náµ¢, 3 * âˆ‘áµ¢ qáµ¢Â² Náµ¢)`.

  The first components is for the mixed U(1)-MSSM, see equation (22) of arXiv:1401.5084.
  The second component is for the mixed U(1)Y-U(1)-U(1) gauge anomaly,
    see equation (23) of arXiv:1401.5084.
-/
def anomalyCoefficent (F : TenQuanta ğ“©) : ğ“© Ã— ğ“© :=
  ((F.map fun x => x.2.2 â€¢ x.1).sum, 3 * (F.map fun x => x.2.2 â€¢ (x.1 * x.1)).sum)

@[simp]
lemma anomalyCoefficent_of_map {ğ“© ğ“©1 : Type} [CommRing ğ“©] [CommRing ğ“©1]
    (f : ğ“© â†’+* ğ“©1) (F : TenQuanta ğ“©) :
    TenQuanta.anomalyCoefficent (F.map fun y => (f y.1, y.2) : TenQuanta ğ“©1) =
    (f.prodMap f) F.anomalyCoefficent := by
  simp [TenQuanta.anomalyCoefficent, map_multiset_sum, Multiset.map_map, map_ofNat]

lemma anomalyCoefficent_of_reduce [DecidableEq ğ“©] (F : TenQuanta ğ“©) :
    F.reduce.anomalyCoefficent = F.anomalyCoefficent := by
  simp [anomalyCoefficent]
  constructor
  Â· let f : ğ“© â†’ Fluxes â†’+ ğ“© := fun q5 => {
      toFun := fun x => x.2 â€¢ q5
      map_zero' := by simp
      map_add' := by
        intros x y
        simp [add_mul] }
    simpa [f] using reduce_sum_eq_sum_toCharges F f
  Â· let f : ğ“© â†’ Fluxes â†’+ ğ“© := fun q5 => {
      toFun := fun x => x.2 â€¢ (q5 * q5)
      map_zero' := by simp
      map_add' := by
        intros x y
        simp [add_mul] }
    apply congrArg
    simpa [f] using reduce_sum_eq_sum_toCharges F f

end ACCs

/-!

## Decomposition of a `TenQuanta`

-/


def decomposeFluxes (f : Fluxes) : Multiset Fluxes :=
  if f = âŸ¨1, 0âŸ© then {âŸ¨1, 0âŸ©}
  else if f = âŸ¨1, 1âŸ© then {âŸ¨1, 1âŸ©}
  else if f = âŸ¨1, -1âŸ© then {âŸ¨1, -1âŸ©}
  else if f = âŸ¨2, 1âŸ© then {âŸ¨1, 1âŸ©, âŸ¨1, 0âŸ©}
  else if f = âŸ¨2, -1âŸ© then {âŸ¨1, -1âŸ©, âŸ¨1, 0âŸ©}
  else if f = âŸ¨3, 0âŸ© then {âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©}
  else if f = âŸ¨2, 0âŸ© then {âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©}
  else {f}

lemma decomposeFluxes_sum_of_noExotics (f : Fluxes) (hf : âˆƒ F âˆˆ FluxesTen.elemsNoExotics, f âˆˆ F) :
    (decomposeFluxes f).sum = f := by
  obtain âŸ¨F, hF, hfFâŸ© := hf
  revert f
  revert F
  decide

def decompose (x : TenQuanta ğ“©) : TenQuanta ğ“© :=
  x.bind fun p => (decomposeFluxes p.2).map fun f => (p.1, f)

lemma decompose_add (x y : TenQuanta ğ“©) :
    (x + y).decompose = x.decompose + y.decompose := by
  simp [decompose]


lemma decompose_filter_charge [DecidableEq ğ“©] (x : TenQuanta ğ“©) (q : ğ“©) :
    (x.decompose).filter (fun p => p.1 = q) =
    decompose (x.filter (fun p => p.1 = q)):= by
  rw [decompose]
  revert x
  apply Multiset.induction
  Â· simp [decompose]
  Â· intro a x ih
    simp
    rw [Multiset.filter_cons, decompose_add, ih]
    congr
    match a with
    | (q', f) =>
    simp [decomposeFluxes]
    by_cases h : q' = q
    Â· subst h
      simp [decompose, decomposeFluxes]
    Â· simp [h, decompose]


lemma decompose_toChargeMap [DecidableEq ğ“©] (x : TenQuanta ğ“©)
    (hx : x.toFluxesTen âˆˆ FluxesTen.elemsNoExotics) :
    x.decompose.toChargeMap = x.toChargeMap := by
  ext q
  rw [toChargeMap, decompose_filter_charge]
  simp [decompose]
  rw [Multiset.map_bind]
  simp
  rw [toChargeMap]
  congr 1
  apply Multiset.map_congr
  Â· rfl
  intro a ha
  apply decomposeFluxes_sum_of_noExotics
  use x.toFluxesTen
  simp_all [toFluxesTen]
  use a.1
  exact ha.1

lemma decompose_toCharges_dedup [DecidableEq ğ“©] (x : TenQuanta ğ“©)
   (hx : x.toFluxesTen âˆˆ FluxesTen.elemsNoExotics):
    x.decompose.toCharges.dedup = x.toCharges.dedup := by
  refine Multiset.dedup_ext.mpr ?_
  intro q
  simp [decompose, toCharges]
  constructor
  Â· rintro âŸ¨a, b, c, h1, h2, rflâŸ©
    exact âŸ¨c, h1âŸ©
  Â· rintro âŸ¨c, h1âŸ©
    have hn : (decomposeFluxes c) â‰  0 := by
      have c_mem_f : c âˆˆ x.toFluxesTen := by
        simp [toFluxesTen]
        use q
      generalize x.toFluxesTen = F at *
      clear h1
      revert c
      revert F
      decide
    apply Multiset.exists_mem_of_ne_zero at hn
    obtain âŸ¨c', hâŸ© := hn
    use c', q, c

lemma decompose_reduce (x : TenQuanta ğ“©) [DecidableEq ğ“©]
    (hx : x.toFluxesTen âˆˆ FluxesTen.elemsNoExotics) :
    x.decompose.reduce = x.reduce := by
  rw [reduce, reduce]
  apply Multiset.map_congr
  Â· rw [decompose_toCharges_dedup x hx]
  Â· intro q hx'
    simp
    change x.decompose.toChargeMap q = x.toChargeMap q
    rw [decompose_toChargeMap x hx]

lemma decompose_toFluxesFive (x : TenQuanta ğ“©)
    (hx : x.toFluxesTen âˆˆ FluxesTen.elemsNoExotics) :
    x.decompose.toFluxesTen = {âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©} âˆ¨
    x.decompose.toFluxesTen = {âŸ¨1, 1âŸ©, âŸ¨1, -1âŸ©, âŸ¨1, 0âŸ©} := by
  rw [toFluxesTen, decompose]
  rw [Multiset.map_bind]
  simp
  have hx : (Multiset.bind x fun a => decomposeFluxes a.2) =
      (Multiset.bind x.toFluxesTen fun a => decomposeFluxes a) := by
    rw [toFluxesTen, Multiset.bind_map]
  rw [hx]
  clear hx
  generalize x.toFluxesTen = F at *
  revert F
  decide

/-!

## toChargesExpand

-/

section toChargesExpand

open SuperSymmetry.SU5.ChargeSpectrum

variable [DecidableEq ğ“©]

/-- Given a finite set of charges `c` the `TenQuanta`
  with fluxes `{(1, 0), (1, 0), (1, 0)}` and `{(1, 1), (1, -1), (1, 0)}`
  and finite set of charges equal to `c`. -/
def ofChargesExpand (c : Finset ğ“©) : Multiset (TenQuanta ğ“©) :=
  /- The {(1, 0), (1, 0), (1, 0)} case. -/
  /- The multisets of cardinality 3 containing 3 elements of `c`. -/
  let S10 : Multiset (Multiset ğ“©) := toMultisetsThree c
  let F1 : Multiset (TenQuanta ğ“©) :=
    (S10.map (fun s => s.map (fun z => (z, âŸ¨1, 0âŸ©)))).filter (fun s => c.val â‰¤ s.toCharges)
  /- The {(1, 1), (1, -1), (1, 0)} case. -/
  let F2 : Multiset (TenQuanta ğ“©) := ((c.product <| c.product <| c).val.map
    fun (x, y, z) => {(x, âŸ¨1, 1âŸ©), (y, âŸ¨1, -1âŸ©), (z, âŸ¨1, 0âŸ©)}).filter (fun s => c.val â‰¤ s.toCharges)
  /- All together-/
  F1 + F2

lemma toFluxesFive_of_mem_ofChargesExpand (c : Finset ğ“©)
    {x : TenQuanta ğ“©} (h : x âˆˆ ofChargesExpand c) :
    x.toFluxesTen = {âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©}
    âˆ¨ x.toFluxesTen = {âŸ¨1, 1âŸ©, âŸ¨1, -1âŸ©, âŸ¨1, 0âŸ©}:= by
  simp [ofChargesExpand] at h
  rcases h with h | h
  Â· obtain âŸ¨âŸ¨s, h, rflâŸ©, h'âŸ© := h
    left
    simp [toFluxesTen]
    rw [h.2]
    decide
  Â· obtain âŸ¨âŸ¨q1, q2, q3, h, rflâŸ©, h'âŸ© := h
    simp [toFluxesTen]

lemma toCharges_of_mem_ofChargesExpand (c : Finset ğ“©)
    {x : TenQuanta ğ“©} (h : x âˆˆ ofChargesExpand c) :
    x.toCharges.toFinset = c := by
  simp [ofChargesExpand] at h
  rcases h with h | h
  Â· obtain âŸ¨âŸ¨s, h, rflâŸ©, h'âŸ© := h
    simp_all [toCharges]
    ext a
    simp only [Multiset.mem_toFinset]
    constructor
    Â· intro hr
      apply h.1
      simpa using hr
    Â· intro hr
      exact Multiset.mem_of_le h' hr
  Â· obtain âŸ¨âŸ¨q1, q2, q3, h, rflâŸ©, h'âŸ© := h
    simp_all [toCharges]
    refine Eq.symm ((fun {Î±} {sâ‚ sâ‚‚} => Finset.ext_iff.mpr) ?_)
    intro a
    constructor
    Â· intro hr
      simpa using Multiset.mem_of_le h' hr
    Â· intro hr
      simp at hr
      simp only [SProd.sprod, Multiset.mem_product] at h
      rcases hr with rfl | rfl | rfl
      Â· exact h.1
      Â· exact h.2.1
      Â· exact h.2.2

lemma mem_ofChargesExpand_of_toCharges_toFluxesTen (c : Finset ğ“©) {x : TenQuanta ğ“©}
    (h : x.toCharges.toFinset = c) (h2 : x.toFluxesTen =
      {âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©} âˆ¨ x.toFluxesTen = {âŸ¨1, 1âŸ©, âŸ¨1, -1âŸ©, âŸ¨1, 0âŸ©}) :
    x âˆˆ ofChargesExpand c := by
  simp [ofChargesExpand]
  rcases h2 with h2 | h2
  Â· left
    subst h
    simp only [Multiset.toFinset_subset, Multiset.toFinset_val]
    apply And.intro
    Â· use x.toCharges
      simp only [Multiset.Subset.refl, true_and]
      apply And.intro
      Â· simp [toCharges]
        trans x.toFluxesTen.card
        Â· simp [toFluxesTen]
        rw [h2]
        decide
      Â· trans Multiset.map (fun z => z) x
        swap
        Â· simp
        rw [toCharges, Multiset.map_map]
        apply Multiset.map_congr
        rfl
        intro p hp
        simp only [Function.comp_apply]
        have h1 : p.2 âˆˆ x.toFluxesTen := by
          simp [toFluxesTen]
          use p.1
        rw [h2] at h1
        simp at h1
        change _ = (p.1, p.2)
        rw [h1]
    Â· exact Multiset.dedup_le x.toCharges
  Â· right
    have h2' := h2
    simp [toFluxesTen] at h2
    rw [â† Multiset.map_eq_cons] at h2
    obtain âŸ¨p1, hp1, hp1_2, h2âŸ© := h2
    rw [â† Multiset.map_eq_cons] at h2
    obtain âŸ¨p2, hp2, hp2_2, h2âŸ© := h2
    rw [Multiset.map_eq_singleton] at h2
    obtain âŸ¨p3, hp3, hp3_2âŸ© := h2
    apply And.intro
    Â· use p1.1, p2.1, p3.1
      simp only [SProd.sprod, Multiset.mem_product]
      subst h
      simp only [Multiset.toFinset_val, Multiset.mem_dedup, Int.reduceNeg]
      refine âŸ¨âŸ¨?_, ?_, ?_âŸ©, ?_âŸ©
      Â· simp [toCharges]
        use p1.2
      Â· simp [toCharges]
        use p2.2
        apply Multiset.erase_subset p1 x hp2
      Â· simp [toCharges]
        use p3.2
        apply Multiset.erase_subset p1 x
        apply Multiset.erase_subset p2 _
        rw [hp3]
        simp
      Â· symm
        refine Eq.symm (Multiset.eq_of_le_of_card_le ?_ ?_)
        Â· refine (Multiset.cons_le_of_notMem ?_).mpr âŸ¨?_, ?_âŸ©
          Â· simp
          Â· rw [â† hp1_2]
            exact hp1
          refine (Multiset.cons_le_of_notMem ?_).mpr âŸ¨?_, ?_âŸ©
          Â· simp
          Â· rw [â† hp2_2]
            apply Multiset.erase_subset p1 x
            exact hp2
          simp only [Multiset.singleton_le]
          rw [â† hp3_2]
          apply Multiset.erase_subset p1 x
          apply Multiset.erase_subset p2 _
          rw [hp3]
          simp
        Â· trans x.toFluxesTen.card
          Â· simp [toFluxesTen]
          rw [h2']
          simp
    Â· rw [â† h]
      simp only [Multiset.toFinset_val]
      exact Multiset.dedup_le x.toCharges

lemma mem_ofChargesExpand_iff (c : Finset ğ“©) {x : TenQuanta ğ“©} :
    x âˆˆ ofChargesExpand c â†”
    x.toCharges.toFinset = c âˆ§ (x.toFluxesTen = {âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©, âŸ¨1, 0âŸ©}
    âˆ¨ x.toFluxesTen = {âŸ¨1, 1âŸ©, âŸ¨1, -1âŸ©, âŸ¨1, 0âŸ©}) := by
  constructor
  Â· intro h
    constructor
    exact toCharges_of_mem_ofChargesExpand c h
    exact toFluxesFive_of_mem_ofChargesExpand c h
  Â· intro h
    obtain âŸ¨h1, h2âŸ© := h
    exact mem_ofChargesExpand_of_toCharges_toFluxesTen c h1 h2

lemma mem_ofChargesExpand_of_noExotics_hasNoZero (F : TenQuanta ğ“©) (c : Finset ğ“©)
    (hc : F.toCharges.toFinset = c)
    (h1 : F.toFluxesTen.NoExotics) (h2 : F.toFluxesTen.HasNoZero) :
    âˆƒ y âˆˆ ofChargesExpand c, y.reduce = F.reduce := by
  have hf : F.toFluxesTen âˆˆ FluxesTen.elemsNoExotics := by
    rw [â† FluxesTen.noExotics_iff_mem_elemsNoExotics]
    simp_all
    exact h2
  use F.decompose
  rw [decompose_reduce F hf]
  simp
  rw [mem_ofChargesExpand_iff]
  apply And.intro
  Â· trans  F.decompose.toCharges.dedup.toFinset
    Â· simp
    simp [decompose_toCharges_dedup F hf, â† hc]
  Â· exact decompose_toFluxesFive F hf

lemma reduce_hasNoZeros_of_mem_ofChargesExpand (c : Finset ğ“©) (F : TenQuanta ğ“©)
    (h : F âˆˆ ofChargesExpand c) :
    F.reduce.toFluxesTen.HasNoZero := by
  simp [reduce, toFluxesTen, FluxesTen.HasNoZero]
  intro x hx
  have hs : (Multiset.map (fun y => y.2) (Multiset.filter (fun f => f.1 = x) F))
      âˆˆ F.toFluxesTen.powerset := by
    simp [toFluxesTen]
  have h1 : Multiset.map (fun y => y.2) (Multiset.filter (fun f => f.1 = x) F) â‰  0 := by
    simp only [ne_eq, Multiset.map_eq_zero]
    rw [@Multiset.filter_eq_nil]
    simp [Prod.forall, not_forall, Classical.not_imp, Decidable.not_not, exists_and_right,
      exists_eq_right]
    simp [toCharges] at hx
    obtain âŸ¨f, hâŸ© := hx
    use f
  generalize (Multiset.map (fun y => y.2) (Multiset.filter (fun f => f.1 = x) F)) = s at *
  rw [mem_ofChargesExpand_iff] at h
  rcases h with âŸ¨h1, h | hâŸ©
  all_goals
  Â· rw [h] at hs
    fin_cases hs
    Â· simp at h1
    all_goals
    Â· decide

lemma reduce_noExotics_of_mem_ofChargesExpand (c : Finset ğ“©) (F : TenQuanta ğ“©)
    (h : F âˆˆ ofChargesExpand c) :
    F.reduce.toFluxesTen.NoExotics := by
  apply reduce_noExotics_of_mem_elemsNoExotics
  rw [mem_ofChargesExpand_iff] at h
  rcases h with âŸ¨h1, h | hâŸ©
  all_goals
  Â· rw [h]
    decide


lemma reduce_mem_elemsNoExotics_of_mem_ofChargesExpand (c : Finset ğ“©) (F : TenQuanta ğ“©)
    (h : F âˆˆ ofChargesExpand c) :
    F.reduce.toFluxesTen âˆˆ FluxesTen.elemsNoExotics := by
  rw [â† FluxesTen.noExotics_iff_mem_elemsNoExotics]
  constructor
  Â· exact reduce_noExotics_of_mem_ofChargesExpand c F h
  Â· exact reduce_hasNoZeros_of_mem_ofChargesExpand c F h

lemma mem_ofChargesExpand_map_reduce_iff (c : Finset ğ“©) (S : TenQuanta ğ“©) :
    S âˆˆ (ofChargesExpand c).map reduce â†” S.toFluxesTen âˆˆ FluxesTen.elemsNoExotics
      âˆ§ S.toCharges.toFinset = c âˆ§ S.reduce = S := by
  constructor
  Â· intro h
    simp at h
    obtain âŸ¨F, h1, rflâŸ© := h
    refine âŸ¨?_, ?_, ?_âŸ©
    Â· exact reduce_mem_elemsNoExotics_of_mem_ofChargesExpand c F h1
    Â· rw [mem_ofChargesExpand_iff] at h1
      rw [â† h1.1, reduce_toCharges]
      exact Multiset.toFinset_dedup F.toCharges
    Â· rw [reduce_reduce]
  Â· intro h
    simp only [Multiset.mem_map]
    rw [â† h.2.2]
    have h1 := h.1
    rw [â† FluxesTen.noExotics_iff_mem_elemsNoExotics] at h1
    refine mem_ofChargesExpand_of_noExotics_hasNoZero S c ?_ ?_ ?_
    Â· exact h.2.1
    Â· exact h1.1
    Â· exact h1.2

end toChargesExpand
end TenQuanta

end SU5

end FTheory
