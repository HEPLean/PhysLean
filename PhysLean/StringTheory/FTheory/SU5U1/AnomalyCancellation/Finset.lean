/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.StringTheory.FTheory.SU5U1.AnomalyCancellation.Basic
import PhysLean.StringTheory.FTheory.SU5U1.NoExotics.FiveBar
/-!

# Finsets containing the anomaly coefficients

This file contains the construction of
a finiset which carries the anomaly coefficients for
a multiset of hypercharge fluxes and a multiset of hypercharges,
not necessarily paired.

-/
namespace FTheory

namespace SU5U1
namespace MatterContent

variable {I : CodimensionOneConfig} (ğ“œ : MatterContent I)

/-!

## Auxillary results

Related to zips and projections of multisets.

-/

/-- Given two multisets `S` and `T` of equal cardinality, `multiSetPairs S T` is the
  finset containing all `Multiset (â„¤ Ã— â„¤)` which project in the first element to `S`
  and the second element to `T`.

  This is defined in an executable way. -/
def multiSetPairs (S T : Multiset â„¤)  : Finset (Multiset (â„¤ Ã— â„¤)) :=
  let T'' := (S.sort LE.le).permutations.map (fun S => Multiset.ofList (List.zip S (T.sort LE.le)))
  T''.toFinset

lemma mem_multiSetPairs_iff_mem_list {S T : Multiset â„¤} (X : Multiset (â„¤ Ã— â„¤)) :
    X âˆˆ multiSetPairs S T â†” X âˆˆ (S.sort LE.le).permutations.map
      (fun S => Multiset.ofList (List.zip S (T.sort LE.le))) := by
  rw [multiSetPairs]
  simp

lemma mem_multiSetPairs_iff_exist_perm {S T : Multiset â„¤} (X : Multiset (â„¤ Ã— â„¤)) :
    X âˆˆ multiSetPairs S T â†” âˆƒ (l : List â„¤), l.Perm (S.sort LE.le) âˆ§
      X = Multiset.ofList (List.zip l (T.sort LE.le)) := by
  rw [multiSetPairs]
  aesop

lemma mem_multiSetPairs_iff_exist_toList {S T : Multiset â„¤} (X : Multiset (â„¤ Ã— â„¤)) :
    X âˆˆ multiSetPairs S T â†” âˆƒ (l : List â„¤), l.Perm (S.sort LE.le) âˆ§
      X.toList.Perm (List.zip l (T.sort LE.le)) := by
  rw [mem_multiSetPairs_iff_exist_perm]
  constructor
  Â· intro h
    obtain âŸ¨l, hp, rflâŸ© := h
    use l
    simp_all
    generalize (l.zip (Multiset.sort LE.le T)) = l
    refine List.perm_iff_count.mpr ?_
    intro a
    rw [â† Multiset.coe_count]
    simp
  Â· intro h
    obtain âŸ¨l,  h1, h2âŸ© := h
    use l
    simp_all
    refine Multiset.ext.mpr ?_
    intro a
    rw [List.perm_iff_count] at h2
    rw [Multiset.coe_count]
    rw [â† h2 a]
    rw [â† Multiset.coe_count]
    simp

lemma zip_perm_orderedInsert (l : â„¤) : (r ls : List â„¤) â†’ (h : r.length = (l :: ls).length ) â†’
    âˆƒ (r' : List â„¤), r'.Perm r âˆ§ (r'.zip (l :: ls)).Perm (r.zip (ls.orderedInsert LE.le l))
  | rs, [] => by
    aesop
  | [], l2 :: ls => by
    aesop
  | r ::  [], l2 :: ls => by
    aesop
  | r :: r1 :: [], l2 :: ls => by
    simp only [List.length_cons, List.length_nil, zero_add, Nat.reduceAdd, right_eq_add,
      List.length_eq_zero_iff, List.orderedInsert]
    intro h
    subst h
    simp only [List.orderedInsert]
    split_ifs
    Â· use [r, r1]
    Â· use [r1, r]
      constructor
      Â· exact List.Perm.swap r r1 []
      Â· simp
        exact List.Perm.swap (r, l2) (r1, l) []
  | r2 :: r1 :: rs, l2 :: ls => by
    intro h
    simp only [List.orderedInsert]
    split_ifs
    Â· use  r2 :: r1 :: rs
    Â· have ih := zip_perm_orderedInsert l (r1 :: rs) ls (by simp_all)
      obtain âŸ¨r', h1, h2âŸ© := ih
      have h' : ((r2 :: r1 :: rs).zip (l2 :: List.orderedInsert LE.le l ls)).Perm
          ((r2, l2) :: (r'.zip (l :: ls))) := by
        simp only [List.zip_cons_cons, List.perm_cons]
        exact id (List.Perm.symm h2)
      have hn :  âˆƒ r'' : List â„¤,
          r''.Perm (r2 :: r') âˆ§ (r''.zip (l :: l2 :: ls)).Perm
          ((r2, l2) :: (r'.zip (l :: ls))) := by
        induction r' with
        | nil =>  simp at h1
        | cons rx r' ih' =>
          use rx :: r2 :: r'
          constructor
          Â· exact List.Perm.swap r2 rx r'
          Â· simp
            exact List.Perm.swap (r2, l2) (rx, l) (r'.zip ls)
      obtain âŸ¨r'', h3, h4âŸ© := hn
      use  r''
      constructor
      Â· trans (r2 :: r')
        Â· exact h3
        Â· exact List.Perm.cons r2 h1
      Â· trans ((r2, l2) :: (r'.zip (l :: ls)))
        Â· exact h4
        Â· exact id (List.Perm.symm h')

lemma zip_perm_insertionSort : (r l3 : List â„¤) â†’ (h : r.length = l3.length ) â†’
    âˆƒ (r' : List â„¤), r'.Perm r âˆ§ (r'.zip l3).Perm (r.zip (l3.insertionSort LE.le))
  | [], [] => by
    simp
  | [], l :: ls => by
    simp
  | r :: rs, [] => by
    simp
  | r :: rs, l :: ls => by
    intro h
    simp only [List.insertionSort]
    have h1 := zip_perm_orderedInsert l (r :: rs) (List.insertionSort LE.le ls) (by simp_all)
    obtain âŸ¨r1, h2, h3âŸ© := h1
    have h2 : âˆƒ r2 : List â„¤, r2.Perm r1 âˆ§ (r2.zip (l :: ls)).Perm
        (r1.zip (l :: List.insertionSort LE.le ls)) := by
      induction r1 with
      | nil =>  simp
      | cons rx r1 ih' =>
        -- r2  perm rx :: r1
        have hlenrsr1 : r1.length = rs.length := by
          simpa using List.Perm.length_eq h2
        obtain âŸ¨r2, h4, h5âŸ© := zip_perm_insertionSort r1 ls (by simp at h; omega)
        use rx :: r2
        constructor
        Â· exact List.Perm.cons rx h4
        Â· simp
          exact h5
    obtain âŸ¨r2, h4, h5âŸ© := h2
    use r2
    constructor
    Â· exact List.Perm.trans h4 h2
    Â· exact List.Perm.trans h5 h3

lemma zip_perm (l1 l2 l3 : List â„¤)  (hp : l2.Perm l3) (hl : l2.length = l1.length):
    âˆƒ (r : List â„¤), r.Perm l1 âˆ§ (l1.zip l2).Perm (r.zip l3) := by
  have hl' : (l2.insertionSort LE.le) = (l3.insertionSort LE.le) := by
    apply List.eq_of_perm_of_sorted (r := LE.le)
    Â· trans l2
      Â· exact List.perm_insertionSort LE.le l2
      trans l3
      Â· exact hp
      Â· exact List.Perm.symm (List.perm_insertionSort LE.le l3)
    Â· exact List.sorted_insertionSort LE.le l2
    Â· exact List.sorted_insertionSort LE.le l3
  have h1 : ((l1.zip l2).insertionSort (fun x y => x.2 â‰¤ y.2)).unzip.1.Perm l1 := by
    simp only [List.unzip_fst]
    trans  (List.map Prod.fst ( (l1.zip l2)))
    Â· refine List.Perm.map Prod.fst ?_
      exact List.perm_insertionSort (fun x y => x.2 â‰¤ y.2) (l1.zip l2)
    Â· rw [â† List.unzip_fst]
      rw [List.unzip_zip]
      (expose_names; exact id (Eq.symm hl))
  have h2 : ((l1.zip l2).insertionSort (fun x y => x.2 â‰¤ y.2)).unzip.2 =
      l2.insertionSort LE.le := by
    simp only [List.unzip_snd]
    have h1 := List.map_insertionSort (r := (fun x y => x.2 â‰¤ y.2)) (s := LE.le) Prod.snd
      (l1.zip l2) (by aesop)
    rw [h1]
    rw [â† List.unzip_snd]
    rw [List.unzip_zip]
    (expose_names; exact id (Eq.symm hl))
  have h3 : âˆƒ (r : List â„¤), l1.Perm r âˆ§ (l1.zip l2).Perm (r.zip (l3.insertionSort LE.le)) := by
    use ((l1.zip l2).insertionSort (fun x y => x.2 â‰¤ y.2)).unzip.1
    rw [â† hl', â† h2, List.zip_unzip]
    constructor
    Â· exact id (List.Perm.symm h1)
    Â· exact List.Perm.symm (List.perm_insertionSort (fun x y => x.2 â‰¤ y.2) (l1.zip l2))
  obtain âŸ¨r, h4, h5âŸ© := h3
  have h4 : âˆƒ (r' : List â„¤), r'.Perm r âˆ§ (r'.zip l3).Perm (r.zip (l3.insertionSort LE.le)) := by
    have := List.Perm.length_eq hp
    have := List.Perm.length_eq h4
    exact zip_perm_insertionSort r l3 (by omega)
  obtain âŸ¨r2, h6, h7âŸ© := h4
  use r2
  constructor
  Â· exact List.Perm.trans h6 (id (List.Perm.symm h4))
  Â· exact List.Perm.trans h5 (id (List.Perm.symm h7))

lemma mem_multiSetPairs_iff_exist_toList_perm {S T : Multiset â„¤} (X : Multiset (â„¤ Ã— â„¤))
    (hlen : S.card = T.card) :
    X âˆˆ multiSetPairs S T â†” âˆƒ (l l2 : List â„¤), l.Perm (S.sort LE.le) âˆ§ l2.Perm (T.sort LE.le) âˆ§
    X.toList.Perm (List.zip l l2) := by
  rw [mem_multiSetPairs_iff_exist_perm]
  constructor
  Â· intro h
    obtain âŸ¨l, hp, rflâŸ© := h
    use l, (T.sort LE.le)
    simp_all
    generalize (l.zip (Multiset.sort LE.le T)) = l
    refine List.perm_iff_count.mpr ?_
    intro a
    rw [â† Multiset.coe_count]
    simp
  Â· intro h
    obtain âŸ¨l, l2, h1, h2, h3âŸ© := h
    have hL1 := List.Perm.length_eq h1
    have hL2 := List.Perm.length_eq h2
    simp at hL1 hL2
    obtain âŸ¨r, h1, h2âŸ© := zip_perm l l2 (Multiset.sort LE.le T) (by simp_all) (by omega)
    use r
    constructor
    Â· (expose_names; exact List.Perm.trans h1 h1_1)
    Â· rw [Multiset.coe_eq_coe.mpr (id (List.Perm.symm h2))]
      trans Multiset.ofList X.toList
      Â· simp
      exact Multiset.coe_eq_coe.mpr h3

lemma mem_multiSetPairs_of_proj {S T : Multiset â„¤} (hlen : S.card = T.card)
    (X : Multiset (â„¤ Ã— â„¤)) :
    X âˆˆ multiSetPairs S T â†” X.map Prod.fst = S âˆ§ X.map Prod.snd = T := by
  rw [mem_multiSetPairs_iff_exist_toList_perm _ hlen]
  constructor
  Â· intro h
    obtain âŸ¨l, l2, h1, h2, h3âŸ© := h
    constructor
    Â· trans Multiset.ofList (List.map Prod.fst X.toList)
      Â· rw [â† Multiset.map_coe]
        simp
      trans Multiset.ofList S.toList
      swap
      Â· simp
      refine Multiset.coe_eq_coe.mpr ?_
      trans (List.map Prod.fst (l.zip l2))
      Â· apply List.Perm.map Prod.fst
        exact h3
      rw [â† List.unzip_fst, List.unzip_zip]
      simp only
      trans (Multiset.sort LE.le S)
      Â· exact h1
      rw [â† Multiset.coe_eq_coe]
      simp only [Multiset.sort_eq, Multiset.coe_toList]
      have hL1 := List.Perm.length_eq h1
      have hL2 := List.Perm.length_eq h2
      simp at hL1 hL2
      omega
    Â· trans Multiset.ofList (List.map Prod.snd X.toList)
      Â· rw [â† Multiset.map_coe]
        simp
      trans Multiset.ofList T.toList
      swap
      Â· simp
      refine Multiset.coe_eq_coe.mpr ?_
      trans (List.map Prod.snd (l.zip l2))
      Â· apply List.Perm.map Prod.snd
        exact h3
      rw [â† List.unzip_snd, List.unzip_zip]
      simp only
      trans (Multiset.sort LE.le T)
      Â· exact h2
      rw [â† Multiset.coe_eq_coe]
      simp only [Multiset.sort_eq, Multiset.coe_toList]
      have hL1 := List.Perm.length_eq h1
      have hL2 := List.Perm.length_eq h2
      simp at hL1 hL2
      omega
  Â· intro h
    use (List.map Prod.fst X.toList), (List.map Prod.snd X.toList)
    constructor
    Â· rw [â† Multiset.coe_eq_coe]
      simp only [Multiset.sort_eq]
      rw [â† Multiset.map_coe]
      simpa using h.1
    constructor
    Â· rw [â† Multiset.coe_eq_coe]
      simp only [Multiset.sort_eq]
      rw [â† Multiset.map_coe]
      simpa using h.2
    Â· rw [â† List.unzip_fst, â† List.unzip_snd, List.zip_unzip]

/-!

## fiveAnomalyCoefficient membership

-/

/-- Given a multiset `N` corresponding to hypercharge fluxes, and a multiset
  `Q` corresponding to charges for five bar matter, `anomalyCoefficientSet N Q`
  is the finset of all possible
  anomaly coefficents that can occur for this `N` and `Q`. -/
def fiveAnomalyCoefficientSet (N Q : Multiset â„¤ ): Finset (â„¤ Ã— â„¤) :=
  ((multiSetPairs N Q).val.map
      fun Nq => ((Nq.map fun a => a.2 * a.1).sum,
      (Nq.map fun a => a.2 * a.2 * a.1).sum)).toFinset

lemma fiveAnomalyCoefficient_mem_anomalyCoefficientSet :
    ğ“œ.fiveAnomalyCoefficient âˆˆ fiveAnomalyCoefficientSet (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.N)
      (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.q) := by
  simp [fiveAnomalyCoefficientSet]
  use ğ“œ.quantaBarFiveMatter.map (fun a => (a.N, a.q))
  constructor
  Â· rw [mem_multiSetPairs_of_proj]
    Â· simp
    Â· simp
  Â· rw [fiveAnomalyCoefficient]
    simp

/-- Given a multiset `N` corresponding to hypercharge fluxes, and a multiset
  `Q` corresponding to charges for ten-dimension matter, `anomalyCoefficientSet N Q`
  is the finset of all possible
  anomaly coefficents that can occur for this `N` and `Q`. -/
def tenAnomalyCoefficientSet (N Q : Multiset â„¤ ): Finset (â„¤ Ã— â„¤) :=
  ((multiSetPairs N Q).val.map
      fun Nq => ((Nq.map fun a => a.2 * a.1).sum,
     3 * (Nq.map fun a => a.2 * a.2 * a.1).sum)).toFinset

lemma tenAnomalyCoefficient_mem_anomalyCoefficientSet :
    ğ“œ.tenAnomalyCoefficient âˆˆ tenAnomalyCoefficientSet (ğ“œ.quantaTen.map QuantaTen.N)
      (ğ“œ.quantaTen.map QuantaTen.q) := by
  simp [tenAnomalyCoefficientSet]
  use ğ“œ.quantaTen.map (fun a => (a.N, a.q))
  constructor
  Â· rw [mem_multiSetPairs_of_proj]
    Â· simp
    Â· simp
  Â· rw [tenAnomalyCoefficient]
    simp

/-!

## fiveAnomalyCoefficient membership for different cardinialities

-/

lemma fiveAnomalyCoefficient_mem_anomalyCoefficientSet_card_three
    (he : ğ“œ.NoExotics) (h3 : ğ“œ.ThreeChiralFamiles)
    (h3L : ğ“œ.ThreeLeptonDoublets) (hcard : ğ“œ.quantaBarFiveMatter.card = 3):
    ğ“œ.fiveAnomalyCoefficient âˆˆ
      fiveAnomalyCoefficientSet {-3, 1, 2} (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.q) âˆª
      fiveAnomalyCoefficientSet {-2, -1, 3} (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.q) âˆª
      fiveAnomalyCoefficientSet {-2, 0, 2} (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.q) âˆª
      fiveAnomalyCoefficientSet {-2, 1, 1} (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.q) âˆª
      fiveAnomalyCoefficientSet {-1, -1, 2} (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.q) âˆª
      fiveAnomalyCoefficientSet {-1, 0, 1} (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.q) âˆª
      fiveAnomalyCoefficientSet {0, 0, 0} (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.q) := by
  have h1 :=  ğ“œ.fiveAnomalyCoefficient_mem_anomalyCoefficientSet
  have h2 := ğ“œ.quantaBarFiveMatter_N_mem he h3 h3L
  have hcard' : (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.N).card = 3 := by
    simpa using hcard
  generalize (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.N) = N at *
  fin_cases h2
  all_goals
    simp at hcard'
  all_goals
    simp_all

end MatterContent

end SU5U1

end FTheory
