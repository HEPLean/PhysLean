/-
Copyright (c) 2025 Zhi Kai Pong. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, Joseph Tooby-Smith, Lode Vermeulen
-/
import PhysLean.SpaceAndTime.Space.Distributions
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.Calculus.FDeriv.Symmetric
import Mathlib.Analysis.SpecialFunctions.Pow.Deriv
import Mathlib.Analysis.Calculus.Gradient.Basic
/-!

# Translations on space

-/

section

variable
  {ùïú} [NontriviallyNormedField ùïú]
  {X} [NormedAddCommGroup X] [NormedSpace ‚Ñù  X]
  {Y} [NormedAddCommGroup Y] [NormedSpace ùïú Y]
  {Œπ : Type*} [Fintype Œπ] {Y' : Œπ ‚Üí Type*} [‚àÄ i, NormedAddCommGroup (Y' i)]
  [‚àÄ i, NormedSpace ùïú (Y' i)] {Œ¶ : X ‚Üí ‚àÄ i, Y' i} {x : X}

namespace Space


noncomputable instance {d} : VAdd (EuclideanSpace ‚Ñù (Fin d)) (Space d) where
  vadd v s := v + s

noncomputable instance {d} : AddAction (EuclideanSpace ‚Ñù (Fin d)) (Space d) where
  add_vadd v1 v2 s := by simp [add_assoc]
  zero_vadd s := by simp

/-!

## Translations of distributions

-/


open Distribution
open SchwartzMap


noncomputable def translateSchwartz {d : ‚Ñï} (a : EuclideanSpace ‚Ñù (Fin d)) :
    ùì¢(Space d, X) ‚ÜíL[‚Ñù] ùì¢(Space d, X) :=
  SchwartzMap.compCLM (ùïú := ‚Ñù)
      (g := fun x => x - a)
      (by
        apply Function.HasTemperateGrowth.of_fderiv (k := 1) (C := 1 + ‚Äña‚Äñ)
        ¬∑ have hx : (fderiv ‚Ñù (fun x => x - a)) =
              fun _ => ContinuousLinearMap.id ‚Ñù (EuclideanSpace ‚Ñù (Fin d)) := by
            funext x
            simp only
            erw [fderiv_sub]
            simp only [fderiv_id', fderiv_fun_const, Pi.zero_apply, sub_zero]
            fun_prop
            fun_prop
          rw [hx]
          exact
            Function.HasTemperateGrowth.const
              (ContinuousLinearMap.id ‚Ñù (EuclideanSpace ‚Ñù (Fin d)))
        ¬∑ fun_prop
        ¬∑ intro x
          simp only [pow_one]
          trans ‚Äñx‚Äñ + ‚Äña‚Äñ
          ¬∑ apply norm_sub_le
          simp [mul_add, add_mul]
          trans 1 + (‚Äñx‚Äñ + ‚Äña‚Äñ)
          ¬∑ simp
          trans (1 + (‚Äñx‚Äñ + ‚Äña‚Äñ)) + ‚Äñx‚Äñ * ‚Äña‚Äñ
          ¬∑ simp
            positivity
          ring_nf
          rfl) (by
          simp only
          use 1, (1 + ‚Äña‚Äñ)
          intro x
          simp only [pow_one]
          apply (norm_le_norm_add_norm_sub' x a).trans
          trans 1 + (‚Äña‚Äñ + ‚Äñx - a‚Äñ)
          ¬∑ simp
          trans (1 + (‚Äña‚Äñ + ‚Äñx - a‚Äñ)) + ‚Äña‚Äñ * ‚Äñx - a‚Äñ
          ¬∑ simp
            positivity
          ring_nf
          rfl)

@[simp]
lemma translateSchwartz_apply {d : ‚Ñï} (a : EuclideanSpace ‚Ñù (Fin d))
    (Œ∑ : ùì¢(Space d, X)) (x : Space d) :
    translateSchwartz a Œ∑ x = Œ∑ (x - a) := rfl

lemma translateSchwartz_coe_eq {d : ‚Ñï} (a : EuclideanSpace ‚Ñù (Fin d))
    (Œ∑ : ùì¢(Space d, X)) :
    (translateSchwartz a Œ∑ : Space d ‚Üí X) = fun x => Œ∑ (x - a) := by
  ext
  simp

noncomputable def translateD {d : ‚Ñï} (a : EuclideanSpace ‚Ñù (Fin d)) :
    ((Space d) ‚Üíd[‚Ñù] X) ‚Üí‚Çó[‚Ñù] ((Space d) ‚Üíd[‚Ñù] X) where
  toFun T := T.comp (translateSchwartz (-a))
  map_add' T1 T2 := by
    ext Œ∑
    simp
  map_smul' c T := by
    simp

lemma translateD_apply {d : ‚Ñï} (a : EuclideanSpace ‚Ñù (Fin d))
    (T : (Space d) ‚Üíd[‚Ñù] X) (Œ∑ : ùì¢(Space d, ‚Ñù)) :
    translateD a T Œ∑ = T (translateSchwartz (-a) Œ∑) := rfl

open InnerProductSpace

@[simp]
lemma translateD_gradD {d : ‚Ñï} (a : EuclideanSpace ‚Ñù (Fin d))
    (T : (Space d) ‚Üíd[‚Ñù] ‚Ñù) :
    gradD (translateD a T) = translateD a (gradD T) := by
  apply gradD_eq_of_inner
  intro Œ∑ y
  rw [translateD_apply, gradD_inner_eq]
  rw [fderivD_apply, fderivD_apply, translateD_apply]
  congr 2
  ext x
  simp
  change fderiv ‚Ñù Œ∑ (x + a) y = fderiv ‚Ñù _ x y
  rw [translateSchwartz_coe_eq]
  simp only [sub_neg_eq_add]
  rw [fderiv_comp_add_right]

open MeasureTheory
lemma translateD_ofFunction {d : ‚Ñï} (a : EuclideanSpace ‚Ñù (Fin d.succ))
    (f : Space d.succ ‚Üí X) (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable f volume) :
    translateD a (ofFunction f hf hae) =
    ofFunction (fun x => f (x - a)) (IsDistBounded.comp_add_right hf fun i => -a i)
    (by
      change AEStronglyMeasurable (f ‚àò fun x => x - a) volume
      rw [MeasureTheory.MeasurePreserving.aestronglyMeasurable_comp_iff (Œºb := volume)]
      ¬∑ fun_prop
      ¬∑ exact measurePreserving_sub_right volume a
      ¬∑ exact measurableEmbedding_subRight a) := by
  ext Œ∑
  rw [translateD_apply, ofFunction_apply, ofFunction_apply]
  trans  ‚à´ (x : EuclideanSpace ‚Ñù (Fin d.succ)), Œ∑ ((x - a) + a) ‚Ä¢ f (x - a); swap
  ¬∑ simp
  let f' := fun x : EuclideanSpace ‚Ñù (Fin d.succ) => Œ∑ (x  + a) ‚Ä¢ f (x)
  change _ = ‚à´ (x : EuclideanSpace ‚Ñù (Fin d.succ)), f' (x - a)
  rw [MeasureTheory.integral_sub_right_eq_self]
  congr
  funext x
  simp [f']

@[simp]
lemma divD_translateD {d : ‚Ñï} (a : EuclideanSpace ‚Ñù (Fin d))
    (T : (Space d) ‚Üíd[‚Ñù] EuclideanSpace ‚Ñù (Fin d) ) :
    divD (translateD a T) = translateD a (divD T) := by
  ext Œ∑
  rw [divD_apply_eq_sum_fderivD]
  rw [translateD_apply, divD_apply_eq_sum_fderivD]
  congr
  funext i
  rw [fderivD_apply, fderivD_apply, translateD_apply]
  simp
  have h1 : ((translateSchwartz (-a)) ((SchwartzMap.evalCLM (ùïú := ‚Ñù) (basis i)) ((fderivCLM ‚Ñù) Œ∑)))
      = ((SchwartzMap.evalCLM (ùïú := ‚Ñù) (basis i))
        ((fderivCLM ‚Ñù) ((translateSchwartz (-a)) Œ∑))) := by
    ext x
    rw [translateSchwartz_apply]
    simp
    change fderiv ‚Ñù Œ∑ (x + a) (basis i) = fderiv ‚Ñù _ x (basis i)
    rw [translateSchwartz_coe_eq]
    simp
    rw [fderiv_comp_add_right]
  rw [h1]


end Space
