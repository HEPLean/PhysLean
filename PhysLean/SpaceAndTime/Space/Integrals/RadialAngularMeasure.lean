/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Mathematics.Distribution.Basic
import PhysLean.SpaceAndTime.Space.Integrals.Basic
/-!

# The radial angular measure on Space

## i. Overview

The normal measure on `Space d` is `r^(d-1) dr dÎ©` in spherical coordinates,
where `dÎ©` is the angular measure on the unit sphere. The radial angular measure
is the measure `dr dÎ©`, cancelling the radius contribution from the measure in spherical
coordinates.

This file is equivalent to `invPowMeasure`, which will slowly be deprecated.

## ii. Key results

- `radialAngularMeasure`: The radial angular measure on `Space d`.

## iii. Table of contents

- A. The definition of the radial angular measure
  - A.1. Basic equalities
- B. Integrals with respect to radialAngularMeasure
- C. The radialAngularMeasure on balls
- D. Integrability conditions
- E. HasTemperateGrowth of measures
  - E.1. Integrability of powers
  - E.2. radialAngularMeasure has temperate growth

## iv. References

-/
open SchwartzMap NNReal Real
noncomputable section

variable (ğ•œ : Type) {E F F' : Type} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup F']

variable [NormedSpace â„ E] [NormedSpace â„ F]

namespace Space

open MeasureTheory

/-!

## A. The definition of the radial angular measure

-/

/-- The measure on `Space d` weighted by `1 / â€–xâ€– ^ (d - 1)`. -/
def radialAngularMeasure {d : â„•} : Measure (Space d) :=
  volume.withDensity (fun x : Space d => ENNReal.ofReal (1 / â€–xâ€– ^ (d - 1)))

/-!

### A.1. Basic equalities

-/

lemma radialAngularMeasure_eq_volume_withDensity {d : â„•} : radialAngularMeasure =
    volume.withDensity (fun x : Space d => ENNReal.ofReal (1 / â€–xâ€– ^ (d - 1))) := by
  rfl

@[simp]
lemma radialAngularMeasure_zero_eq_volume :
    radialAngularMeasure (d := 0) = volume := by
  simp [radialAngularMeasure]

/-!

### A.2. SFinite property

-/

instance (d : â„•) : SFinite (radialAngularMeasure (d := d)) := by
  dsimp [radialAngularMeasure]
  infer_instance

/-!

## B. Integrals with respect to radialAngularMeasure

-/

lemma integral_radialAngularMeasure {d : â„•} (f : Space d â†’ F) :
    âˆ« x, f x âˆ‚radialAngularMeasure = âˆ« x, (1 / â€–xâ€– ^ (d - 1)) â€¢ f x := by
  dsimp [radialAngularMeasure]
  erw [integral_withDensity_eq_integral_smul (by fun_prop)]
  congr
  funext x
  simp only [one_div]
  refine Eq.symm (Mathlib.Tactic.LinearCombination.smul_eq_const ?_ (f x))
  simp

lemma lintegral_radialMeasure {d : â„•} (f : Space d â†’ ENNReal) (hf : Measurable f) :
    âˆ«â» x, f x âˆ‚radialAngularMeasure = âˆ«â» x, ENNReal.ofReal (1 / â€–xâ€– ^ (d - 1)) * f x := by
  dsimp [radialAngularMeasure]
  rw [lintegral_withDensity_eq_lintegral_mul]
  simp only [one_div, Pi.mul_apply]
  Â· fun_prop
  Â· fun_prop

lemma lintegral_radialMeasure_eq_spherical_mul (d : â„•) (f : Space d.succ â†’ ENNReal)
    (hf : Measurable f) :
    âˆ«â» x, f x âˆ‚radialAngularMeasure = âˆ«â» x, f (x.2.1 â€¢ x.1.1)
      âˆ‚(volume (Î± := Space d.succ).toSphere.prod (Measure.volumeIoiPow 0)) := by
  rw [lintegral_radialMeasure, lintegral_volume_eq_spherical_mul]
  apply lintegral_congr_ae
  filter_upwards with x
  have hx := x.2.2
  simp [Nat.succ_eq_add_one, -Subtype.coe_prop] at hx
  simp [norm_smul]
  rw [abs_of_nonneg (le_of_lt x.2.2)]
  trans ENNReal.ofReal (â†‘x.2 ^ d * (x.2.1 ^ d)â»Â¹) * f (x.2.1 â€¢ â†‘x.1.1)
  Â· rw [ENNReal.ofReal_mul]
    ring
    have h2 := x.2.2
    simp at h2
    positivity
  trans ENNReal.ofReal 1 * f (x.2.1 â€¢ â†‘x.1.1)
  Â· congr
    field_simp
  simp only [ENNReal.ofReal_one, Nat.succ_eq_add_one, one_mul]
  fun_prop
  fun_prop

/-!

## C. The radialAngularMeasure on balls

-/

@[simp]
lemma radialAngularMeasure_closedBall (r : â„) :
    radialAngularMeasure (Metric.closedBall (0 : Space 3) r) = ENNReal.ofReal (4 * Ï€ * r) := by
  rw [â† setLIntegral_one, â† MeasureTheory.lintegral_indicator,
    lintegral_radialMeasure_eq_spherical_mul]
  rotate_left
  Â· refine (measurable_indicator_const_iff 1).mpr ?_
    exact measurableSet_closedBall
  Â· exact measurableSet_closedBall
  have h1 (x : (Metric.sphere (0 : Space) 1) Ã— â†‘(Set.Ioi (0 : â„))) :
      (Metric.closedBall (0 : Space) r).indicator (fun x => (1 : ENNReal)) (x.2.1 â€¢ x.1.1) =
      (Set.univ Ã—Ë¢ {a | a.1 â‰¤ r}).indicator (fun x => 1) x := by
    refine Set.indicator_const_eq_indicator_const ?_
    simp [norm_smul]
    rw [abs_of_nonneg (le_of_lt x.2.2)]
  simp [h1]
  rw [MeasureTheory.lintegral_indicator, setLIntegral_one]
  rotate_left
  Â· refine MeasurableSet.prod ?_ ?_
    Â· exact MeasurableSet.univ
    Â· simp
      fun_prop
  rw [MeasureTheory.Measure.prod_prod]
  simp [Measure.volumeIoiPow]
  rw [MeasureTheory.Measure.comap_apply]
  rotate_left
  Â· exact Subtype.val_injective
  Â· intro s hs
    refine MeasurableSet.subtype_image ?_ hs
    exact measurableSet_Ioi
  Â· simp
    fun_prop
  trans 3 * ENNReal.ofReal (4 / 3 * Ï€) * volume (Î± := â„) (Set.Ioc 0 r)
  Â· congr
    ext x
    simp only [Set.mem_image, Set.mem_setOf_eq, Subtype.exists, Set.mem_Ioi, exists_and_left,
      exists_prop, exists_eq_right_right, Set.mem_Ioc]
    grind
  simp only [volume_Ioc, sub_zero]
  trans ENNReal.ofReal (3 * ((4 / 3 * Ï€))) * ENNReal.ofReal r
  Â· simp [ENNReal.ofReal_mul]
  field_simp
  rw [â† ENNReal.ofReal_mul]
  simp only [Nat.ofNat_pos, mul_nonneg_iff_of_pos_left]
  positivity

lemma radialAngularMeasure_real_closedBall (r : â„) (hr : 0 < r) :
    radialAngularMeasure.real (Metric.closedBall (0 : Space 3) r) = 4 * Ï€ * r := by
  change (radialAngularMeasure (Metric.closedBall (0 : Space 3) r)).toReal = _
  simp only [radialAngularMeasure_closedBall, ENNReal.toReal_ofReal_eq_iff]
  positivity

/-!

## D. Integrability conditions

-/

lemma integrable_radialAngularMeasure_iff {d : â„•} {f : Space d â†’ F} :
    Integrable f (radialAngularMeasure (d := d)) â†”
      Integrable (fun x => (1 / â€–xâ€– ^ (d - 1)) â€¢ f x) volume := by
  dsimp [radialAngularMeasure]
  erw [integrable_withDensity_iff_integrable_smulâ‚€ (by fun_prop)]
  simp only [one_div]
  refine integrable_congr ?_
  filter_upwards with x
  rw [Real.toNNReal_of_nonneg, NNReal.smul_def]
  simp only [inv_nonneg, norm_nonneg, pow_nonneg, coe_mk]
  positivity

omit [NormedSpace â„ F] in
lemma integrable_radialAngularMeasure_of_spherical {d : â„•} (f : Space d.succ â†’ F)
    (fae : AEStronglyMeasurable f radialAngularMeasure)
    (ht : StronglyMeasurable f)
    (hf : Integrable (fun x => f (x.2.1 â€¢ x.1.1))
    (volume (Î± := Space d.succ).toSphere.prod (Measure.volumeIoiPow 0))) :
    Integrable f radialAngularMeasure := by
  have h1 := hf.1
  have h2 := hf.2
  refine âŸ¨fae, ?_âŸ©
  Â· rw [hasFiniteIntegral_iff_norm]
    rw [lintegral_radialMeasure_eq_spherical_mul]
    rw [â† hasFiniteIntegral_iff_norm]
    exact h2
    Â· simp
      refine StronglyMeasurable.enorm ht

/-!

## E. HasTemperateGrowth of measures

-/

/-!

### E.1. Integrability of powers

-/
private lemma integrable_neg_pow_on_ioi (n : â„•) :
    IntegrableOn (fun x : â„ => (|((1 : â„) + x) ^ (- (n + 2) : â„)|)) (Set.Ioi 0) := by
  rw [MeasureTheory.integrableOn_iff_comap_subtypeVal]
  rw [â† MeasureTheory.integrable_smul_measure (c := n + 1)]
  apply MeasureTheory.integrable_of_integral_eq_one
  trans (n + 1) * âˆ« (x : â„) in Set.Ioi 0, ((1 + x) ^ (- (n + 2) : â„)) âˆ‚volume
  Â· rw [â† MeasureTheory.integral_subtype_comap]
    simp only [neg_add_rev, Function.comp_apply, integral_smul_measure, smul_eq_mul]
    congr
    funext x
    simp only [abs_eq_self]
    apply Real.rpow_nonneg
    apply add_nonneg
    Â· exact zero_le_one' â„
    Â· exact le_of_lt x.2
    exact measurableSet_Ioi
  have h0 (x : â„) (hx : x âˆˆ Set.Ioi 0) : ((1 : â„) + â†‘x) ^ (- (n + 2) : â„) =
      ((1 + x) ^ ((n + 2)))â»Â¹ := by
    rw [â† Real.rpow_natCast]
    rw [â† Real.inv_rpow]
    rw [â† Real.rpow_neg_one, â† Real.rpow_mul]
    simp only [neg_mul, one_mul]
    simp only [neg_add_rev, Nat.cast_add, Nat.cast_ofNat]
    have hx : 0 < x := hx
    positivity
    apply add_nonneg
    Â· exact zero_le_one' â„
    Â· exact le_of_lt hx
  trans (n + 1) * âˆ« (x : â„) in Set.Ioi 0, ((1 + x) ^ (n + 2))â»Â¹ âˆ‚volume
  Â· congr 1
    refine setIntegral_congr_aeâ‚€ ?_ ?_
    Â· simp
    filter_upwards with x hx
    rw [h0 x hx]
  trans (n + 1) * âˆ« (x : â„) in Set.Ioi 1, (x ^ (n + 2))â»Â¹ âˆ‚volume
  Â· congr 1
    let f := fun x : â„ => (x ^ (n + 2))â»Â¹
    change âˆ« (x : â„) in Set.Ioi 0, f (1 + x) âˆ‚volume = âˆ« (x : â„) in Set.Ioi 1, f x âˆ‚volume
    let fa := fun x : â„ => 1 + x
    change âˆ« (x : â„) in Set.Ioi 0, f (fa x) âˆ‚volume = _
    rw [â† MeasureTheory.MeasurePreserving.setIntegral_image_emb (Î½ := volume)]
    simp [fa]
    simp [fa]
    exact measurePreserving_add_left volume 1
    simp [fa]
    exact measurableEmbedding_addLeft 1
  Â· trans (n + 1) * âˆ« (x : â„) in Set.Ioi 1, (x ^ (- (n + 2) : â„)) âˆ‚volume
    Â· congr 1
      refine setIntegral_congr_aeâ‚€ ?_ ?_
      Â· simp
      filter_upwards with x hx
      have hx : 1 < x := hx
      rw [â† Real.rpow_natCast]
      rw [â† Real.inv_rpow]
      rw [â† Real.rpow_neg_one, â† Real.rpow_mul]
      simp only [neg_mul, one_mul]
      simp only [Nat.cast_add, Nat.cast_ofNat, neg_add_rev]
      positivity
      positivity
    rw [integral_Ioi_rpow_of_lt]
    field_simp
    have h0 : (-2 + -(n : â„) + 1) â‰  0 := by
      by_contra h
      have h1 : (1 : â„) - 0 = 2 + n := by
        rw [â† h]
        ring
      simp at h1
      linarith
    simp only [neg_add_rev, Real.one_rpow, mul_one]
    field_simp
    ring
    linarith
    linarith
  Â· simp
  Â· simp
  Â· simp

lemma radialAngularMeasure_integrable_pow_neg_two {d : â„•} :
    Integrable (fun x : Space d => (1 + â€–xâ€–) ^ (- (d + 1) : â„))
      radialAngularMeasure := by
  match d with
  | 0 => simp
  | dm1 + 1 =>
  apply integrable_radialAngularMeasure_of_spherical
  Â· apply AEMeasurable.aestronglyMeasurable
    fun_prop
  Â· fun_prop
  simp [norm_smul]
  rw [MeasureTheory.integrable_prod_iff]
  rotate_left
  Â· apply AEMeasurable.aestronglyMeasurable
    fun_prop
  refine âŸ¨?_, by simpâŸ©
  filter_upwards with x
  simp [Measure.volumeIoiPow]
  let f := fun x : â„ => |(1 + x) ^ (- (dm1 + 2) : â„)|
  have h0 : (fun (y : â†‘(Set.Ioi 0)) => (1 + |y.1|) ^ (-1 + (-1 + -â†‘dm1) : â„)) =
      f âˆ˜ Subtype.val := by
    funext x
    rcases x with âŸ¨x, hxâŸ©
    simp at hx
    simp [f]
    ring_nf
    rw [abs_of_nonneg (by positivity), abs_of_nonneg (by positivity)]
  rw [h0]
  change Integrable (f âˆ˜ Subtype.val) _
  rw [â† MeasureTheory.integrableOn_iff_comap_subtypeVal measurableSet_Ioi]
  exact integrable_neg_pow_on_ioi dm1

/-!

### E.2. radialAngularMeasure has temperate growth

-/

instance (d : â„•) : Measure.HasTemperateGrowth (radialAngularMeasure (d := d)) where
  exists_integrable := by
    use d + 1
    simpa using radialAngularMeasure_integrable_pow_neg_two (d := d)

end Space
