/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, Joseph Tooby-Smith, Lode Vermeulen
-/
import PhysLean.SpaceAndTime.Space.Derivatives.Grad
/-!

# Divergence on Space

## i. Overview

In this module we define the divergence operator on functions and
distributions from `Space d` to `EuclideanSpace ‚Ñù (Fin d)`, and prove various basic
properties about it.

## ii. Key results

- `div` : The divergence operator on functions from `Space d` to `EuclideanSpace ‚Ñù (Fin d)`.
- `distDiv` : The divergence operator on distributions from `Space d` to `EuclideanSpace ‚Ñù (Fin d)`.
- `distDiv_ofFunction` : The divergence of a distribution from a bounded function.

## iii. Table of contents

- A. The divergence on functions
  - A.1. The divergence on the zero function
  - A.2. The divergence on a constant function
  - A.3. The divergence distributes over addition
  - A.4. The divergence distributes over scalar multiplication
  - A.5. The divergence of a linear map is a linear map
- B. Divergence of distributions
  - B.1. Basic equalities
  - B.2. Divergence on distributions from bounded functions

## iv. References

-/

namespace Space

variable {W} [NormedAddCommGroup W] [NormedSpace ‚Ñù W]

/-!

## A. The divergence on functions

-/

/-- The vector calculus operator `div`. -/
noncomputable def div {d} (f : Space d ‚Üí EuclideanSpace ‚Ñù (Fin d)) :
    Space d ‚Üí ‚Ñù := fun x =>
  -- get i-th component of `f`
  let fi i x := (f x) i
  -- derivative of i-th component in i-th coordinate
  -- ‚àÇf·µ¢/‚àÇx‚±º
  let df i x := ‚àÇ[i] (fi i) x
  ‚àë i, df i x

@[inherit_doc div]
macro (name := divNotation) "‚àá" "‚¨ù" f:term:100 : term => `(div $f)

/-!

### A.1. The divergence on the zero function

-/

@[simp]
lemma div_zero : ‚àá ‚¨ù (0 : Space d ‚Üí EuclideanSpace ‚Ñù (Fin d)) = 0 := by
  unfold div Space.deriv Finset.sum
  simp only [Pi.ofNat_apply, fderiv_fun_const, ContinuousLinearMap.zero_apply, Multiset.map_const',
    Finset.card_val, Finset.card_univ, Fintype.card_fin, Multiset.sum_replicate, smul_zero]
  rfl

/-!

### A.2. The divergence on a constant function

-/

@[simp]
lemma div_const : ‚àá ‚¨ù (fun _ : Space d => v) = 0 := by
  unfold div Space.deriv Finset.sum
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.zero_apply, Multiset.map_const',
    Finset.card_val, Finset.card_univ, Fintype.card_fin, Multiset.sum_replicate, smul_zero]
  rfl

/-!

### A.3. The divergence distributes over addition

-/

lemma div_add (f1 f2 : Space d ‚Üí EuclideanSpace ‚Ñù (Fin d))
    (hf1 : Differentiable ‚Ñù f1) (hf2 : Differentiable ‚Ñù f2) :
    ‚àá ‚¨ù (f1 + f2) = ‚àá ‚¨ù f1 + ‚àá ‚¨ù f2 := by
  unfold div
  simp only [Pi.add_apply]
  funext x
  simp only [Pi.add_apply]
  rw [‚Üê Finset.sum_add_distrib]
  congr
  funext i
  simp [Space.deriv]
  rw [fderiv_fun_add]
  simp only [ContinuousLinearMap.add_apply]
  ¬∑ fun_prop
  ¬∑ fun_prop

/-!

### A.4. The divergence distributes over scalar multiplication

-/

lemma div_smul (f : Space d ‚Üí EuclideanSpace ‚Ñù (Fin d)) (k : ‚Ñù)
    (hf : Differentiable ‚Ñù f) :
    ‚àá ‚¨ù (k ‚Ä¢ f) = k ‚Ä¢ ‚àá ‚¨ù f := by
  unfold div
  simp only [Pi.smul_apply]
  funext x
  simp only [Pi.smul_apply, smul_eq_mul]
  rw [Finset.mul_sum]
  congr
  funext i
  simp [Space.deriv]
  rw [fderiv_const_mul]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  ¬∑ fun_prop

/-!

### A.5. The divergence of a linear map is a linear map

-/

lemma div_linear_map (f : W ‚Üí Space 3 ‚Üí EuclideanSpace ‚Ñù (Fin 3))
    (hf : ‚àÄ w, Differentiable ‚Ñù (f w))
    (hf' : IsLinearMap ‚Ñù f) :
    IsLinearMap ‚Ñù (fun w => ‚àá ‚¨ù (f w)) := by
  constructor
  ¬∑ intro w w'
    rw [hf'.map_add]
    rw [div_add]
    repeat fun_prop
  ¬∑ intros k w
    rw [hf'.map_smul]
    rw [div_smul]
    fun_prop

/-!

## B. Divergence of distributions

-/

open MeasureTheory SchwartzMap InnerProductSpace Distribution

/-- The divergence of a distribution `(Space d) ‚Üíd[‚Ñù] (EuclideanSpace ‚Ñù (Fin d))` as a distribution
  `(Space d) ‚Üíd[‚Ñù] ‚Ñù`. -/
noncomputable def distDiv {d} :
    ((Space d) ‚Üíd[‚Ñù] (EuclideanSpace ‚Ñù (Fin d))) ‚Üí‚Çó[‚Ñù] (Space d) ‚Üíd[‚Ñù] ‚Ñù where
  toFun f :=
    let trace : (Space d ‚ÜíL[‚Ñù] (EuclideanSpace ‚Ñù (Fin d))) ‚ÜíL[‚Ñù] ‚Ñù := {
      toFun v := ‚àë i, ‚ü™v (basis i), EuclideanSpace.single i 1‚ü´_‚Ñù
      map_add' v1 v2 := by
        simp only [ContinuousLinearMap.add_apply, EuclideanSpace.inner_single_right, PiLp.add_apply,
          conj_trivial, one_mul]
        rw [Finset.sum_add_distrib]
      map_smul' a v := by
        simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, EuclideanSpace.inner_single_right,
          PiLp.smul_apply, smul_eq_mul, conj_trivial, one_mul, RingHom.id_apply]
        rw [Finset.mul_sum]
      cont := by fun_prop}
    trace.comp (Distribution.fderivD ‚Ñù f)
  map_add' f1 f2 := by
    ext x
    simp
  map_smul' a f := by
    ext x
    simp

/-!

### B.1. Basic equalities

-/

lemma distDiv_apply_eq_sum_fderivD {d}
    (f : (Space d) ‚Üíd[‚Ñù] EuclideanSpace ‚Ñù (Fin d)) (Œ∑ : ùì¢(Space d, ‚Ñù)) :
    distDiv f Œ∑ = ‚àë i, fderivD ‚Ñù f Œ∑ (basis i) i := by
  simp [distDiv, EuclideanSpace.inner_single_right]

lemma distDiv_apply_eq_sum_distDeriv {d}
    (f : (Space d) ‚Üíd[‚Ñù] EuclideanSpace ‚Ñù (Fin d)) (Œ∑ : ùì¢(Space d, ‚Ñù)) :
    distDiv f Œ∑ = ‚àë i, distDeriv i f Œ∑ i := by
  rw [distDiv_apply_eq_sum_fderivD]
  rfl

/-!

### B.2. Divergence on distributions from bounded functions

-/

/-- The divergence of a distribution from a bounded function. -/
lemma distDiv_ofFunction {dm1 : ‚Ñï} {f : Space dm1.succ ‚Üí EuclideanSpace ‚Ñù (Fin dm1.succ)}
    {hf : IsDistBounded f} (Œ∑ : ùì¢(Space dm1.succ, ‚Ñù)) :
    distDiv (distOfFunction f hf) Œ∑ =
    - ‚à´ x : Space dm1.succ, ‚ü™f x, Space.grad Œ∑ x‚ü´_‚Ñù := by
  rw [distDiv_apply_eq_sum_fderivD]
  conv_rhs =>
    enter [1, 2, x]
    rw [grad_eq_sum, inner_sum]
  conv_lhs =>
    enter [2, i]
    rw [fderivD_apply, distOfFunction_apply]
  /- The following lemma could probably be moved out of this result. -/
  have integrable_lemma (i j : Fin (dm1 + 1)) :
      Integrable (fun x =>
        (((SchwartzMap.evalCLM ‚Ñù (Space dm1.succ) ‚Ñù (basis i))
        ((fderivCLM ‚Ñù (Space dm1.succ) ‚Ñù) Œ∑)) x ‚Ä¢ f x) j) volume := by
    simp only [PiLp.smul_apply]
    exact (hf.pi_comp j).integrable_space _
  rw [MeasureTheory.integral_finset_sum]
  ¬∑ simp
    congr
    funext i
    rw [MeasureTheory.eval_integral_piLp]
    ¬∑ congr
      funext x
      simp [inner_smul_right, EuclideanSpace.inner_single_right]
      left
      rw [deriv_eq_fderiv_basis]
    ¬∑ intro j
      exact integrable_lemma i j
  ¬∑ intro i hi
    simp only [Nat.succ_eq_add_one, inner_smul_right, EuclideanSpace.inner_single_right]
    convert integrable_lemma i i using 2
    rename_i x
    simp only [conj_trivial, one_mul, Nat.succ_eq_add_one, PiLp.smul_apply, smul_eq_mul,
      mul_eq_mul_right_iff]
    left
    rw [deriv_eq_fderiv_basis]
    rfl

end Space
