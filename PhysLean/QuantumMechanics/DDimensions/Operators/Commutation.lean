/-
Copyright (c) 2026 Gregory J. Loges. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gregory J. Loges
-/
import PhysLean.QuantumMechanics.DDimensions.Operators.AngularMomentum
/-!

# Commutation relations

-/

namespace QuantumMechanics
noncomputable section
open Constants
open SchwartzMap ContinuousLinearMap

/-- Kronecker delta, `Î´[i,j] = if i = j then 1 else 0`. -/
local macro "Î´[" i:term "," j:term "]" : term => `(if $i = $j then 1 else 0)

private lemma delta_symm (i j : Fin d) :
    (if i = j then A else B) = (if j = i then A else B) :=
  ite_cond_congr (Eq.propIntro (fun a â†¦ Eq.symm a) (fun a â†¦ Eq.symm a))

lemma lie_leibniz_left {d : â„•} (A B C : ð“¢(Space d, â„‚) â†’L[â„‚] ð“¢(Space d, â„‚)) :
    â…A âˆ˜L B, Câ† = A âˆ˜L â…B, Câ† + â…A, Câ† âˆ˜L B := by
  dsimp only [Bracket.bracket]
  simp only [ContinuousLinearMap.mul_def, comp_assoc, comp_sub, sub_comp, sub_add_sub_cancel]

lemma lie_leibniz_right {d : â„•} (A B C : ð“¢(Space d, â„‚) â†’L[â„‚] ð“¢(Space d, â„‚)) :
    â…A, B âˆ˜L Câ† = B âˆ˜L â…A, Câ† + â…A, Bâ† âˆ˜L C := by
  dsimp only [Bracket.bracket]
  simp only [ContinuousLinearMap.mul_def, comp_assoc, comp_sub, sub_comp, sub_add_sub_cancel']

/-
## Position / position commutators
-/

/-- Position operators commute: `[xáµ¢, xâ±¼] = 0`. -/
lemma position_commutation_position {d : â„•} (i j : Fin d) : â…ð±[i], ð±[j]â† = 0 := by
  dsimp only [Bracket.bracket]
  ext Ïˆ x
  simp only [coe_sub', coe_mul, Pi.sub_apply, Function.comp_apply, SchwartzMap.sub_apply,
    ContinuousLinearMap.zero_apply, SchwartzMap.zero_apply, positionOperator_apply]
  ring

/-
## Momentum / momentum commutators
-/

/-- Momentum operators commute: `[páµ¢, pâ±¼] = 0`. -/
lemma momentum_commutation_momentum {d : â„•} (i j : Fin d) : â…ð©[i], ð©[j]â† = 0 := by
  dsimp only [Bracket.bracket]
  ext Ïˆ x
  simp only [coe_sub', coe_mul, Pi.sub_apply, Function.comp_apply, SchwartzMap.sub_apply,
    ContinuousLinearMap.zero_apply, SchwartzMap.zero_apply, momentumOperator_apply_fun]
  rw [Space.deriv_const_smul _ ?_, Space.deriv_const_smul _ ?_]
  Â· rw [Space.deriv_commute _ (Ïˆ.smooth _), sub_self]
  Â· exact Space.deriv_differentiable (Ïˆ.smooth _) i
  Â· exact Space.deriv_differentiable (Ïˆ.smooth _) j

lemma momentum_momentum_eq {d : â„•} (i j : Fin d) : ð©[i] âˆ˜L ð©[j] = ð©[j] âˆ˜L ð©[i] := by
  rw [â† sub_eq_zero]
  exact momentum_commutation_momentum i j

lemma momentumSqr_commutation_momentum {d : â„•} (i : Fin d) :
    â…momentumOperatorSqr (d := d), ð©[i]â† = 0 := by
  dsimp only [Bracket.bracket, momentumOperatorSqr]
  rw [Finset.mul_sum, Finset.sum_mul, â† Finset.sum_sub_distrib]
  conv_lhs =>
    enter [2, j]
    simp only [ContinuousLinearMap.mul_def]
    rw [comp_assoc]
    rw [momentum_momentum_eq j i, â† comp_assoc]
    rw [momentum_momentum_eq j i, comp_assoc]
    rw [sub_self]
  rw [Finset.sum_const_zero]

/-
## Position / momentum commutators
-/

/-- The canonical commutation relations: `[xáµ¢, pâ±¼] = iâ„ Î´áµ¢â±¼ðŸ™`. -/
lemma position_commutation_momentum {d : â„•} (i j : Fin d) : â…ð±[i], ð©[j]â† =
    (Complex.I * â„ * Î´[i,j]) â€¢ ContinuousLinearMap.id â„‚ ð“¢(Space d, â„‚) := by
  dsimp only [Bracket.bracket]
  ext Ïˆ x
  simp only [ContinuousLinearMap.smul_apply, SchwartzMap.smul_apply, coe_id', id_eq, smul_eq_mul,
    coe_sub', coe_mul, Pi.sub_apply, Function.comp_apply, SchwartzMap.sub_apply]
  rw [positionOperator_apply, momentumOperator_apply_fun]
  rw [momentumOperator_apply, positionOperator_apply_fun]
  simp only [neg_mul, Pi.smul_apply, smul_eq_mul, mul_neg, sub_neg_eq_add]

  have h : (fun x â†¦ â†‘(x i) * Ïˆ x) = (fun (x : Space d) â†¦ x i) â€¢ Ïˆ := rfl
  rw [h]
  rw [Space.deriv_smul (by fun_prop) (SchwartzMap.differentiableAt Ïˆ)]
  rw [Space.deriv_component, delta_symm j i]
  simp only [mul_add, Complex.real_smul, ite_smul, one_smul, zero_smul, mul_ite, mul_one, mul_zero,
    ite_mul, zero_mul]
  ring

lemma position_position_commutation_momentum {d : â„•} (i j k : Fin d) : â…ð±[i] âˆ˜L ð±[j], ð©[k]â† =
    (Complex.I * â„ * Î´[i,k]) â€¢ ð±[j] + (Complex.I * â„ * Î´[j,k]) â€¢ ð±[i] := by
  rw [lie_leibniz_left]
  rw [position_commutation_momentum, position_commutation_momentum]
  rw [ContinuousLinearMap.comp_smul, ContinuousLinearMap.smul_comp]
  rw [id_comp, comp_id]
  rw [add_comm]

lemma position_commutation_momentum_momentum {d : â„•} (i j k : Fin d) : â…ð±[i], ð©[j] âˆ˜L ð©[k]â† =
    (Complex.I * â„ * Î´[i,k]) â€¢ ð©[j] + (Complex.I * â„ * Î´[i,j]) â€¢ ð©[k] := by
  rw [lie_leibniz_right]
  rw [position_commutation_momentum, position_commutation_momentum]
  rw [ContinuousLinearMap.comp_smul, ContinuousLinearMap.smul_comp]
  rw [id_comp, comp_id]

lemma position_commutation_momentumSqr {d : â„•} (i : Fin d) : â…ð±[i], ð©Â²â† =
    (2 * Complex.I * â„) â€¢ ð©[i] := by
  unfold momentumOperatorSqr
  rw [lie_sum]
  simp only [position_commutation_momentum_momentum]
  simp only [mul_ite, mul_one, mul_zero, ite_smul, zero_smul, Finset.sum_add_distrib,
    Finset.sum_ite_eq, Finset.mem_univ, â†“reduceIte]
  ext Ïˆ x
  simp only [ContinuousLinearMap.add_apply, coe_smul', Pi.smul_apply, SchwartzMap.add_apply,
    SchwartzMap.smul_apply, smul_eq_mul]
  ring

/-
## Angular momentum / position commutators
-/

lemma angularMomentum_commutation_position {d : â„•} (i j k : Fin d) : â…ð‹[i,j], ð±[k]â† =
    (Complex.I * â„ * Î´[i,k]) â€¢ ð±[j] - (Complex.I * â„ * Î´[j,k]) â€¢ ð±[i] := by
  unfold angularMomentumOperator
  rw [sub_lie]
  rw [lie_leibniz_left, lie_leibniz_left]
  rw [position_commutation_position, position_commutation_position]
  rw [â† lie_skew, position_commutation_momentum]
  rw [â† lie_skew, position_commutation_momentum]
  rw [delta_symm k i, delta_symm k j]
  simp only [ContinuousLinearMap.comp_neg, ContinuousLinearMap.comp_smul, comp_id, zero_comp,
    add_zero, add_comm, sub_neg_eq_add, â† sub_eq_add_neg]

/-
## Angular momentum / momentum commutators
-/

lemma angularMomentum_commutation_momentum {d : â„•} (i j k : Fin d) : â…ð‹[i,j], ð©[k]â† =
    (Complex.I * â„ * Î´[i,k]) â€¢ ð©[j] - (Complex.I * â„ * Î´[j,k]) â€¢ ð©[i] := by
  unfold angularMomentumOperator
  rw [sub_lie]
  rw [lie_leibniz_left, lie_leibniz_left]
  rw [momentum_commutation_momentum, momentum_commutation_momentum]
  rw [position_commutation_momentum, position_commutation_momentum]
  simp only [ContinuousLinearMap.smul_comp, id_comp, comp_zero, zero_add]

lemma angularMomentum_commutation_momentumSqr {d : â„•} (i j : Fin d) :
    â…ð‹[i,j], momentumOperatorSqr (d := d)â† = 0 := by
  unfold momentumOperatorSqr
  conv_lhs =>
    rw [lie_sum]
    enter [2, k]
    rw [lie_leibniz_right]
    rw [angularMomentum_commutation_momentum]
    simp only [comp_sub, comp_smulâ‚›â‚—, RingHom.id_apply, sub_comp, smul_comp]
    rw [momentum_momentum_eq _ i, momentum_momentum_eq j _]
  simp only [Finset.sum_add_distrib, Finset.sum_sub_distrib, mul_ite, mul_zero, ite_smul,
    zero_smul, Finset.sum_ite_eq, Finset.mem_univ, â†“reduceIte, sub_self, add_zero]

lemma angularMomentumSqr_commutation_momentumSqr {d : â„•} :
    â…angularMomentumOperatorSqr (d := d), momentumOperatorSqr (d := d)â† = 0 := by
  unfold angularMomentumOperatorSqr
  conv_lhs =>
    rw [sum_lie]
    enter [2, i]
    rw [sum_lie]
    enter [2, j]
    rw [smul_lie, lie_leibniz_left]
    rw [angularMomentum_commutation_momentumSqr]
    rw [comp_zero, zero_comp, add_zero, smul_zero]
  simp only [Finset.sum_const_zero]

/-
## Angular momentum / angular momentum commutators
-/

lemma angularMomentum_commutation_angularMomentum {d : â„•} (i j k l : Fin d) : â…ð‹[i,j], ð‹[k,l]â† =
    (Complex.I * â„ * Î´[i,k]) â€¢ ð‹[j,l] - (Complex.I * â„ * Î´[i,l]) â€¢ ð‹[j,k]
    - (Complex.I * â„ * Î´[j,k]) â€¢ ð‹[i,l] + (Complex.I * â„ * Î´[j,l]) â€¢ ð‹[i,k] := by
  nth_rw 2 [angularMomentumOperator]
  rw [lie_sub]
  rw [lie_leibniz_right, lie_leibniz_right]
  rw [angularMomentum_commutation_momentum, angularMomentum_commutation_position]
  rw [angularMomentum_commutation_momentum, angularMomentum_commutation_position]
  unfold angularMomentumOperator
  simp only [ContinuousLinearMap.comp_sub, ContinuousLinearMap.sub_comp,
    ContinuousLinearMap.comp_smul, ContinuousLinearMap.smul_comp]
  ext Ïˆ x
  simp only [mul_ite, mul_one, mul_zero, ite_smul, zero_smul, coe_sub', Pi.sub_apply,
    ContinuousLinearMap.add_apply, SchwartzMap.sub_apply, SchwartzMap.add_apply, smul_sub]
  ring

@[sorryful]
lemma angularMomentumSqr_commutation_angularMomentum {d : â„•} (i j : Fin d) :
    â…angularMomentumOperatorSqr (d := d), ð‹[i,j]â† = 0 := by
  unfold angularMomentumOperatorSqr
  conv_lhs =>
    rw [sum_lie]
    enter [2, k]
    rw [sum_lie]
    enter [2, l]
    simp only [smul_lie]
    rw [lie_leibniz_left]
    rw [angularMomentum_commutation_angularMomentum]
  ext Ïˆ x
  -- SchwartzMap.sum_apply
  sorry

end
end QuantumMechanics
