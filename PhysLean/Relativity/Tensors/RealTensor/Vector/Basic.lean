/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matteo Cipollina, Joseph Tooby-Smith
-/
import PhysLean.Relativity.Tensors.RealTensor.Metrics.Basic
import Mathlib.Geometry.Manifold.IsManifold.Basic
import PhysLean.Relativity.Tensors.Elab
/-!

# Lorentz Vectors

In this module we define Lorentz vectors as real Lorentz tensors with a single up index.
We create an API around Lorentz vectors to make working with them as easy as possible.

-/
open Module IndexNotation
open CategoryTheory
open MonoidalCategory
open Matrix
open MatrixGroups
open Complex
open TensorProduct
open IndexNotation
open CategoryTheory
noncomputable section

namespace Lorentz
open realLorentzTensor

/-- Real contravariant Lorentz vector. -/
def Vector (d : ‚Ñï := 3) := Fin 1 ‚äï Fin d ‚Üí ‚Ñù

namespace Vector

open TensorSpecies
open Tensor

/-!

## B. Defining the tensorial instance

-/

/-!

## B.1. The instance of a module

-/

instance {d} : AddCommMonoid (Vector d) :=
  inferInstanceAs (AddCommMonoid ((Fin 1 ‚äï Fin d)‚Üí ‚Ñù ))

instance {d} : Module ‚Ñù (Vector d) :=
  inferInstanceAs (Module ‚Ñù ((Fin 1 ‚äï Fin d)‚Üí ‚Ñù ))


/-!

## B.2. Equivalence of indexing sets

-/

/-- The equivalence between the type of indices of a Lorentz vector and
  `Fin 1 ‚äï Fin d`. -/
def indexEquiv {d : ‚Ñï} :
    ComponentIdx (S := (realLorentzTensor d)) ![Color.up] ‚âÉ Fin 1 ‚äï Fin d :=
  let e : (ComponentIdx (S := (realLorentzTensor d)) ![Color.up])
    ‚âÉ Fin (1 + d) := {
    toFun := fun f => Fin.cast (by rfl) (f 0)
    invFun := fun x => (fun j => Fin.cast (by simp) x)
    left_inv := fun f => by
      ext j
      fin_cases j
      simp
    right_inv := fun x => by rfl}
  e.trans finSumFinEquiv.symm

/-!

## B.3. The tensorial instance

-/

instance tensorial {d : ‚Ñï} : Tensorial (realLorentzTensor d) ![.up] (Vector d) where
  toTensor :=
    (LinearEquiv.piCongrLeft' ‚Ñù _ indexEquiv).symm.trans <|
    (Finsupp.linearEquivFunOnFinite ‚Ñù ‚Ñù
        (ComponentIdx (S := realLorentzTensor d) ![.up])).symm.trans <|
    (Tensor.basis (S := realLorentzTensor d) ![.up]).repr.symm

/-!

## B.4. Basic properties of toTensor

-/

open Tensorial

lemma toTensor_symm_apply {d : ‚Ñï} (p : ‚ÑùT[d, .up]) :
    (toTensor (self := tensorial)).symm p =
    (Equiv.piCongrLeft' _ indexEquiv <|
    Finsupp.equivFunOnFinite <|
    (Tensor.basis (S := (realLorentzTensor d)) _).repr p) := rfl

lemma toTensor_symm_pure {d : ‚Ñï} (p : Pure (realLorentzTensor d) ![.up]) (i : Fin 1 ‚äï Fin d) :
    (toTensor (self := tensorial)).symm p.toTensor i =
    ((Lorentz.contrBasisFin d).repr (p 0)) (indexEquiv.symm i 0) := by
  rw [toTensor_symm_apply]
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd,
    Equiv.piCongrLeft'_apply, Finsupp.equivFunOnFinite_apply, Fin.isValue]
  rw [Tensor.basis_repr_pure]
  simp only [Pure.component, Finset.univ_unique, Fin.default_eq_zero, Fin.isValue,
    Finset.prod_singleton, cons_val_zero]
  rfl

/-!

## C. Topology and smoothness

-/

/-!

### C.1. Induced instance of a normed space

-/

instance {d : ‚Ñï} : NormedSpace ‚Ñù (Vector d) := Tensorial.normedSpace

/-!

### C.2. Instance of a charted space

-/

/-- The instance of a `ChartedSpace` on `Vector d`. -/
instance {d} : ChartedSpace (Vector d) (Vector d) := chartedSpaceSelf (Vector d)

/-!

### C.3. Structure of a smooth manifold

-/

open Manifold in
/-- The structure of a smooth manifold on Vector . -/
def asSmoothManifold (d : ‚Ñï) : ModelWithCorners ‚Ñù (Vector d) (Vector d) := ùìò(‚Ñù, Vector d)

/-!

## D. As functions

-/

instance {d} : CoeFun (Vector d) (fun _ => Fin 1 ‚äï Fin d ‚Üí ‚Ñù) where
  coe := fun v => v

/-!

### D.1. Relation to EuclideanSpace

-/

lemma apply_eq_toEuclideanSpace {d : ‚Ñï} (v : Vector d) (i : Fin 1 ‚äï Fin d) :
    v i = (toEuclideanSpace v) (indexEquiv.symm i) := by
  simp [Tensorial.toEuclideanSpace, Tensor.toEuclideanSpace, toTensor]
  erw [LinearEquiv.piCongrLeft'_symm_apply]
  rw [Equiv.piCongrLeft'_symm_apply]
  simp

/-!

###¬†D.2. Linearlity of function application

-/

@[simp]
lemma apply_smul {d : ‚Ñï} (c : ‚Ñù) (v : Vector d) (i : Fin 1 ‚äï Fin d) :
    (c ‚Ä¢ v) i = c * v i := rfl

@[simp]
lemma apply_add {d : ‚Ñï} (v w : Vector d) (i : Fin 1 ‚äï Fin d) :
    (v + w) i = v i + w i := rfl

@[simp]
lemma apply_sub {d : ‚Ñï} (v w : Vector d) (i : Fin 1 ‚äï Fin d) :
    (v - w) i = v i - w i := by
  rw [apply_eq_toEuclideanSpace (v := v - w)]
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, map_sub, PiLp.sub_apply]
  simp [‚Üê apply_eq_toEuclideanSpace]

@[simp]
lemma neg_apply {d : ‚Ñï} (v : Vector d) (i : Fin 1 ‚äï Fin d) :
    (-v) i = - v i := by
  rw [apply_eq_toEuclideanSpace (v := -v)]
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, map_neg, PiLp.neg_apply]
  simp [‚Üê apply_eq_toEuclideanSpace]

@[simp]
lemma zero_apply {d : ‚Ñï} (i : Fin 1 ‚äï Fin d) :
    (0 : Vector d) i = 0 := rfl

/-!

## E. The Basis

We define the basis on `Vector d` indexed by `Fin 1 ‚äï Fin d`.
-/

/-- The basis on `Vector d` indexed by `Fin 1 ‚äï Fin d`. -/
def basis {d : ‚Ñï} : Basis (Fin 1 ‚äï Fin d) ‚Ñù (Vector d) :=
  Pi.basisFun ‚Ñù _

/-!

### E.1. Components of the basis vectors

-/

@[simp]
lemma basis_apply {d : ‚Ñï} (Œº ŒΩ : Fin 1 ‚äï Fin d) :
    basis Œº ŒΩ = if Œº = ŒΩ then 1 else 0 := by
  simp [basis]
  erw [Pi.basisFun_apply, Pi.single_apply]
  congr 1
  exact Lean.Grind.eq_congr' rfl rfl

/-!

### E.2. Components of a vector in the basis

-/

lemma basis_repr_apply {d : ‚Ñï} (p : Vector d) (Œº : Fin 1 ‚äï Fin d) :
    basis.repr p Œº = p Œº := by
  simp [basis]
  erw [Pi.basisFun_repr]

/-!

### E.3. Relation to tensorial basis

-/

lemma basis_eq_tensorial_basis {d : ‚Ñï} :
    basis = (Tensorial.basis (Vector d)).reindex indexEquiv := by
  ext Œº
  simp
  rw [Tensorial.basis]
  simp
  rw [Tensor.basis_apply]
  funext i
  rw [toTensor_symm_pure]
  simp [contrBasisFin, Pure.basisVector]
  conv_rhs =>
    enter [1, 2]
    change (contrBasisFin d) (indexEquiv.symm Œº 0)
  simp [contrBasisFin, indexEquiv, Finsupp.single_apply]

lemma tensorial_basis_eq_basis {d : ‚Ñï} :
    (Tensorial.basis (Vector d)) = basis.reindex indexEquiv.symm := by
  rw [basis_eq_tensorial_basis]
  ext Œº
  simp

/-!

### E.4. Relation to the tensor basis

-/

lemma toTensor_basis_eq_tensor_basis {d : ‚Ñï} (Œº : Fin 1 ‚äï Fin d) :
    toTensor (basis Œº) = Tensor.basis ![Color.up] (indexEquiv.symm Œº) := by
  rw [basis_eq_tensorial_basis]
  simp [Tensorial.toTensor_basis]

lemma toTensor_symm_tensor_basis_eq_basis {d : ‚Ñï}
    (Œº : ComponentIdx (S := realLorentzTensor d) ![Color.up]) :
    (toTensor (self := tensorial)).symm (Tensor.basis ![Color.up] Œº) =
    basis (indexEquiv Œº) := by
  apply toTensor.injective
  rw [toTensor_basis_eq_tensor_basis]
  simp


lemma tensor_basis_repr_toTensor_apply {d : ‚Ñï} (p : Vector d) (Œº : ComponentIdx ![Color.up]) :
    (Tensor.basis ![Color.up]).repr (toTensor p) Œº =
    p (indexEquiv Œº) := by
  obtain ‚ü®p, rfl‚ü© := toTensor.symm.surjective p
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, LinearEquiv.apply_symm_apply]
  apply induction_on_pure (t := p)
  ¬∑ intro p
    rw [Tensor.basis_repr_pure]
    simp only [Pure.component, Finset.univ_unique, Fin.default_eq_zero, Fin.isValue,
      Finset.prod_singleton, cons_val_zero, Nat.succ_eq_add_one, Nat.reduceAdd]
    rw [toTensor_symm_pure]
    simp
    rfl
  ¬∑ intro r t h
    simp [h]
  ¬∑ intro t1 t2 h1 h2
    simp [h1, h2]

/-!

### E.5. Maps in terms of the basis

-/

lemma map_apply_eq_basis_mulVec {d : ‚Ñï} (f : Vector d ‚Üí‚Çó[‚Ñù] Vector d) (p : Vector d) :
    (f p) = (LinearMap.toMatrix basis basis) f *·µ• p := by
  exact Eq.symm (LinearMap.toMatrix_mulVec_repr basis basis f p)

/-!

## F. The action of the Lorentz group

The action of the Lorentz group is given to us for free by the `Tensorial` instance.
-/

/-!

### F.1. Components of the action

-/

lemma smul_eq_sum {d : ‚Ñï} (i : Fin 1 ‚äï Fin d) (Œõ : LorentzGroup d) (p : Vector d) :
    (Œõ ‚Ä¢ p) i = ‚àë j, Œõ.1 i j * p j := by
  obtain ‚ü®p, rfl‚ü© := toTensor.symm.surjective p
  rw [smul_toTensor_symm]
  apply induction_on_pure (t := p)
  ¬∑ intro p
    rw [actionT_pure]
    rw [toTensor_symm_pure]
    conv_lhs =>
      enter [1, 2]
      change Œõ.1 *·µ• (p 0)
    rw [contrBasisFin_repr_apply]
    conv_lhs => simp only [Fin.isValue, Nat.succ_eq_add_one, Nat.reduceAdd, indexEquiv,
      cons_val_zero, Fin.cast_eq_self, Equiv.symm_trans_apply, Equiv.symm_symm,
      Equiv.coe_fn_symm_mk, Equiv.symm_apply_apply, ContrMod.mulVec_val]
    rw [mulVec_eq_sum]
    simp only [Finset.sum_apply]
    congr
    funext j
    simp only [Fin.isValue, Pi.smul_apply, transpose_apply, MulOpposite.smul_eq_mul_unop,
      MulOpposite.unop_op, Nat.succ_eq_add_one, Nat.reduceAdd, mul_eq_mul_left_iff]
    left
    rw [toTensor_symm_pure, contrBasisFin_repr_apply]
    congr
    simp [indexEquiv]
  ¬∑ intro r t h
    simp only [actionT_smul, _root_.map_smul]
    change r * toTensor (self := tensorial).symm (Œõ ‚Ä¢ t) i = _
    rw [h]
    rw [Finset.mul_sum]
    congr
    funext x
    simp only [Nat.succ_eq_add_one, Nat.reduceAdd, apply_smul]
    ring
  ¬∑ intro t1 t2 h1 h2
    simp only [actionT_add, map_add, h1, h2, apply_add]
    rw [‚Üê Finset.sum_add_distrib]
    congr
    funext x
    ring

/-!

### F.2. As a matrix multiplication

-/

lemma smul_eq_mulVec {d} (Œõ : LorentzGroup d) (p : Vector d) :
    Œõ ‚Ä¢ p = Œõ.1 *·µ• p := by
  funext i
  rw [smul_eq_sum, mulVec_eq_sum]
  simp only [op_smul_eq_smul, Finset.sum_apply, Pi.smul_apply, transpose_apply, smul_eq_mul,
    mul_comm]

/-!

### F.3. Applying the negative of a Lorentz transformation

-/

lemma neg_smul {d} (Œõ : LorentzGroup d) (p : Vector d) :
    (-Œõ) ‚Ä¢ p = - (Œõ ‚Ä¢ p) := by
  funext i
  rw [smul_eq_sum, neg_apply, smul_eq_sum]
  simp

/-!

### F.4. The action is faithful

-/

lemma _root_.LorentzGroup.eq_of_action_vector_eq {d : ‚Ñï}
    {Œõ Œõ' : LorentzGroup d} (h : ‚àÄ p : Vector d, Œõ ‚Ä¢ p = Œõ' ‚Ä¢ p) :
    Œõ = Œõ' := by
  apply LorentzGroup.eq_of_mulVec_eq
  simpa only [smul_eq_mulVec] using fun x => h x

/-!

### F.5. The action on the basis

-/

lemma smul_basis {d : ‚Ñï} (Œõ : LorentzGroup d) (Œº : Fin 1 ‚äï Fin d) :
    Œõ ‚Ä¢ basis Œº = ‚àë ŒΩ, Œõ.1 ŒΩ Œº ‚Ä¢ basis ŒΩ := by
  funext i
  rw [smul_eq_sum]
  simp only [basis_apply, mul_ite, mul_one, mul_zero, Finset.sum_ite_eq, Finset.mem_univ,
    ‚ÜìreduceIte]
  trans ‚àë ŒΩ, ((Œõ.1 ŒΩ Œº ‚Ä¢ basis ŒΩ) i)
  ¬∑ simp
  rw [Fintype.sum_apply]

/-!

## G. The spatial and time parts

-/

/-!

### G.1. The spatial part

-/

/-- Extract spatial components from a Lorentz vector,
    returning them as a vector in Euclidean space. -/
abbrev spatialPart {d : ‚Ñï} (v : Vector d) : EuclideanSpace ‚Ñù (Fin d) :=
  fun i => v (Sum.inr i)

lemma spatialPart_apply_eq_toCoord {d : ‚Ñï} (v : Vector d) (i : Fin d) :
    spatialPart v i = v (Sum.inr i) := rfl

lemma spatialPart_basis_sum_inr {d : ‚Ñï} (i : Fin d) (j : Fin d) :
    spatialPart (basis (Sum.inr i)) j =
      (Finsupp.single (Sum.inr i : Fin 1 ‚äï Fin d) 1) (Sum.inr j) := by
  simp [spatialPart, basis_apply]
  rw [Finsupp.single_apply]
  simp

lemma spatialPart_basis_sum_inl {d : ‚Ñï} (i : Fin d) :
    spatialPart (basis (Sum.inl 0)) i = 0 := by simp

/-!

### G.2. The time part

-/

/-- Extract time component from a Lorentz vector -/
abbrev timeComponent {d : ‚Ñï} (v : Vector d) : ‚Ñù :=
  v (Sum.inl 0)

lemma timeComponent_basis_sum_inr {d : ‚Ñï} (i : Fin d) :
    timeComponent (basis (Sum.inr i)) = 0 := by simp

lemma timeComponent_basis_sum_inl {d : ‚Ñï} :
    timeComponent (d := d) (basis (Sum.inl 0)) = 1 := by simp

end Vector

end Lorentz
